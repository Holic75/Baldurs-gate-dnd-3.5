

BACKUP ~3ed/backup~
AUTHOR ~denis.holic@gmail.com~
VERSION ~v1.01~


LANGUAGE
	~English~ ~3ed/tra~ ~3ed/tra/english.tra~

/////////////////////////////////////////////////
//3ED-fication
BEGIN @0
	INCLUDE ~3ed/ADD_SPELL_HEADER.tph~
	INCLUDE ~3ed/ADD_BONUS_FEATS.tph~
	INCLUDE ~3ed/ADD_BONUS_SPELLS_PROCESS.tph~
	INCLUDE ~3ed/ADD_BONUS_SPELLS.tph~

	
	//common part
	COPY ~3ed/Core/Clab~ ~override~ 
	COPY ~3ed/Core/ClassRequirements~ ~override~ 
	STRING_SET 9556  @001 //true class fighter 
	STRING_SET 9572  @002 //fighter/thief 
	STRING_SET 9573  @003 //fighter/cleric
	STRING_SET 9574  @004 //fighter/mage
	STRING_SET 9576  @005 //fighter/mage/thief
	STRING_SET 9579  @006 //fighter/druid
	STRING_SET 9581  @007 //fighter/mage/cleric
	STRING_SET 24231 @007 //fighter/mage/cleric
	STRING_SET 24284 @008 //berserker
	STRING_SET 24285 @009 //wizardslayer
	STRING_SET 25361 @009 //wizardslayer
	STRING_SET 24286 @010 //kensai
	STRING_SET 25362 @010 //kensai
	STRING_SET 24235 @011 //barbarian
	STRING_SET 25390 @011 //barbarian
	STRING_SET 31975 @012 //dwarven defender
	STRING_SET 9558  @013 //paladin
	STRING_SET 24287 @014 //cavalier 
	STRING_SET 25363 @014 //cavalier 
	STRING_SET 24288 @015 //inquisitor
	STRING_SET 25364 @015 //inquisitor
	STRING_SET 24289 @016 //undead hunter
	STRING_SET 25365 @016 //undead hunter
	STRING_SET 28606 @017 //blackguard
	STRING_SET 9557  @018 //Ranger
	STRING_SET 9580  @019 //cleric/ranger
	STRING_SET 24298 @020 //archer
	STRING_SET 25366 @020 //archer
	STRING_SET 24300 @021 //beastmaster
	STRING_SET 25368 @021 //beastmaster
	STRING_SET 24299 @022 //stalker
	STRING_SET 25367 @022 //stalker
	STRING_SET 9559  @023 //cleric
	STRING_SET 24230 @023 //cleric
	STRING_SET 9577  @024 //cleric/mage
	STRING_SET 9578  @025 //cleric/thief
	
	STRING_SET 24309  @026 //priest of lathander
	STRING_SET 25380  @026    //priest of lathander
	STRING_SET 24308  @027    //priest of helm
	STRING_SET 25379  @027    //priest of helm
	STRING_SET 24307  @028    //priest of talos
	STRING_SET 25378  @028   //priest of talos	
	STRING_SET 32346  @029    //priest of tyr
	STRING_SET 9560  @030 //druid
	STRING_SET 24312  @031 //avenger
	STRING_SET 25377  @031 //avenger
	STRING_SET 24311  @032 //shapeshifter
	STRING_SET 25376  @032 //shapeshifter
	STRING_SET 24310 @033     //totemic druid
	STRING_SET 25375 @033     //totemic druid
		STRING_SET 24257 @0330001     //totemic druid (low)
		STRING_SET 25313 @0330001     //totemic druid (low)
		STRING_SET 24279 @0330002     //totemic druid (mixed)
		STRING_SET 25344 @0330002     //totemic druid (mixed)
	STRING_SET 9563  @034 //mage
	STRING_SET 17245 @034 //mage
	STRING_SET 10940 @035 //mage (dual)
	STRING_SET 9575  @036 //mage/thief
	// no need to change specialist mages/ wild mages
	STRING_SET 9561  @037 //thief
	STRING_SET 10941 @038 //thief (dual)
	STRING_SET 24301 @039 //assassin
	STRING_SET 25369 @039 //assassin
	STRING_SET 24303 @040 //swashbuckler
	STRING_SET 25371 @040 //swashbuckler
	STRING_SET 24302 @041 //bounty hunter
	STRING_SET 25370 @041 //bounty hunter
		STRING_SET 17124 @0410001 //bounty hunter(low)
		STRING_SET 24249 @0410001 //bounty hunter(low)
		STRING_SET 25308 @0410001 //bounty hunter(low)
		STRING_SET 17125 @0410002 //bounty hunter(mixed)
		STRING_SET 24271 @0410002 //bounty hunter(mixed)
		STRING_SET 25339 @0410002 //bounty hunter(mixed)
	STRING_SET 31972 @042 //shadowdancer
	STRING_SET 9562  @043 //bard
	STRING_SET 24304 @044 //blade
	STRING_SET 25372 @044 //blade
	STRING_SET 24306 @045 //skald
	STRING_SET 25374 @045 //skald
	STRING_SET 24305 @046 //jester
	STRING_SET 25373 @046 //jester

	STRING_SET 24233 @047 //sorcerer
	STRING_SET 31978 @048 //dragon disciple
		STRING_SET 31976 @0480001 //dragon disciple (low)
		STRING_SET 31977 @0480002 //dragon disciple (mixed)
	STRING_SET 24234 @049 //monk
	STRING_SET 31981 @050 //DARK MOON MONK
	STRING_SET 31984 @051 //SUN SOUL MONK
	
	STRING_SET 32341 @052 //shaman
	////STATS/////////////////////
	STRING_SET 9587  @053//Charisma
	STRING_SET 9585  @054//Intelligence
	STRING_SET 9586  @055//Wisdom
	STRING_SET 9584  @056//Dexterity
	STRING_SET 9583  @057//Constitution
	STRING_SET 9582  @058//Strength
	//WEAPONS
	STRING_SET 24999 @059//2weapon style
	STRING_SET 25044 @060//2h weapon
	STRING_SET 25045 @061//sword and shield
	STRING_SET 25046 @062//single weapon
	STRING_SET 24219 @063//Weapon focus
	STRING_SET 24220 @064//Weapon specialization
	STRING_SET 24221 @065//Improved weapon focus
	STRING_SET 24222 @066//Improved weapon specialization
	STRING_SET 24223 @067//weapon mastery
	//Races
	STRING_SET 9550  @068//Humans
	STRING_SET 9552  @069//Elves
	STRING_SET 9551  @070//Dwarves
	STRING_SET 9553  @071//Gnomes
	STRING_SET 9554  @072//Halflings
	STRING_SET 24204 @073//Half-orcs
	STRING_SET 9555  @088//Half elves

	//correcting caster levels for paladins and rangers
	COPY ~3ed/Core/CasterLevelRegularisation/NegativeLevel~ ~override~	
	OUTER_FOR (i= 0;i<=15;i=i+1) BEGIN
		COPY ~3ed/Core/CasterLevelRegularisation/CLVLBN.SPL~ ~override/CLBN%i%.SPL~			
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=191 parameter1=i END
			SPRINT resource EVALUATE_BUFFER ~CLBN%i%~
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=318 STR_VAR resource END
	END
	
	COPY ~3ed/Core/Proficiencies~ ~override~ 
	COPY ~3ed/Core/Savingthrows~ ~override~
	//adding saves based on character abilities
    COPY_EXISTING_REGEXP GLOB ~\(CLAB+.\)\|\(OHTYR\)*\.2DA~ ~override~
		COUNT_2DA_ROWS 20 "nrows"
		INSERT_2DA_ROW nrows 20
			~SAVCONAPI AP_DEXSAVFT **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~
	 
		INSERT_2DA_ROW nrows+1 20
			~SAVDEXABI AP_CONSAVFT **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~
	 
		INSERT_2DA_ROW nrows+2 20
			~SAVWISABI AP_WISSAVFT **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~
		
		
	COPY ~3ed/Core/SpellsPerLvl~ ~override~
	COPY ~3ed/Core/Stats~ ~override~
	
	LAF ADD_BONUS_SPELLS_PROCESS STR_VAR folder_name=~3ed/Core/BonusSpells/~ ability_name=~SPLPR~ END
	LAF ADD_BONUS_SPELLS_PROCESS STR_VAR folder_name=~3ed/Core/BonusSpells/~ ability_name=~SPLWI~ END
	
	LAF ADD_BONUS_SPELLS INT_VAR stat_greater_id=130 stat_less_id=131 STR_VAR folder_name=~3ed/Core/BonusSpells/~ ability_name=~SPLPR~ game_name=~SPLWIP~ END //priest spells wis
	LAF ADD_BONUS_SPELLS INT_VAR stat_greater_id=128 stat_less_id=129 STR_VAR folder_name=~3ed/Core/BonusSpells/~ ability_name=~SPLWI~ game_name=~SPLINW~ END //wizard spells int
	LAF ADD_BONUS_SPELLS INT_VAR stat_greater_id=132 stat_less_id=133 STR_VAR folder_name=~3ed/Core/BonusSpells/~ ability_name=~SPLPR~ game_name=~SPLCHP~ END //priest spells cha
	LAF ADD_BONUS_SPELLS INT_VAR stat_greater_id=132 stat_less_id=133 STR_VAR folder_name=~3ed/Core/BonusSpells/~ ability_name=~SPLWI~ game_name=~SPLCHW~ END //wizard spells cha
	
	COPY ~3ed/Core/BonusSpells/SPLBON1.SPL~ ~override/SPLMAGMD.SPL~ //bonus wizard spells with mask
		LPF ADD_SPELL_EFFECT INT_VAR opcode=326 target = 2 duration=1 parameter1=19 parameter2 =115 STR_VAR resource=~SPLINWMD~ END//for non sorcerors
		LPF ADD_SPELL_EFFECT INT_VAR opcode=326 target = 2 duration=1 parameter1=19 parameter2 =105 STR_VAR resource=~SPLCHWMD~ END//for sorcerers
		
	
	
	COPY ~3ed/Core/ThiefSkill~ ~override~
	COPY ~3ed/Core/Xp~ ~override~
	//--------------------------------------------------------------------------------------------
	//paladin update (Lay On Hands and Smite evil, saves, + bonus for undead hunter)
	//COPY ~3ed/Classes/Paladin/TULVLUH.SPL~ ~override~
	COPY ~3ed/Classes/Paladin/PDNSVCR.SPL~ ~override~//correction for paladin saves
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=40 d_level=1 add_at_level1=1 STR_VAR clab=~CLABPA+.*\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~PDNSVCR~ END	
	COPY ~3ed/Classes/Paladin/LayOnHands~ ~override~
		EXTEND_TOP ~baldur.bcs~ ~3ed/Classes/Paladin/PALADIN.bcs~	//script to update number of lay on hands uses depending on charisma
	
	COPY ~3ed/Classes/Paladin/SmiteEvil/SMTEVL1B.BAM~ ~override~
	COPY ~3ed/Classes/Paladin/SmiteEvil/SMTEVL1C.BAM~ ~override~
	COPY ~3ed/Classes/Paladin/SmiteEvil/SMTTH.SPL~ ~override~//cha bonus to hit
	COPY ~3ed/Classes/Paladin/SmiteEvil/SMTEVL.SPL~ ~override~//main spell smite evil
		SAY NAME1 @074
		SAY UNIDENTIFIED_DESC @075
		WRITE_ASCII 0x003a ~SMTEVL1C~ #8 //spell icon
		LPF ALTER_SPELL_HEADER STR_VAR icon=~SMTEVL1B~ END //spell header icon
		FOR (i=12;i<=25;i=i+2)	BEGIN //adding charisma dependent to hit bonus 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=326 insert_point=0 target=1 parameter1=i parameter2=132 duration=1 STR_VAR resource=~SMTTH~ END
		END
			
	OUTER_FOR (i=1;i<=20;i=i+1) BEGIN
		COPY ~3ed/Classes/Paladin/SmiteEvil/SMTDM.SPL~ ~override/SMTDM%i%.SPL~//dmg
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=12  parameter1=i  END	
	END
	
	COPY ~3ed/Classes/Paladin/SmiteEvil/SMTEVLH.EFF~ ~override~//hit effect
	COPY ~3ed/Classes/Paladin/SmiteEvil/SMTEVLH.SPL~ ~override~//hit effect (spell)
		FOR (i=1;i<=20;i=i+1) BEGIN
			LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=1 target_count=0 range=1 required_level=i speed=0 projectile=0  END
			SPRINT resource EVALUATE_BUFFER ~SMTDM%i%~
			LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter2=37 duration=1 STR_VAR resource END //dmg aginst evil creatures
		END
	
	//smite undead
	COPY_EXISTING ~SMTEVL.SPL~ ~override/SMTUND.SPL~//main spell smite undead
		SAY NAME1 @0740001
		SAY UNIDENTIFIED_DESC @0750001
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~SMTUNDH~ END
	
	COPY_EXISTING ~SMTEVLH.EFF~ ~override/SMTUNDH.EFF~//hit effect
	COPY_EXISTING ~SMTEVLH.SPL~ ~override/SMTUNDH.SPL~//hit effect (spell)
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=326 parameter2=1 END //change evil to undead
		
	//-----------------------------------------------------------		
	//paladin cha_saves
	COPY_EXISTING_REGEXP GLOB ~CLABPA+.*\.2DA~ ~override~
		COUNT_2DA_ROWS 20 "nrows"
		INSERT_2DA_ROW nrows 20
			~CHASAVABI AP_CHASAVFT **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~
	
	//----------------------------------------------------------//	
	//wizardslayer mr bonus (25%+2/level till level 30)
	COPY ~3ed/Classes/Wizardslayer~ ~override~
	//----------------------------------------------------------//	
	//monk (ac bonus, wis ac bonus, flurry of blows, fist damage/ enchantment)
	COPY ~3ed/Classes/Monk~ ~override~
    COPY_EXISTING_REGEXP GLOB ~CLABMO+.*\.2DA~ ~override~
		COUNT_2DA_ROWS 20 "nrows"
		INSERT_2DA_ROW nrows 20
			~MNKFLRABI AP_MKFLR1FT **** **** **** AP_MKFLR2FT **** **** **** AP_MKFLR3FT **** AP_MKFLR4FT **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~
		INSERT_2DA_ROW nrows 20
			~WISDACABI AP_WISDACFT **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~				
		
	
	COPY_EXISTING  ~MFIST6.ITM~ ~override~
		SET  fist_name=RESOLVE_STR_REF (@091)
		WRITE_LONG 0x0008 fist_name
		WRITE_LONG 0x000c fist_name
	//----------------------------------------------------------//	
	//ninja (bounty hunter->ninja)
	COPY ~3ed/Classes/Ninja~ ~override~
		
	COPY_EXISTING ~KIPWR.SPL~ ~override~
			SAY NAME1 @100116
			SAY UNIDENTIFIED_DESC @100117
			
			
    COPY_EXISTING_REGEXP GLOB ~CLABTH03\.2DA~ ~override~
		COUNT_2DA_ROWS 20 "nrows"
		INSERT_2DA_ROW nrows 20
			~WISDACABI AP_WISDACFT **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~				
	
	
	OUTER_FOR (player_id=1;player_id<=6;player_id=player_id + 1) BEGIN
		EXTEND_TOP ~BALDUR.BCS~ ~3ed/Classes/Ninja/NINJA.baf~
			EVALUATE_BUFFER			
	END
	
	
/* 	//Priest of talos saving throw penalties for lightning bolt
	COPY ~3ed/Classes/PriestOfTalos~ ~override~ */
	
	//shapeshifts that can use items
	COPY ~3ed/Classes/Druid~ ~override~
	COPY_EXISTING ~DRDPOLY.SPL~ ~override~ //standard shapeshift
		SAY NAME1 @092
		SAY UNIDENTIFIED_DESC @093
		
	COPY_EXISTING ~PL_WRWF.SPL~ ~override~ //werewolf
		SAY NAME1 @094
		SAY UNIDENTIFIED_DESC @095
	
	//----------------------------------------------------------//
	//lingering song, + improved song bonuses, + rage for skald
	//COPY ~3ed/Classes/Bard~ ~override~
	//----------------------------------------------------------//
	//assasin poison weapon progression change
	COPY ~3ed/Classes/Assassin/bdpweapn.spl~ ~override~

	//update poison weapon description
	STRING_SET 32236 @076 
	//shadowdancer special abilities (mislead)
	COPY ~3ed/Classes/Shadowdancer~ ~override~
	//--------------------------------------------------------//
	//traps at lvls 1,10,15,20
	COPY ~3ed/Classes/ThiefSetTraps~ ~override~
	STRING_SET 32220 @077 // set snare
	STRING_SET 32221 @078 // set special snare
	//--------------------------------------------------//
	//barbarian rage update (tireless, mighty) 
	COPY ~3ed/Classes/Barbarian/Rage/BRBFTG.spl~ ~override~  //after rage fatigue
	COPY ~3ed/Classes/Barbarian/Rage/BRBRGE.spl~ ~override~  //rage itself
	
	//change duration
		LPF ALTER_SPELL_EFFECT INT_VAR duration_high=60 END 
	//add fatigue for low level rage
		LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 power=0 parameter1=1 parameter2=1 timing=4 resist_dispel=2 duration=60 probability1=100 header=1 STR_VAR resource=~BRBFTG~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=206 target=1 power=0 parameter1=26799 parameter2=0 timing=0 duration=59 probability1=100 STR_VAR resource=~BRBRGE~ END
	STRING_SET 32234 @079
	//----------------------------------------------------------//
	//Skald rage
	COPY ~3ed/Classes/Skald/SKLDFTG.spl~ ~override~  //after rage fatigue
	COPY ~3ed/Classes/Skald/SKLDRGE.spl~ ~override~  //rage itself
	
	//change duration
		LPF ALTER_SPELL_EFFECT INT_VAR duration_high=60 END 
	//add fatigue for low level rage
		LPF ADD_SPELL_EFFECT INT_VAR opcode=146 target=1 power=0 parameter1=1 parameter2=1 timing=4 resist_dispel=2 duration=60 probability1=100 header=1 STR_VAR resource=~SKLDFTG~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=206 target=1 power=0 parameter1=26799 parameter2=0 timing=0 duration=59 probability1=100 STR_VAR resource=~SKLDRGE~ END //protection from another rage application

	
	//----------------------------------------------------------//
	//swashbuckler luck of heroes and critical hit effects
	COPY ~3ed/Classes/Swashbuckler~ ~override~
	//update display strings
	COPY_EXISTING ~CRITSTR.SPL~ ~override~
		SET  effect_strref=RESOLVE_STR_REF (@100008)
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=139 parameter1=effect_strref END
	COPY_EXISTING ~CRITCON.SPL~ ~override~
		SET  effect_strref=RESOLVE_STR_REF (@100009)
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=139 parameter1=effect_strref END
	
	//-----------------------------------------------//
	COPY ~3ed/Classes/Ranger~ ~override~ //remove twf bonus
	//-----------------------------------------------//
	//archer update (improved called shot, create arrows)
	COPY ~3ed/Classes/Archer~ ~override~
	COPY_EXISTING ~ARCHARW.SPL~ ~override~
	  SAY NAME1 @080
	  SAY UNIDENTIFIED_DESC @081
	  
	 STRING_SET 32231 @082  //called shot
	 //----------------------------------------------//
	 //shaman dance update (duration +1 additional round at lvl1, +2 at lvl 6 +3 at lvel 12 +4 at lvl 18)
	 COPY ~3ed/Classes/Shaman/spsh003.SPL~ ~override~
	 COPY ~3ed/Classes/Shaman/spsh004.SPL~ ~override~
	 COPY ~3ed/Classes/Shaman/SHMDNC.SPL~ ~override~
	 LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete=363 END //remove movement rate checking
	 FOR (i=1;i<=20;i=i+1) BEGIN
		//change protection from spell resource
		LPF ALTER_SPELL_EFFECT INT_VAR header=i match_opcode=318 STR_VAR resource=~SHMDNC~ END
		//change duration based on level
		PATCH_IF (i<6) BEGIN
			LPF ALTER_SPELL_EFFECT INT_VAR header=i match_opcode=328 duration =13 END
		END ELSE PATCH_IF (i<12) BEGIN
			LPF ALTER_SPELL_EFFECT INT_VAR header=i match_opcode=328 duration =19 END
		END ELSE PATCH_IF (i<18) BEGIN
			LPF ALTER_SPELL_EFFECT INT_VAR header=i match_opcode=328 duration =25 END
		END ELSE BEGIN
			LPF ALTER_SPELL_EFFECT INT_VAR header=i match_opcode=328 duration =31 END
		END
	 END
	 //--------------------------------------------//
	 //Berserker update - berserker rage, last berserk
	 COPY ~3ed/Classes/Berserker/SPCL321D.SPL~ ~override~
	 COPY ~3ed/Classes/Berserker/SPCL321.SPL~ ~override~
		LPF ALTER_SPELL_EFFECT INT_VAR duration_high=36 END //duration to 6 rounds
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=12 duration=37 END //damage 1 second after
		LPF ADD_SPELL_EFFECT INT_VAR opcode=206 target=1 dispel_resist=2 duration=34 STR_VAR resource=~SPCL321D~ END //protection from fatigue (to be used starting from lvl 8)
		STRING_SET 32235 @083 //change rage description
	 COPY ~3ed/Classes/Berserker/LSTBRSK.BAM~ ~override~
	 COPY ~3ed/Classes/Berserker/LSTBRSK.SPL~ ~override~
			  SAY NAME1 @084
			  SAY UNIDENTIFIED_DESC @085
			  LPF ALTER_SPELL_EFFECT INT_VAR duration_high=36 END //duration to 6 rounds
			  LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=12 duration=37 END //damage 1 second after
			  LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=206 duration=2400 END //restore duration of inability to rage
	 //-------------------------------------------//
	 //jester update  - song, posion weapon, sneak attack, charm/confusion immunity, cha saves
	  COPY ~3ed/Classes/Jester/JSTRIMM.SPL~ ~override~ //immunities
	  //COPY ~3ed/Classes/Jester/SPCL751A.SPL~ ~override~ // song
		SET protection_str_ref=RESOLVE_STR_REF (@86)
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=206 parameter1=protection_str_ref END
		
	 //--------------------------------------------//
	 //update proficiencies description
	 STRING_SET 24590 @089
	 STRING_SET 24315 @090
	 STRING_SET 9588  @090
	 //----------------------------------------------//
	 
	 //----------------------------------------------//
	 STRING_SET 32329 @093  //greater werewolf description
	 STRING_SET 32245 @095 //wyvern shapeshift description
	 STRING_SET 32246 @094 //fire salamander description
	 //update moonblade to become longsword, seeking sword apr, // gauntlets of dextirity and potion of agility to give +2 to dex instead of setting it to 18
	 //also copy magical weapons with corrected proficiency or category field
	 COPY ~3ed/Items~ ~override~
	 STRING_SET 32249 @096 //seeking sword
	 STRING_SET 10303 @097 //moonblade descritption
	 STRING_SET 9990  @100007
	// STRING_SET 7371  @100011
	// STRING_SET 7383  @098 //gauntlets of dexterity
	// STRING_SET 17859 @099 //potion of agility
		
	
	//update sneak attack description
	STRING_SET 3033 @087 //sneak attack description
	STRING_SET 3034 @087 //sneak attack description
	STRING_SET 3042 @087 //sneak attack description
		
	//allow everyone to use basic robes
	//change descriptions
	STRING_SET 10046 @100001 //general robe description
	
	ACTION_DEFINE_ASSOCIATIVE_ARRAY RobesForEveryone BEGIN
		~CLCK09~ =>10031
		~CLCK10~ =>10032
		~CLCK11~ =>10033
		~CLCK12~ =>10034
		~CLCK13~ =>10035
		~CLCK14~ =>10036	
	END
	
	OUTER_SET StrLabel=100000
	ACTION_PHP_EACH RobesForEveryone AS RobeName => DescrStrRef BEGIN
		COPY_EXISTING ~%RobeName%.ITM~ ~override~
			WRITE_LONG 0x001e 0  //allow all calsses to use robe
			WRITE_BYTE 0x0029 0  //allow barbarians to use robe
		
		OUTER_SET StrLabel=StrLabel+1
		OUTER_SPRINT desc_string (AT "StrLabel")
		STRING_SET_EVALUATE %DescrStrRef% ~%desc_string%~			
	END
	
	
	//remove stat requirements from specialist mages description
	OUTER_SET specialist_start=24290
	OUTER_SPRINT regexp_to_remove @100010

	OUTER_FOR (i=0;i<8;i=i+1) BEGIN
		OUTER_SET str_to_read=specialist_start + i
		ACTION_GET_STRREF str_to_read spec_descr
		OUTER_PATCH_SAVE new_spec_desc ~%spec_descr%~ BEGIN
			REPLACE_TEXTUALLY ~%regexp_to_remove%~ ~~
		END	
		STRING_SET_EVALUATE %str_to_read% ~%new_spec_desc%~	
	END
	
	
	
	//turn undead -> channel energy
	INCLUDE ~3ed/ADD_CHANNEL_ENERGY.tph~
	//for paladins, clerics, cleric/thiefs, fighter/clerics
	STRING_SET 12126 @100101
	
	
	COPY ~3ed/Classes/TurnUndead/CHANNEGB.BAM~ ~override~
	COPY ~3ed/Classes/TurnUndead/CHANNEGC.BAM~ ~override~
	COPY ~3ed/Classes/TurnUndead/CHANPOSB.BAM~ ~override~
	COPY ~3ed/Classes/TurnUndead/CHANPOSC.BAM~ ~override~
	
	OUTER_SET max_channel_lvl=10
	OUTER_SET max_multi_class_channel_lvl=5	
	
	OUTER_FOR (i=1;i<=max_channel_lvl;i=i+1) BEGIN
		COPY ~3ed/Classes/TurnUndead/DMUNDD.EFF~ ~override/EN_DMG%i%.EFF~
			WRITE_LONG 0x0038 i //i*1d4 damage (same part will be also added with save)
		COPY ~3ed/Classes/TurnUndead/HEAL.EFF~ ~override/EN_HL%i%.EFF~
			WRITE_LONG 0x0038 2*i //i*2d4 healed 
	END
	
	
	COPY ~3ed/Classes/TurnUndead/CHANEN0.SPL~ ~override/CE_0.SPL~ //removal of channel energy during rest
	OUTER_SET  pos_name_ref=RESOLVE_STR_REF (@100111)
	OUTER_SET  pos_descr_ref=RESOLVE_STR_REF (@100112)
	OUTER_SET  neg_name_ref=RESOLVE_STR_REF (@100113)
	OUTER_SET  neg_descr_ref=RESOLVE_STR_REF (@100114)
	
	//channel energy for clerics and paladins
 	LAF ADD_CHANNEL_ENERGY INT_VAR level_offset=1 divisor=2 pos_name_ref pos_descr_ref neg_name_ref neg_descr_ref STR_VAR id=~C~ END
	//channel energy for cleric/thief and fighter/cleric
 	LAF ADD_CHANNEL_ENERGY INT_VAR level_offset=2 divisor=4 pos_name_ref pos_descr_ref neg_name_ref neg_descr_ref STR_VAR id=~M~ END
	//give channel energy depending on kit and class
	COPY ~3ed/Classes/TurnUndead/CHANEN1.SPL~ ~override/CE_1.SPL~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=3 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_PC1~ END //good cleric
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=3 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_NC1~  END //evil cleric 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=15 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_PM1~   END //good cleric theif
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=15 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_NM1~  END //evil cleric thief
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=8 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_PM1~ END //good fighter cleric
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=8 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_NM1~  END //evil fighter cleric
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=14 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_PM1~ END //good cleric/mage
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=14 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_NM1~  END //evil cleric/mage
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=6 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_PC1~  END //good paladin
		LPF ADD_SPELL_EFFECT INT_VAR opcode=177 target=2 parameter1=6 parameter2=5 duration=1 timing=1 STR_VAR  resource= ~CE_NC1~  END //evil paladin
		
	COPY ~3ed/Classes/TurnUndead/CHANGV.SPL~ ~override/CE_GV.SPL~
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~CE_1~ END
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~CE_PC1~ END //2 additional uses for paladin undead hunter
		
	//first attribution of abilities
	COPY ~3ed/Feats/FeatAttribution/SFTCREC.SPL~ ~override/CE_C1.SPL~
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~CE_GV~ END//cleric
	COPY ~3ed/Feats/FeatAttribution/SFTCREFC.SPL~ ~override/CE_FC1.SPL~
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~CE_GV~ END //fighter cleric
	COPY ~3ed/Feats/FeatAttribution/SFTCRECT.SPL~ ~override/CE_CT1.SPL~
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~CE_GV~ END //cleric thief
	COPY ~3ed/Feats/FeatAttribution/SFTCRECM.SPL~ ~override/CE_CM1.SPL~
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~CE_GV~ END //cleric thief
	//COPY ~3ed/Feats/FeatAttribution/SFTCREP.SPL~ ~override/CE_P1.SPL~
	//	LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~CE_1~ END//paladin
		
	//script for giving channel abilities	
	OUTER_FOR (player_id=1;player_id<=6;player_id=player_id + 1) BEGIN
		EXTEND_TOP ~BALDUR.BCS~ ~3ed/Classes/TurnUndead/CHANEN.baf~
			EVALUATE_BUFFER			
	END
	
	//turn undead level regularisation
	OUTER_SET max_channel_lvl=10
	OUTER_SET max_multi_class_channel_lvl=5
	OUTER_SET max_lvl=40
	OUTER_FOR (i=0;i<=max_lvl;i=i+1) BEGIN
		COPY ~3ed/Classes/TurnUndead/TULVL1.SPL~ ~override/CE_LVL%i%.SPL~
		SET bonus=1 - i
		WRITE_LONG 0x009e bonus
	END
	
	COPY ~3ed/Classes/TurnUndead/CE_LVL.SPL~ ~override~
		SPRINT clab_line ~CE_LVL_SET~
		FOR (i=1;i<=max_lvl;i=i+1) BEGIN
			LPF ADD_SPELL_HEADER INT_VAR type=1 location=4 target=5 target_count=0 range=1 required_level=i speed=1 END
		
			SET cleric_level=i -  (i+1)/2 + 1
		
			SET paladin_level = i -  (i+1)/2 - 1
			PATCH_IF (i<3) BEGIN
				SET paladin_level=1
			END
            
			SET multi_class_level = i - (i+2)/4 + 1
			PATCH_IF (multi_class_level>max_multi_class_channel_lvl) BEGIN
				SET multi_class_level=max_multi_class_channel_lvl
			END
			
			SPRINT resource EVALUATE_BUFFER ~CE_LVL%cleric_level%~
			LPF ADD_SPELL_EFFECT INT_VAR header=i  opcode=326 target=2 parameter1=3 parameter2=105 duration=1 timing=1 STR_VAR  resource END //cleric 
			
			SPRINT resource EVALUATE_BUFFER ~CE_LVL%paladin_level%~
			LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=6 parameter2=105 duration=1 timing=1 STR_VAR  resource END //paladin
			
			SPRINT resource EVALUATE_BUFFER ~CE_LVL%multi_class_level%~
			LPF ADD_SPELL_EFFECT INT_VAR header=i  opcode=326 target=2 parameter1=8 parameter2=105 duration=1 timing=1 STR_VAR  resource END //fighter cleric 
			LPF ADD_SPELL_EFFECT INT_VAR header=i  opcode=326 target=2 parameter1=15 parameter2=105 duration=1 timing=1 STR_VAR  resource END //cleric thief
			LPF ADD_SPELL_EFFECT INT_VAR header=i  opcode=326 target=2 parameter1=14 parameter2=105 duration=1 timing=1 STR_VAR  resource END //cleric mage
			
			SPRINT clab_line EVALUATE_BUFFER ~%clab_line%   AP_CE_LVL~
		END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=144 target=2 parameter1=0 parameter2=6 timing=1 duration=1 END //disable turn undead button
	//creating  channel levels string in clabs
COPY_EXISTING_REGEXP GLOB ~\(CLABPA01\)\|\(CLABPA02\)\|\(CLABPA04\)\|\(CLABPA06\)\|\(CLABPR0+.*\)\|\(OHTYR\)\.2DA~ ~override~
		COUNT_2DA_ROWS 20 "nrows"	
		INSERT_2DA_ROW nrows 20 ~%clab_line%~
		
		
	COPY ~3ed/Core/THAC0/THAC0H.SPL~ ~override~ //high thaco for swashbucklers
	//thaco regularization
	OUTER_FOR (i=0;i<=20;i=i+1) BEGIN
		COPY ~3ed/Core/THAC0/THAC0.SPL~ ~override/THAC0%i%.SPL~
			LPF ALTER_SPELL_EFFECT INT_VAR parameter1=i END
	END
	//medium thaco
	COPY ~3ed/Core/THAC0/THAC0M.SPL~ ~override/THAC0M.SPL~
		FOR (i=1;i<=20;i=i+1) BEGIN
			LPF ADD_SPELL_HEADER INT_VAR type=1 location=4 target=5 target_count=0 range=1 required_level=i speed=0 END
				SET r = 20 - (3*i/4)
				SPRINT resource EVALUATE_BUFFER ~THAC0%r%~
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=8 parameter2=105 timing=0 duration=1 STR_VAR resource END // ftr/cleric
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=16 parameter2=105 timing=0 duration=1 STR_VAR resource END // ftr/druid
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=7 parameter2=105 timing=0 duration=1 STR_VAR resource END // ftr/mage
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=9 parameter2=105 timing=0 duration=1 STR_VAR resource END // ftr/thief
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=19 parameter2=105 timing=0 duration=1 STR_VAR resource END // sorceror for battle caster
		END
	//low thac0
	COPY ~3ed/Core/THAC0/THAC0M.SPL~ ~override/THAC0L.SPL~
		FOR (i=1;i<=20;i=i+1) BEGIN
			LPF ADD_SPELL_HEADER INT_VAR type=1 location=4 target=5 target_count=0 range=1 required_level=i speed=0 END
				SET r = 20 - (i/2)
				SPRINT resource EVALUATE_BUFFER ~THAC0%r%~
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=13 parameter2=105 timing=0 duration=1 STR_VAR resource END // mage/thief
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=14 parameter2=105 timing=0 duration=1 STR_VAR resource END // cleric/mage
		END
	//high thaco for swashbuckler
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=40 d_level=1 add_at_level1=1 
						STR_VAR clab=~CLABTH04\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~THAC0H~ END		
	//mdium thaco (fighter-*, and battle caster)
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=40 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLABFI01\)\|\(CLABSO01\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~THAC0M~ END		
	//low thaco (cleric/mage and mage/thief)
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=40 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLABTH01\)\|\(CLABPR01\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~THAC0L~ END		
	//
	COPY ~3ed/Core/APR~ ~override~
	//high apr
		LAF ADD_BONUS_FEATS INT_VAR min_level=6 max_level=20 d_level=5 add_at_level1=0 
						STR_VAR clab=~\(CLAB+.*\)\|\(OHTYR\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~APR_H~ END	
	//medium apr
		LAF ADD_BONUS_FEATS INT_VAR min_level=8 max_level=20 d_level=7 add_at_level1=0 
						STR_VAR clab=~\(CLAB+.*\)\|\(OHTYR\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~APR_M~ END	
	//remove line from swashbuckler and add high apr bonus
	COPY_EXISTING ~CLABTH04.2DA~ ~override~
		COUNT_2DA_ROWS 20 "nrows"
		SET nrows=nrows - 1
		REMOVE_2DA_ROW nrows 20

		
	//low apr
		LAF ADD_BONUS_FEATS INT_VAR min_level=11 max_level=20 d_level=10 add_at_level1=0 
						STR_VAR clab=~\(CLAB+.*\)\|\(OHTYR\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~APR_L~ END	
	//remove line from battle sorceror
		COPY_EXISTING ~CLABSO01.2DA~ ~override~
		COUNT_2DA_ROWS 20 "nrows"
		SET nrows=nrows - 1
		REMOVE_2DA_ROW nrows 20
	LAF ADD_BONUS_FEATS INT_VAR min_level=8 max_level=20 d_level=7 add_at_level1=0 
						STR_VAR clab=~CLABSO01\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~APRBON~ END	
		
	
	//saving throws regularization
	COPY ~3ed/Core/THAC0/THAC0H.SPL~ ~override/SAVESDH.SPL~ //high thaco for swashbucklers
		FOR (i=1;i<=20;i=i+1) BEGIN
			LPF ALTER_SPELL_EFFECT INT_VAR header=i parameter1=(16 - i/2) new_opcode = 33  END //saving throw vs death value
		END
	COPY_EXISTING ~SAVESDH.SPL~  ~override/SAVESWH.SPL~ LPF ALTER_SPELL_EFFECT INT_VAR new_opcode = 34  END //saving throw vs wand
	COPY_EXISTING ~SAVESDH.SPL~  ~override/SAVESPH.SPL~ LPF ALTER_SPELL_EFFECT INT_VAR new_opcode = 35  END //saving throw vs polymorph
	COPY_EXISTING ~SAVESDH.SPL~  ~override/SAVESBH.SPL~ LPF ALTER_SPELL_EFFECT INT_VAR new_opcode = 36  END //saving throw vs  breath
	COPY_EXISTING ~SAVESDH.SPL~  ~override/SAVESSH.SPL~ LPF ALTER_SPELL_EFFECT INT_VAR new_opcode = 37  END //saving throw vs spell
		
	//reflex saves to barbarian and rangers
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=40 d_level=1 add_at_level1=1 STR_VAR clab=~\(CLABRN+.*\)\|\(CLABFI05\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SAVESWH~ END
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=40 d_level=1 add_at_level1=1 STR_VAR clab=~\(CLABRN+.*\)\|\(CLABFI05\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SAVESBH~ END
	
	//spell saves for bard and paladin
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=40 d_level=1 add_at_level1=1 STR_VAR clab=~\(CLABBA+.*\)\|\(CLABPA+.*\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SAVESSH~ END	
	//fort saves for battle caster	
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=40 d_level=1 add_at_level1=1 STR_VAR clab=~CLABSO01\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SAVESDH~ END	
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=40 d_level=1 add_at_level1=1 STR_VAR clab=~CLABSO01\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SAVESPH~ END	
		
		
		
	INCLUDE ~3ed/DIMINISH_SPELL_CASTING.tph~
	
	//ftr/cleric spells
	LAF DIMINISH_SPELL_CASTING STR_VAR clab=~CLABPR01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFC.SPL~ 
		 caption=~F_CSPL~ 2DAfile=~3ed/SpellProgression/F_CSPELLS.2DA~ END
    //cleric/thief
	LAF DIMINISH_SPELL_CASTING STR_VAR clab=~CLABPR01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRECT.SPL~ 
		 caption=~C_TSPL~ 2DAfile=~3ed/SpellProgression/F_CSPELLS.2DA~ END	
	//ranger/cleric spells
	LAF DIMINISH_SPELL_CASTING STR_VAR clab=~CLABPR01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRERC.SPL~ 
		 caption=~R_CSPL~ 2DAfile=~3ed/SpellProgression/F_CSPELLS.2DA~ END		 
	//fighter/druid spells
	LAF DIMINISH_SPELL_CASTING STR_VAR clab=~CLABDR01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFD.SPL~ 
		 caption=~F_DSPL~ 2DAfile=~3ed/SpellProgression/F_CSPELLS.2DA~ END
		 
	//ftr/mage spells
	LAF DIMINISH_SPELL_CASTING INT_VAR is_wizard=1 levels_to_remove=0b110000000  STR_VAR clab=~CLABMA+.*\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFM.SPL~ 
		 caption=~F_MSPL~ 2DAfile=~3ed/SpellProgression/F_MSPELLS.2DA~ END
	//mage/thief spells 
	LAF DIMINISH_SPELL_CASTING INT_VAR is_wizard=1  STR_VAR clab=~CLABMA+.*\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREMT.SPL~ 
		 caption=~M_TSPL~ 2DAfile=~3ed/SpellProgression/F_MSPELLS.2DA~ END	
		 
	//cleric/mage spells
	LAF DIMINISH_SPELL_CASTING STR_VAR clab=~CLABPR01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRECM.SPL~ 
		 caption=~CMCSPL~ 2DAfile=~3ed/SpellProgression/CMCSPELLS.2DA~ END
		 
	LAF DIMINISH_SPELL_CASTING INT_VAR is_wizard=1 STR_VAR clab=~CLABMA+.*\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRECM.SPL~ 
		caption=~CMMSPL~ 2DAfile=~3ed/SpellProgression/CMMSPELLS.2DA~ END
		
	//-1 spell per level for specialist clerics
		LAF DIMINISH_SPELL_CASTING  STR_VAR clab=~\(CLABPR02\)\|\(CLABPR03\)\|\(CLABPR04\)\|\(OHTYR\)\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREC.SPL~ 
		caption=~SP_CL_SPL~ 2DAfile=~3ed/SpellProgression/SP_CLERIC.2DA~ END
	
	

	//xp regularization for multiclasses
	OUTER_FOR (player_id=1;player_id<=6;player_id=player_id+1) BEGIN
		EXTEND_TOP ~BALDUR.BCS~ ~3ed/HybridXP/XPPlayer.baf~
			EVALUATE_BUFFER	
	END
	EXTEND_TOP ~BALDUR.BCS~ ~3ed/HybridXP/XPMRK.baf~
	
	//double xp 
	
	COPY ~3ed/HybridXP/MLTXP.EFF~ ~override~
	COPY ~3ed/HybridXP/MLTXP.SPL~ ~override~
	
	//fighter/mage
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~CLABFI01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFM.SPL~ feat_type_file=~MLTXP~ caption=~MLTXPF_M~ END
	//fighter/cleric
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~CLABFI01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFC.SPL~ feat_type_file=~MLTXP~ caption=~MLTXPF_C~ END
	//fighter/thief
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~CLABFI01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFT.SPL~ feat_type_file=~MLTXP~ caption=~MLTXPF_T~ END
	//fighter/druid
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~CLABFI01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFD.SPL~ feat_type_file=~MLTXP~ caption=~MLTXPF_D~ END
						
	//cleric/ranger
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~CLABPR01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRERC.SPL~ feat_type_file=~MLTXP~ caption=~MLTXPR_C~ END
	//cleric/thief
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~CLABPR01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRECT.SPL~ feat_type_file=~MLTXP~ caption=~MLTXPC_T~ END
	//cleric/mage
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~CLABPR01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRECM.SPL~ feat_type_file=~MLTXP~ caption=~MLTXPC_M~ END
		
    //mage/thief
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~CLABTH01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREMT.SPL~ feat_type_file=~MLTXP~ caption=~MLTXPM_T~ END		
	
						
	COPY ~3ed/HybridXP/STARTARE.2DA~ ~override~
////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////

BEGIN @100
INCLUDE ~3ed/ADD_SPELL_HEADER.tph~//Persistent Stat Replacement Feats
INCLUDE ~3ed/ADD_PSR_FEAT.tph~//Persistent Stat Replacement Feats
INCLUDE ~3ed/ADD_PSB_FEAT.tph~//Persistent Stat Bonus Feats
INCLUDE ~3ed/ADD_PSBP_FEAT.tph~//Persistent Stat Bonus/Penalty Feats
INCLUDE ~3ed/ADD_PSL_FEAT.tph~//Persistent Simple Feats
INCLUDE ~3ed/ADD_ACT_FEAT.tph~//Activated mode Feats
INCLUDE ~3ed/ADD_LUA_FEAT.tph~//Limited Use Feats
INCLUDE ~3ed/ADD_SELECT_FEAT.tph~//select feat ability
INCLUDE ~3ed/CREATE_PROFICIENCY_DESCRIPTION.tph~ //proficiency tree description
INCLUDE ~3ed/GET_FEAT_CLASSKIT_CONDITION.tph~//trigger condition for class	
INCLUDE ~3ed/UPDATE_SFTCRE_DLG.tph~//updating feat giving creature dialog	
INCLUDE ~3ed/REPLACE_SUBSTRING.tph~

	LAF ADD_PSR_FEAT INT_VAR min_val=1 max_val=25 step=2 par1=125 par2=122 STR_VAR ability_name=~WPFNS~ END //weapon finesse (dex replaces str for thac0 for light weapons)
	LAF ADD_PSR_FEAT INT_VAR min_val=2 max_val=25 step=2 par1=125 par2=128 STR_VAR ability_name=~INSTK~ END //insightfull strike (int replaces str for damage for light weapons)
	
	LAF ADD_PSB_FEAT INT_VAR min_val=12 max_val=25 step=2 par1=128 STR_VAR ability_name=~CMBINT~ END //combat intuition (int bonus to ac with light armor)
	LAF ADD_PSB_FEAT INT_VAR min_val=12 max_val=25 step=2 par1=122 STR_VAR ability_name=~ELTSTK~ END //elegant strike (dex bonus to damage with light weapons)
	LAF ADD_PSB_FEAT INT_VAR min_val=12 max_val=25 step=2 par1=130 STR_VAR ability_name=~WISDAC~ END //monk/kensai wisdom bonus to ac with light or no armor 
	
	LAF ADD_PSBP_FEAT INT_VAR min_val=1 bdr_val=11 max_val=25 step=2 par1=132 STR_VAR ability_name=~CHASAV~ END //cha bonus to saving throws (for paladin and jester)
	LAF ADD_PSBP_FEAT INT_VAR min_val=1 bdr_val=11 max_val=25 step=2 par1=122 STR_VAR ability_name=~DEXSAV~ END //dex bonus to saving throws
	LAF ADD_PSBP_FEAT INT_VAR min_val=1 bdr_val=11 max_val=25 step=2 par1=130 STR_VAR ability_name=~WISSAV~ END //wis bonus to saving throws
	LAF ADD_PSBP_FEAT INT_VAR min_val=1 bdr_val=11 max_val=25 step=2 par1=126 STR_VAR ability_name=~CONSAV~ END //con bonus to saving throws
	LAF ADD_PSBP_FEAT INT_VAR min_val=2 bdr_val=12 max_val=25 step=2 par1=132 STR_VAR ability_name=~TUDLVL~ END //cha bonus to Turn undead level (for paladin and cleric)
	
	//prtection against applying saves bonus second time
	COPY_EXISTING ~DEXSAVFT.SPL~ ~override~ 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=206 target=2 duration=1 timing=9 STR_VAR resource=~DEXSAVFT~ END 
	//prtection against applying saves bonus second time
	COPY_EXISTING ~WISSAVFT.SPL~ ~override~ 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=206 target=2 duration=1 timing=9 STR_VAR resource=~WISSAVFT~ END 
	//prtection against applying saves bonus second time
	COPY_EXISTING ~CONSAVFT.SPL~ ~override~ 
		LPF ADD_SPELL_EFFECT INT_VAR opcode=206 target=2 duration=1 timing=9 STR_VAR resource=~CONSAVFT~ END 
	
	
	LAF ADD_PSL_FEAT STR_VAR ability_name=~RPDSTI~ END //improved rapid shot (+1 attack with bows)/darts/throwing daggers
		COPY_EXISTING ~RPDSTIFT.SPL~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~RPDSHTBN~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~RPDSHT1~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~RPDSHT1~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~RPDSHT0~ END
		

	LAF ADD_PSL_FEAT STR_VAR ability_name=~PBSHOT~ END //point blank shot (+1 attack,+1 damage with all ranged weapons)
	LAF ADD_PSL_FEAT STR_VAR ability_name=~MKFLR3~ END //flurry of blows +1 attack without penalties
	//add removal of effect of flurry2 and ability buttons, add protection from flurry 2,1
		COPY_EXISTING ~MKFLR3FT.SPL~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~MKFLR21~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~MKFLR2BN~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~MKFLR21~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~MKFLR20~ END
		
	LAF ADD_PSL_FEAT STR_VAR ability_name=~MKFLR4~ END //flurry of blows +2 attacks without penalties
	//add removal of effect of flurry3 feat and effect, add protection from flurry 3,2,1
	COPY_EXISTING ~MKFLR4FT.SPL~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 duration=1 insert_point=0 STR_VAR resource=~MKFLR3BN~ END 
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 duration=1 insert_point=0 STR_VAR resource=~MKFLR3FT~ END 
	
    	
	OUTER_SPRINT game_name @101  OUTER_SPRINT game_description @102
	LAF ADD_ACT_FEAT STR_VAR ability_name=~CMBEXP~ game_name game_description END //combat expertise (+3AC, -3 to hit)
	OUTER_SPRINT game_name @103  OUTER_SPRINT game_description @104
	LAF ADD_ACT_FEAT STR_VAR ability_name=~PWRATK~ game_name game_description  END //power attack (+3dmg, -3 to hit for melee weapons)
	OUTER_SPRINT game_name @105  OUTER_SPRINT game_description @106
	LAF ADD_ACT_FEAT STR_VAR ability_name=~DEDAIM~ game_name game_description  END //deadly aim (+3AC, -3 to hit for ranged weapons)
	OUTER_SPRINT game_name @107  OUTER_SPRINT game_description @108
	LAF ADD_ACT_FEAT STR_VAR ability_name=~FLRSTK~ game_name game_description  END //flurry strike (+1 attack, -5 to hit with light weapons)
	OUTER_SPRINT game_name @109  OUTER_SPRINT game_description @110
	LAF ADD_ACT_FEAT STR_VAR ability_name=~RPDSHT~ game_name game_description  END //rapid shot (+1 attack, -2 to hit with ranged weapons, +rapid reload)
	

	OUTER_SPRINT game_name @111  OUTER_SPRINT game_description @112
	LAF ADD_ACT_FEAT STR_VAR ability_name=~MKFLR1~ game_name game_description  END //monk flurry of blows (+1 attack, -2 to hit)
	LAF ADD_ACT_FEAT STR_VAR ability_name=~MKFLR2~ game_name game_description  END //monk flurry of blows (+1 attack, -1 to hit)
	//add removal of flurry 1 effect and feat
		COPY_EXISTING ~MKFLR2FT.SPL~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 duration=1 insert_point=0 STR_VAR resource=~MKFLR1BN~ END 
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~MKFLR11~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~MKFLR11~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~MKFLR10~ END
	
	OUTER_SPRINT game_name @115  OUTER_SPRINT game_description @116
	LAF ADD_ACT_FEAT STR_VAR ability_name=~PWRATI~ game_name game_description  END //improved power attack (+5 dmg, -3 thaco)
	//add removal of standard power attack
		COPY_EXISTING ~PWRATIFT.SPL~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 duration=1 insert_point=0 STR_VAR resource=~PWRATIBN~ END 
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~PWRATI1~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~PWRATI1~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~PWRATI0~ END
	
	OUTER_SPRINT game_name @117  OUTER_SPRINT game_description @118
	LAF ADD_ACT_FEAT STR_VAR ability_name=~PWRATS~ game_name game_description  END //supreme power attack (+6 dmg, -3 thaco)
	//add removal of standard power attack
		COPY_EXISTING ~PWRATSFT.SPL~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 duration=1 insert_point=0 STR_VAR resource=~PWRATSBN~ END 
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~PWRATS1~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~PWRATS1~ END
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=172 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource=~PWRATS0~ END	
	
	//adding permanent abilities
	//armored caster
	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
		COPY ~3ed/Feats/PermanentAbilities/ArmoredCaster/ARCST%i%FT.SPL~  ~override~
	END
	//bonus priest spells
	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
		COPY ~3ed/Feats/PermanentAbilities/BonusPriestSpells/PRSLT%i%FT.SPL~  ~override~
	END
	//bonus wizard spells
	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
		COPY ~3ed/Feats/PermanentAbilities/BonusWizardSpells/WZSLT%i%FT.SPL~  ~override~
	END
	//cleave
	COPY ~3ed/Feats/PermanentAbilities/Cleave~  ~override~
	COPY_EXISTING ~CLEAV1FT.SPL~ ~override~
	//Dodge
	COPY ~3ed/Feats/PermanentAbilities/Dodge/DODGEFT.SPL~  ~override~
	//Empower magic
	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
		COPY ~3ed/Feats/PermanentAbilities/EmpowerMagic/EMPWR.SPL~  ~override/EMPWR%i%FT.SPL~
		LPF ALTER_SPELL_EFFECT INT_VAR  parameter1=(4 + 2*i) END //spell damage bonus 
	END
	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
		COPY ~3ed/Feats/PermanentAbilities/QuickenMagic/QUICKEN.SPL~  ~override/QUICK%i%FT.SPL~
	END
	//Extend magic
	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
		COPY ~3ed/Feats/PermanentAbilities/ExtendMagic/EXTND.SPL~  ~override/EXTND%i%FT.SPL~
		LPF ALTER_SPELL_EFFECT INT_VAR  parameter1=100+i*(10+i) END //spell duration
		SPRINT resource EVALUATE_BUFFER ~EXTND%i%FT~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=206 target=2 parameter1=0 duration=1 timing=9 STR_VAR resource END
		FOR (j=1;j<i;j=j+1) BEGIN
			SPRINT resource EVALUATE_BUFFER ~EXTND%j%FT~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 duration=1 timing=9 STR_VAR resource END  //remove effects of previous extend
		END
	END
	//Greater Cleave
	COPY ~3ed/Feats/PermanentAbilities/GreaterCleave~  ~override~
	COPY_EXISTING ~CLEAV2FT.SPL~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 duration=1 timing=9 STR_VAR resource=~CLEAV1FT~ END  //remove effects of previous cleave
		LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 duration=1 timing=9 STR_VAR resource=~CLEAVE1~ END  //remove effects of previous cleave
	//Great Forititude
	COPY ~3ed/Feats/PermanentAbilities/GreatFortitude/GRTFTDFT.SPL~  ~override~
	//Iron Will
	COPY ~3ed/Feats/PermanentAbilities/IronWill/IRONWLFT.SPL~  ~override~
	//Lightning reflexes
	COPY ~3ed/Feats/PermanentAbilities/LightningReflexes/LTNGRXFT.SPL~  ~override~
	//Protection from Magic
	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
		COPY ~3ed/Feats/PermanentAbilities/MagicProtection/MGPTN.SPL~  ~override/MGPTN%i%FT.SPL~
		LPF ALTER_SPELL_EFFECT INT_VAR  parameter1=5 END //resistance bonus 5% level
	END
	//Ranged Critical (Precise shot)
	COPY ~3ed/Feats/PermanentAbilities/RangedCritical/RNGCRTFT.SPL~  ~override~
	//Toughness
	COPY ~3ed/Feats/PermanentAbilities/Toughness/TOUGHSFT.SPL~  ~override~	
	//Study oponent
	OUTER_SPRINT game_name @113  OUTER_SPRINT game_description @114
	LAF ADD_LUA_FEAT INT_VAR n_uses=3 STR_VAR ability_name=~STUDOP~ game_name game_description  END //Study Oponent (-3 AC, thac0 and saves to one enrmy, 3 times per day)
	
	
	//Select Feat Ability
	OUTER_SET   yes_response=RESOLVE_STR_REF (@2101)
	OUTER_SET   no_response=RESOLVE_STR_REF (@2102)
	
	
	//weapon feats	 
	OUTER_FOR (k=1;k<=10;k=k+1) BEGIN
		OUTER_SET proficiency_id=88+k
		OUTER_SET Category_ref=130+k
		OUTER_SPRINT Category (AT "Category_ref")
      	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
			OUTER_SET n_stars_old = i - 1
			OUTER_SET lvl= 4*(i - 1) - (i>1)
			OUTER_SPRINT new_name  EVALUATE_BUFFER ~FT%proficiency_id%_%i%~
			OUTER_SET WpnGrade_ref=140+i
			OUTER_SPRINT WpnGradeString (AT "WpnGrade_ref")
			OUTER_SET WpnGradeDescription_ref=145+i
			OUTER_SPRINT WpnGradeDescString (AT "WpnGradeDescription_ref")
			OUTER_SPRINT game_name EVALUATE_BUFFER ~%Category% (%i%): %WpnGradeString%~
			OUTER_SPRINT game_description EVALUATE_BUFFER ~%WpnGradeDescString%~
			OUTER_SET n_stars=i
			LAF ADD_SELECT_FEAT INT_VAR proficiency_id n_stars STR_VAR new_name feat=~~ game_name game_description END

			LAF UPDATE_SFTCRE_DLG INT_VAR n_stars_old n_stars proficiency_id lvl STR_VAR sftcre_name=~sftcre~ new_name conditions_str END
			LAF UPDATE_SFTCRE_DLG INT_VAR n_stars_old n_stars proficiency_id lvl STR_VAR sftcre_name=~fftcre~ new_name conditions_str END
		END
    END
	//archery feats array
	ACTION_DEFINE_ARRAY ArcheryFeats BEGIN
		~RPDSHTFT~ ~PBSHOTFT~ ~RPDSTIFT~ ~DEDAIMFT~ ~RNGCRTFT~
	END
	//weapon styles
	OUTER_FOR (k=1;k<=5;k=k+1) BEGIN
		OUTER_SET proficiency_id=110+k
		OUTER_SET Category_ref=150+k
		OUTER_SPRINT Category (AT "Category_ref")
      	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
			OUTER_SET n_stars_old = i - 1
			OUTER_SET lvl= 4*(i - 1) - (i>1)
			OUTER_SPRINT new_name  EVALUATE_BUFFER ~FT%proficiency_id%_%i%~
			OUTER_SET WpnGrade_ref=155+i
			OUTER_SPRINT WpnGradeString (AT "WpnGrade_ref")
			OUTER_SET WpnGradeDescription_ref=160+i+(k - 1)*5
			OUTER_SPRINT WpnGradeDescString (AT "WpnGradeDescription_ref")
			OUTER_SPRINT game_name EVALUATE_BUFFER ~%Category% (%i%): %WpnGradeString%~
			OUTER_SPRINT game_description EVALUATE_BUFFER ~%WpnGradeDescString%~
			OUTER_SPRINT feat ~~
			OUTER_SET n_stars=i
			ACTION_IF (k=5) BEGIN //archery
				OUTER_SET archery_feat_id=i - 1
				OUTER_SPRINT feat EVALUATE_BUFFER $ArcheryFeats("%archery_feat_id%")
			END
			LAF ADD_SELECT_FEAT INT_VAR proficiency_id n_stars STR_VAR new_name feat game_name game_description END

			LAF UPDATE_SFTCRE_DLG INT_VAR n_stars_old n_stars proficiency_id lvl STR_VAR sftcre_name=~sftcre~ new_name conditions_str END
			LAF UPDATE_SFTCRE_DLG INT_VAR n_stars_old n_stars proficiency_id lvl STR_VAR sftcre_name=~fftcre~ new_name conditions_str END
		END
    END	
	//combat feats - combat prowess, warfare, tactics
	ACTION_DEFINE_ARRAY GeneralFeats BEGIN
		~WPFNSFT~ ~DODGEFT~ ~LTNGRXFT~ ~FLRSTKFT~ ~ELTSTKFT~ //prowess
		~PWRATKFT~ ~CLEAV1FT~ ~GRTFTDFT~ ~CLEAV2FT~ ~TOUGHSFT~ //warfare
		~INSTKFT~ ~CMBEXPFT~ ~IRONWLFT~ ~STUDOPFT~ ~CMBINTFT~ //tactics	
		~PRSLT1FT~ ~PRSLT2FT~ ~PRSLT3FT~ ~PRSLT4FT~ ~PRSLT5FT~ //bonus priest spells
		~WZSLT1FT~ ~WZSLT2FT~ ~WZSLT3FT~ ~WZSLT4FT~ ~WZSLT5FT~ //bonus wizard spells
		~EMPWR1FT~ ~EMPWR2FT~ ~EMPWR3FT~ ~EMPWR4FT~ ~EMPWR5FT~ //empower magic		
		~EXTND1FT~ ~EXTND2FT~ ~EXTND3FT~ ~EXTND4FT~ ~EXTND5FT~ //extend magic	
		~MGPTN1FT~ ~MGPTN2FT~ ~MGPTN3FT~ ~MGPTN4FT~ ~MGPTN5FT~ //endure magic	
		~QUICK1FT~ ~QUICK2FT~ ~QUICK3FT~ ~QUICK4FT~ ~QUICK5FT~ //quicken magic	
	END
	
	

	
	OUTER_FOR (k=1;k<=9;k=k+1) BEGIN //9
		OUTER_SET proficiency_id=98+k
		OUTER_SET Category_ref=185+k
		OUTER_SPRINT Category (AT "Category_ref")
      	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN //5
			OUTER_SET n_stars_old = i - 1
			OUTER_SET lvl= 4*(i - 1) - (i>1)
			OUTER_SPRINT new_name  EVALUATE_BUFFER ~FT%proficiency_id%_%i%~
			OUTER_SET FeatGrade_ref=195+i+(k - 1)*5
			OUTER_SPRINT FeatGradeString (AT "FeatGrade_ref")
			OUTER_SET FeatGradeDescription_ref=240+i+(k - 1)*5
			OUTER_SPRINT FeatGradeDescString (AT "FeatGradeDescription_ref")
			OUTER_SPRINT game_name EVALUATE_BUFFER ~%Category% (%i%): %FeatGradeString%~
			OUTER_SPRINT game_description EVALUATE_BUFFER ~%FeatGradeDescString%~
			OUTER_SET feat_id= (i - 1)+(k - 1)*5
			OUTER_SPRINT feat EVALUATE_BUFFER $GeneralFeats("%feat_id%")
			OUTER_SET n_stars=i
			LAF ADD_SELECT_FEAT INT_VAR proficiency_id n_stars STR_VAR new_name feat game_name game_description END
			LAF UPDATE_SFTCRE_DLG INT_VAR n_stars_old n_stars proficiency_id lvl STR_VAR sftcre_name=~sftcre~ new_name conditions_str END
			ACTION_IF (k<=3 OR k==9) BEGIN
				LAF UPDATE_SFTCRE_DLG INT_VAR n_stars_old n_stars proficiency_id lvl STR_VAR sftcre_name=~fftcre~ new_name conditions_str END
			END
			ACTION_IF (k>3) BEGIN
				LAF UPDATE_SFTCRE_DLG INT_VAR n_stars_old n_stars proficiency_id lvl STR_VAR sftcre_name=~mftcre~ new_name conditions_str END
			END
		END
    END
	
	
	COPY ~3ed/Feats/FeatAttribution/sftcre.d~  ~3ed/Feats/FeatAttribution/sftcre.d~  	
	REPLACE_TEXTUALLY ~%~	~|~
	REPLACE_TEXTUALLY ~|condition_str|~ ~~
	REPLACE_TEXTUALLY ~|give_feat_str|~ ~~	

	COMPILE ~3ed/Feats/FeatAttribution/sftcre.d~  
	
	COPY ~3ed/Feats/FeatAttribution/fftcre.d~  ~3ed/Feats/FeatAttribution/fftcre.d~  	
	REPLACE_TEXTUALLY ~%~	~|~
	REPLACE_TEXTUALLY ~|condition_str|~ ~~
	REPLACE_TEXTUALLY ~|give_feat_str|~ ~~	

	COMPILE ~3ed/Feats/FeatAttribution/fftcre.d~  
	
	COPY ~3ed/Feats/FeatAttribution/mftcre.d~  ~3ed/Feats/FeatAttribution/mftcre.d~  	
	REPLACE_TEXTUALLY ~%~	~|~
	REPLACE_TEXTUALLY ~|condition_str|~ ~~
	REPLACE_TEXTUALLY ~|give_feat_str|~ ~~	

	COMPILE ~3ed/Feats/FeatAttribution/mftcre.d~  

	
	
	COPY ~3ed/Feats/FeatAttribution/SFTCRE.BCS~ ~override~
	//all feats
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/SFTCRE.CRE~
		WRITE_ASCII 0x248 ~SFTCRE~ #8//override script
		WRITE_ASCII 0x02cc ~sftcre~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/SFTCRE.EFF~
		WRITE_ASCII 0x0030 ~SFTCRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/SFTCRE.SPL~	
		LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~SFTCRE~ END
		
	//fighter feats
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/FFTCRE.CRE~
		WRITE_ASCII 0x248 ~SFTCRE~ #8//override script
		WRITE_ASCII 0x02cc ~fftcre~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/FFTCRE.EFF~
		WRITE_ASCII 0x0030 ~FFTCRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/FFTCRE.SPL~	
		LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~FFTCRE~ END
		
	//mage feats
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/MFTCRE.CRE~
		WRITE_ASCII 0x248 ~SFTCRE~ #8//override script
		WRITE_ASCII 0x02cc ~mftcre~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/MFTCRE.EFF~
		WRITE_ASCII 0x0030 ~MFTCRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/MFTCRE.SPL~	
		LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~MFTCRE~ END
	
	
	INCLUDE ~3ed/ADD_BONUS_FEATS.tph~
//add bonus feats to fighter (lvl1 and every even level)
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=20 d_level=2 add_at_level1=1 
						STR_VAR clab=~CLABFI01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREF.SPL~ feat_type_file=~FFTCRE~ caption=~FTRBONFT~ END
						
//add bonus feats to barbarian and warven defender every 5 levels starting from lvl5
	LAF ADD_BONUS_FEATS INT_VAR min_level=5 max_level=20 d_level=5 add_at_level1=0 
						STR_VAR clab=~\(CLABFI05\)\|\(CLABFI06\)\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREF.SPL~ feat_type_file=~FFTCRE~ caption=~FKTBONFT~ END

//add bonus feats to ftr/cleric every 4 levels and at level 1
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=20 d_level=4 add_at_level1=1 
						STR_VAR clab=~CLABFI01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFC.SPL~ feat_type_file=~SFTCRE~ caption=~F_CBONFT~ END
						
	//set delay to 1 second (to avoid hangup on 1st level character creation)
	COPY_EXISTING ~F_CBONFT.SPL~ ~override~
		LPF  ALTER_SPELL_EFFECT INT_VAR duration  =1 timing =3  END
	
//same for ftr/mage
	LAF ADD_BONUS_FEATS INT_VAR min_level=5 max_level=20 d_level=5 add_at_level1=0 
						STR_VAR clab=~CLABFI01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFM.SPL~ feat_type_file=~SFTCRE~ caption=~F_MBONFT~ END
						
//add bonus feats to ftr/thief every 3 levels starting from lvl1
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=20 d_level=3 add_at_level1=1 
						STR_VAR clab=~CLABFI01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFT.SPL~ feat_type_file=~FFTCRE~ caption=~F_TBONFT~ END	
					
//add bonus feats to thief (everyone except assasin and ninja) (lvl 10,13,16,19 )
	LAF ADD_BONUS_FEATS INT_VAR min_level=10 max_level=20 d_level=3 add_at_level1=0 
						STR_VAR clab=~\(CLABTH01\)\|\(CLABTH04\)\|\(CLABTH05\)\.2DA~
							mask_file=~3ed/Feats/FeatAttribution/SFTCRET.SPL~ feat_type_file=~FFTCRE~ caption=~THFBONFT~ END	
//same for cleric thief
/* 	LAF ADD_BONUS_FEATS INT_VAR min_level=10 max_level=20 d_level=3 add_at_level1=0 
						STR_VAR clab=~CLABTH01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRECT.SPL~ feat_type_file=~SFTCRE~ caption=~C_TBONFT~ END		 */				
							
//add bonus feats to mage and sorcerer every 5 levels starting from lvl 5
	LAF ADD_BONUS_FEATS INT_VAR min_level=5 max_level=20 d_level=5 add_at_level1=0 
						STR_VAR clab=~CLABMA+.*\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREW.SPL~ feat_type_file=~MFTCRE~ caption=~WIZBONFT~ END
//battle caster
	LAF ADD_BONUS_FEATS INT_VAR min_level=5 max_level=20 d_level=5 add_at_level1=0 
						STR_VAR clab=~CLABSO01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRES.SPL~ feat_type_file=~SFTCRE~ caption=~BCSBONFT~ END
//same for mage thief
	LAF ADD_BONUS_FEATS INT_VAR min_level=5 max_level=20 d_level=5 add_at_level1=0 
						STR_VAR clab=~CLABMA+.*\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREMT.SPL~ feat_type_file=~SFTCRE~ caption=~M_TBONFT~ END
//bards
	LAF ADD_BONUS_FEATS INT_VAR min_level=5 max_level=20 d_level=5 add_at_level1=0 
						STR_VAR clab=~CLABBA+.*\.2DA~ mask_file=~~ caption=~SFTCRE~ END											
//add bonus feat to humans at lvl1
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLAB+.*\)\|\(OHTYR\)\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREH.SPL~ feat_type_file=~SFTCRE~ caption=~HUMBONFT~ END


						
//archery feats tree for archer and ftr/druid
    COPY ~3ed/Feats/FeatAttribution/SFTCRE_E.SPL~ ~override/ARCHFT.SPL~
		FOR (i=1;i<=5;i=i+1) BEGIN
			SET lvl=4*(i - 1)
			PATCH_IF (lvl==0) BEGIN
				SET lvl=1
			END
			LPF ADD_SPELL_HEADER INT_VAR type=1 location=4 target=5 target_count=0 range=1 required_level=lvl speed=0 END
				SPRINT resource EVALUATE_BUFFER ~SFT115_%i%~
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=12 parameter2=105 timing=0 duration=1 STR_VAR resource END // ranger
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=16 parameter2=105 timing=0 duration=1 STR_VAR resource END // ftr/druid
		END		
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=16 d_level=4 add_at_level1=1 
						STR_VAR clab=~\(CLABFI01\)\|\(CLABRN02\)\.2DA~
							mask_file=~~ feat_type_file=~~ caption=~ARCHFT~ END	
//2 weapon fighting for ranger and ranger/cleric
    COPY ~3ed/Feats/FeatAttribution/SFTCRE_E.SPL~ ~override/TWFFT.SPL~
		FOR (i=1;i<=5;i=i+1) BEGIN
			SET lvl=4*(i - 1)
			PATCH_IF (lvl==0) BEGIN
				SET lvl=1
			END
			LPF ADD_SPELL_HEADER INT_VAR type=1 location=4 target=5 target_count=0 range=1 required_level=lvl speed=0 END
				SPRINT resource EVALUATE_BUFFER ~SFT114_%i%~
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=12 parameter2=105 timing=0 duration=1 STR_VAR resource END // ranger
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=18 parameter2=105 timing=0 duration=1 STR_VAR resource END // cleric/rgr
		END		
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=16 d_level=4 add_at_level1=1 
						STR_VAR clab=~CLABRN01\.2DA~
							mask_file=~~ feat_type_file=~~ caption=~TWFFT~ END	
//armored arcana (ftr/mage, lvl1 bard, lvl1 -mage/thief)
    COPY ~3ed/Feats/FeatAttribution/SFTCRE_E.SPL~ ~override/ARMARCFT.SPL~
		FOR (i=1;i<=5;i=i+1) BEGIN
			SET lvl=4*(i - 1)
			PATCH_IF (lvl==0) BEGIN
				SET lvl=1
			END
			LPF ADD_SPELL_HEADER INT_VAR type=1 location=4 target=5 target_count=0 range=1 required_level=lvl speed=0 END
				SPRINT resource EVALUATE_BUFFER ~ARCST%i%FT~
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=7 parameter2=105 timing=0 duration=1 STR_VAR resource END // fighter/mage
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=5 parameter2=105 timing=0 duration=1 STR_VAR resource END // bard
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=13 parameter2=105 timing=0 duration=1 STR_VAR resource END // mage/thief
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=19 parameter2=105 timing=0 duration=1 STR_VAR resource END // sorceror (for battle caster)
		END
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=16 d_level=4 add_at_level1=1 
						STR_VAR clab=~CLABFI01\.2DA~
							mask_file=~~ feat_type_file=~~ caption=~ARMARCFT~ END	
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=4 add_at_level1=1 
						STR_VAR clab=~\(CLABBA+.*\)\|\(CLABSO01\)\|\(CLABTH01\)\.2DA~
							mask_file=~~ feat_type_file=~~ caption=~ARMARCFT~ END								
//armored training (ftr, ftr-thief)
	COPY ~3ed/Feats/ArmorTraining~ ~override~
    COPY ~3ed/Feats/FeatAttribution/SFTCRE_E.SPL~ ~override/ARMTRFT.SPL~
		FOR (i=1;i<=5;i=i+1) BEGIN
			SET lvl=4*(i - 1)
			PATCH_IF (lvl==0) BEGIN
				SET lvl=1
			END
			LPF ADD_SPELL_HEADER INT_VAR type=1 location=4 target=5 target_count=0 range=1 required_level=lvl speed=0 END
				SPRINT resource EVALUATE_BUFFER ~ARMTR%i%~
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=2 parameter2=105 timing=0 duration=1 STR_VAR resource END // fighter
				LPF ADD_SPELL_EFFECT INT_VAR header=i opcode=326 target=2 parameter1=9 parameter2=105 timing=0 duration=1 STR_VAR resource END // fighter/thief
		END
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=16 d_level=4 add_at_level1=1 
						STR_VAR clab=~CLABFI01\.2DA~
							mask_file=~~ feat_type_file=~~ caption=~ARMTRFT~ END		
		
											
//ability bonus for half-elf
COPY ~3ed/Feats/HalfElfBonus/HFSTRBN.SPL~ ~override~ 
COPY ~3ed/Feats/HalfElfBonus/HFCONBN.SPL~ ~override~ 
COPY ~3ed/Feats/HalfElfBonus/HFDEXBN.SPL~ ~override~ 
COPY ~3ed/Feats/HalfElfBonus/HFINTBN.SPL~ ~override~ 
COPY ~3ed/Feats/HalfElfBonus/HFWISBN.SPL~ ~override~ 
COPY ~3ed/Feats/HalfElfBonus/HFCHABN.SPL~ ~override~ 

COMPILE ~3ed/Feats/HalfElfBonus/hfcre.d~  
COPY ~3ed/Feats/FeatAttribution/SFTCRE.BCS~ ~override/HFCRE.BCS~
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/HFCRE.CRE~
		WRITE_ASCII 0x248 ~HFCRE~ #8//override script
		WRITE_ASCII 0x02cc ~hfcre~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/HFCRE.EFF~
		WRITE_ASCII 0x0030 ~HFCRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/HFCRE.SPL~	
		LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~HFCRE~ END
		

//add bonus ability points to  half-elves at lvl1
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLAB+.\)\|\(OHTYR\)\.2DA~ mask_file=~3ed/Feats/HalfElfBonus/SHFCRE.SPL~
						feat_type_file=~HFCRE~ caption=~HLELFBON~ END

		
//add bonus ability points to  small races  (gnome, halfling) at lvl1
COPY ~3ed/Feats/SmallRacesBonus/SMLRCBN.SPL~ ~override~ 	

	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLAB+.\)\|\(OHTYR\)\.2DA~ mask_file=~3ed/Feats/SmallRacesBonus/SMLRACE.SPL~
						feat_type_file=~SMLRCBN~ caption=~SMLRACE~ END


		
		
	//feat attribution for featcre script (gives feats depnding on number of stars after lvlup)
 	COPY ~3ed/Feats/FeatAttribution/FEATCRE.BCS~ ~override/CFEATCRE.BCS~
	//archery (115)
	OUTER_FOR (i=5;i>=1;i=i - 1) BEGIN
		EXTEND_TOP ~CFEATCRE.BCS~ ~3ed/Feats/FeatAttribution/SFEATCRE.baf~
			SPRINT proficiency_id ~115~
			SPRINT n_stars EVALUATE_BUFFER ~%i%~
			EVALUATE_BUFFER			
	END
	//everything else
	OUTER_FOR (k=107;k>=99;k=k - 1) BEGIN
		OUTER_FOR (i=5;i>=1;i=i - 1) BEGIN
			EXTEND_TOP ~CFEATCRE.BCS~ ~3ed/Feats/FeatAttribution/SFEATCRE.baf~
				SPRINT proficiency_id EVALUATE_BUFFER ~%k%~
				SPRINT n_stars EVALUATE_BUFFER ~%i%~
				EVALUATE_BUFFER
		END
	END

	
//create checking feat effect
COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/CFEATCRE.CRE~
	WRITE_ASCII 0x248 ~CFEATCRE~ //override script
COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/CFEATCRE.EFF~
	WRITE_ASCII 0x0030 ~CFEATCRE~ //creature name
COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/CFEATCRE.SPL~	
	LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~CFEATCRE~ END

//add it to every clab file every 3 levels (and lvl1)

	LAF ADD_BONUS_FEATS INT_VAR min_level=3 max_level=40 d_level=3 add_at_level1=1 
						STR_VAR clab=~\(CLAB+.\)\|\(OHTYR\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~CFEATCRE~ END
	 
COPY ~3ed/Classes/Kensai/KENSCRE.BCS~ ~override~
//creature for kensai
	OUTER_FOR (k=1;k<=7;k=k+1) BEGIN
		OUTER_SET proficiency_id=88+k
		COPY ~3ed/Classes/Kensai/KENSFT.SPL~ ~override/KNSFT%proficiency_id%.SPL~  //mark chosen category
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=233 parameter2=proficiency_id END
		//dialog for selecting signature weapon category
		OUTER_SET strref_name=25000 + k - 1
		OUTER_SPRINT condition_str EVALUATE_BUFFER
~~~~~ IF ~~  THEN REPLY #%strref_name% DO ~ApplySpellRES("KNSFT%proficiency_id%",LastSummonerOf(Myself))~  EXIT
|condition_str|~~~~~
		COPY ~3ed/Classes/Kensai/kenscre.d~  ~3ed/Classes/Kensai/kenscre.d~  									
			EVALUATE_BUFFER	
			REPLACE_TEXTUALLY ~|~ ~%~	
		//script for giving bonus feats based on selected weapon category
		OUTER_FOR (i=1;i<=3;i=i+1) BEGIN   
			OUTER_SET lvl=4*i
			OUTER_SET n_stars = i + 1
			EXTEND_TOP ~KENSCRE.BCS~  ~3ed/Classes/Kensai/KENSCRE.baf~
				EVALUATE_BUFFER	
		END
		
    END
	
	COPY ~3ed/Classes/Kensai/kenscre.d~  ~3ed/Classes/Kensai/kenscre.d~
		REPLACE_TEXTUALLY ~%~	~|~
		REPLACE_TEXTUALLY ~|condition_str|~ ~~
		COMPILE ~3ed/Classes/Kensai/kenscre.d~ 
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/KENSCRE.CRE~
		WRITE_ASCII 0x248 ~KENSCRE~ #8//override script
		WRITE_ASCII 0x02cc ~kenscre~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/KENSCRE.EFF~
		WRITE_ASCII 0x0030 ~KENSCRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/KENSCRE.SPL~	
		LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~KENSCRE~ END
	
	
	 
	 
	 
	 

INCLUDE ~3ed/REPLACE_SUBSTRING.tph~
INCLUDE ~3ed/PTCH_WPN.tph~
INCLUDE ~3ed/UPDATE_WEAPON_DMG.tph~
	//proficincy descriptions
	//weapons
	OUTER_FOR (k=0;k<9;k=k+1) BEGIN
		LAF CREATE_PROFICIENCY_DESCRIPTION INT_VAR strref_name=25000+k strref_desc=25024+k label_name=131+k label_desc=2001+k label_featname=141 label_featdesc=146 n_stars=5 label_slot=2025 label_slots=2026 END
	END
	//spears ->Missile weapons
	LAF CREATE_PROFICIENCY_DESCRIPTION INT_VAR strref_name=25010 strref_desc=25034 label_name=140 label_desc=2010 label_featname=141 label_featdesc=146 n_stars=5 label_slot=2025 label_slots=2026 END
	//styles
	OUTER_FOR (k=0;k<3;k=k+1) BEGIN
		LAF CREATE_PROFICIENCY_DESCRIPTION INT_VAR strref_name=25020+k strref_desc=25044+k label_name=151+k label_desc=2011+k label_featname=156 label_featdesc=161+k*5 n_stars=5 label_slot=2025 label_slots=2026 END
	END
	//2weapon combat
	LAF CREATE_PROFICIENCY_DESCRIPTION INT_VAR strref_name=25023 strref_desc=24999 label_name=154 label_desc=2014 label_featname=156 label_featdesc=176 n_stars=5 label_slot=2025 label_slots=2026 END
	//archery <-club
	LAF CREATE_PROFICIENCY_DESCRIPTION INT_VAR strref_name=25009 strref_desc=25033 label_name=155 label_desc=2015 label_featname=156 label_featdesc=181 n_stars=5 label_slot=2025 label_slots=2026 END
	//General feats
	OUTER_FOR (k=0;k<9;k=k+1) BEGIN
		LAF CREATE_PROFICIENCY_DESCRIPTION INT_VAR strref_name=25011+k strref_desc=25035+k label_name=186+k label_desc=2016+k label_featname=196+k*5 label_featdesc=241+k*5 n_stars=5 label_slot=2025 label_slots=2026 END
	END
	COPY ~3ed/2HSTRBonus~ ~override~ //2h weapon bonuses
	//change description for weapons and apply protections from feats
	COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~
		READ_BYTE  0x0031 "proficiency"
		READ_SHORT 0x0038 "stack"   // to diffirintiate between throwing melee versions
		READ_BYTE  0x0018 "flags1"  //2h -> bit1, dropable ->bit2
		READ_BYTE  0x001b "flags4"  //bit 1 -> protection from critical hits
		READ_SHORT 0x001c "category" //=28 for fist weapons, =2 -armor, 68 -robes
		READ_BYTE  0x0020 "mage_usability" 
		
		SET usable_by_mage = NOT (mage_usability BAND 0b00000100)

		
		PATCH_IF (proficiency = 89 ) BEGIN //bastard swords ->straight swords
			LPF UPDATE_WEAPON_DMG INT_VAR match_dice_number = 2 match_dice_size = 4 target_dice_number = 1 target_dice_size = 10 END
			LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~2d4~  new_substring=~1d10~ END
			LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~2d4~  new_substring=~1d10~ END
			LPF PTCH_WPN INT_VAR replace_label=2031 wpn_class_label=131 caption_label=2030 is_melee=1 END				
		END 
		ELSE PATCH_IF (proficiency = 90) BEGIN//longsword ->straight swords
			WRITE_BYTE 0x0031 89
			LPF PTCH_WPN INT_VAR replace_label=2032 wpn_class_label=131 caption_label=2030 is_melee=1 is_light=1 END			
		END   
		ELSE PATCH_IF (proficiency = 93) BEGIN //2h sword ->straight swords
			LPF UPDATE_WEAPON_DMG INT_VAR match_dice_number = 1 match_dice_size = 10 target_dice_number = 2 target_dice_size = 6 END
			LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~1d10~  new_substring=~2d6~ END
			LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~1d10~  new_substring=~2d6~ END
			
			WRITE_BYTE 0x0031 89
			LPF PTCH_WPN INT_VAR replace_label=2033 wpn_class_label=131 caption_label=2030 is_melee=1 is_2h=1 END			
		END   
		ELSE PATCH_IF (proficiency = 91) BEGIN //short sword ->blades
			WRITE_BYTE 0x0031 90
			LPF PTCH_WPN INT_VAR replace_label=2034 wpn_class_label=132 caption_label=2030 is_melee=1 is_light=1 END			
		END 
		ELSE PATCH_IF (proficiency = 96 AND stack=1) BEGIN //daggers ->blades
			WRITE_BYTE 0x0031 90
			LPF PTCH_WPN INT_VAR replace_label=2035 wpn_class_label=132 caption_label=2030 is_melee=1 is_light=1 is_fist=1 END			
		END 
		ELSE PATCH_IF (proficiency = 96 AND stack>1) BEGIN //throwing daggers ->missile weapons
			WRITE_BYTE 0x0031 98
			LPF DELETE_EFFECT INT_VAR match_opcode = 1 match_parameter1=2 match_parameter2=1 END //remove additional attacks
			LPF PTCH_WPN INT_VAR replace_label=2035 wpn_class_label=140 caption_label=2030 is_bow=1 END			
		END
		ELSE PATCH_IF (proficiency = 92) BEGIN //axes and throwing axes (same) ->axes
			WRITE_BYTE 0x0031 91
			LPF PTCH_WPN INT_VAR replace_label=2036 wpn_class_label=133 caption_label=2030 is_melee=1 END		
		END     
		ELSE PATCH_IF (proficiency = 95) BEGIN //scimitar/wakizahshi/ninjato ->curved swords
			WRITE_BYTE 0x0031 92
			LPF PTCH_WPN INT_VAR replace_label=2037 wpn_class_label=134 caption_label=2030 is_melee=1 is_light=1 END		
		END
		ELSE PATCH_IF (proficiency = 94) BEGIN //katana ->curved swords
			WRITE_BYTE 0x0031 92
			LPF PTCH_WPN INT_VAR replace_label=2038 wpn_class_label=134 caption_label=2030 is_melee=1 END			
		END  	
		ELSE PATCH_IF (proficiency = 115) BEGIN //club -> blunt weapons
			WRITE_BYTE 0x0031 93
			LPF PTCH_WPN INT_VAR replace_label=2039 wpn_class_label=135 caption_label=2030 is_melee=1 is_light=1 END			
		END   
		ELSE PATCH_IF (proficiency = 101) BEGIN //mace -> blunt weapons
			WRITE_BYTE 0x0031 93
			LPF PTCH_WPN INT_VAR replace_label=2040 wpn_class_label=135 caption_label=2030 is_melee=1 END		
		END  
		ELSE PATCH_IF (proficiency = 97) BEGIN //warhammer -> blunt weapons
			WRITE_BYTE 0x0031 93
			LPF PTCH_WPN INT_VAR replace_label=2041 wpn_class_label=135 caption_label=2030 is_melee=1 END				
		END 
		ELSE PATCH_IF (proficiency = 102) BEGIN //quarterstaff -> blunt weapons
			WRITE_BYTE 0x0031 93
			LPF PTCH_WPN INT_VAR replace_label=2042 wpn_class_label=135 caption_label=2030 is_melee=1 is_light=1 is_2h=1 is_fist=1 END				
		END 
		ELSE PATCH_IF (proficiency = 100) BEGIN //flail/morning star -> spiked weapons
			WRITE_BYTE 0x0031 94
			LPF PTCH_WPN INT_VAR replace_label=2043 wpn_class_label=136 caption_label=2030 is_melee=1 END			
		END   
		ELSE PATCH_IF (proficiency = 98) BEGIN //spear -> polearms
			LPF UPDATE_WEAPON_DMG INT_VAR match_dice_number = 1 match_dice_size = 6 target_dice_number = 1 target_dice_size = 8 END
			LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~1d6~  new_substring=~1d8~ END
			LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~1d6~  new_substring=~1d8~ END
			
			WRITE_BYTE 0x0031 95
			LPF PTCH_WPN INT_VAR replace_label=2044 wpn_class_label=137 caption_label=2030 is_light=1 is_melee=1 is_2h=1 END					
		END 
		ELSE PATCH_IF (proficiency = 99) BEGIN //halberd -> polearms
			LPF UPDATE_WEAPON_DMG INT_VAR match_dice_number = 1 match_dice_size = 10 target_dice_number = 1 target_dice_size = 12 END
			LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~1d10~  new_substring=~1d12~ END
			LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~1d10~  new_substring=~1d12~ END
			WRITE_BYTE 0x0031 95
			LPF PTCH_WPN INT_VAR replace_label=2045 wpn_class_label=137 caption_label=2030 is_melee=1 is_2h=1 END			
		END    
		ELSE PATCH_IF (proficiency = 103) BEGIN //crossbow -> crossbows
			WRITE_BYTE 0x0031 96
			PATCH_IF (~%SOURCE_FILE%~ STRING_EQUAL ~XBOW06.ITM~) BEGIN
				LPF ALTER_ITEM_EFFECT INT_VAR check_globals=1 match_opcode=1 parameter1=7 END  //fix apr (xbow of speed)
			END
			ELSE BEGIN
				LPF ALTER_ITEM_EFFECT INT_VAR check_globals=1 match_opcode=1 parameter1=6 END  //fix apr
		    END
			WRITE_BYTE 0x0020 0 //allow mages and thiefs to use xbow 
			LPF PTCH_WPN INT_VAR replace_label=2046 wpn_class_label=138 caption_label=2030 is_bow=1 END		
		END 
		ELSE PATCH_IF (proficiency = 105) BEGIN //shortbow -> bows
			WRITE_BYTE 0x0031 97
			LPF DELETE_EFFECT INT_VAR match_opcode = 1 match_parameter1=2 match_parameter2=1 END //remove additional attacks
			LPF PTCH_WPN INT_VAR replace_label=2047 wpn_class_label=139 caption_label=2030 is_bow=1 END			
		END    		
		ELSE PATCH_IF (proficiency = 104) BEGIN //longbow -> bows
			WRITE_BYTE 0x0031 97
			LPF DELETE_EFFECT INT_VAR match_opcode = 1 match_parameter1=2 match_parameter2=1 END //remove additional attacks
			LPF PTCH_WPN INT_VAR replace_label=2048 wpn_class_label=139 caption_label=2030 is_bow=1 END	
			READ_SHORT 0x0026 min_str
			//for composite bows set min str to 14
			PATCH_IF (min_str=18) BEGIN
				WRITE_SHORT 0x0026 14
				LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~18 Strength~  new_substring=~14 Strength~ END
				LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~18 Strength~  new_substring=~14 Strength~ END
			END
		END 
		ELSE PATCH_IF (proficiency = 106) BEGIN //darts -> missile weapons
			WRITE_BYTE 0x0031 98
			LPF DELETE_EFFECT INT_VAR match_opcode = 1 match_parameter1=3 match_parameter2=1 END //remove additional attacks
			LPF PTCH_WPN INT_VAR replace_label=2049 wpn_class_label=140 caption_label=2030 is_bow=1 END				
		END
		ELSE PATCH_IF (proficiency = 107) BEGIN //sling -> missile weapons
			WRITE_BYTE 0x0031 98
			LPF ALTER_ITEM_EFFECT INT_VAR check_globals=1 match_opcode=1 parameter1=6 END //fix apr
			LPF PTCH_WPN INT_VAR replace_label=2050 wpn_class_label=140 caption_label=2030 is_bow=1 END				
		END  		
		ELSE PATCH_IF (category = 28) BEGIN//fist weapons
			LPF PTCH_WPN INT_VAR is_melee=1 is_light=1 is_fist=1 END			
		END
		ELSE PATCH_IF (category = 0x001f) BEGIN//bolts
			WRITE_BYTE 0x0020 0 //allow mages and thiefs to use bolts
		END
		ELSE PATCH_IF (category = 0x0007) BEGIN//helms
			WRITE_BYTE 0x001b (flags4 BOR 0b00000010) //remove critical hit protection from helms
			LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Protects against critical hits~  new_substring=~Does not protect against critical hits~ END
		    LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Protects against critical hits~  new_substring=~Does not protect against critical hits~ END
		END
		ELSE PATCH_IF (category = 0x000c) BEGIN//shields ->supress effects of shield spell
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=321 target=1 duration=1 timing=2 STR_VAR resource=~SHLD_AC~ END
				LPF ADD_ITEM_EQEFFECT INT_VAR opcode=206 target=1 duration=1 timing=2 STR_VAR resource=~SHLD_AC~ END
		END
		
		ELSE PATCH_IF (category = 0x0002 AND usable_by_mage) BEGIN//robes (category from armor to cloak and robes)
			WRITE_SHORT 0x001c 0x0043
		END
		
		COPY_EXISTING_REGEXP ~.+\.sto~ ~override~
			READ_LONG 0x002c "purchased_offset"
			READ_LONG 0x0030 "purchased_count"
			SET insert_robes=0
			FOR (i=0;i<purchased_count;i=i+1) BEGIN
				READ_LONG (purchased_offset + i*4)  "category"
				PATCH_IF (category=0x0002 OR category=0x000b) BEGIN //scrolls or armors
					SET insert_robes=1
				END
			END
			PATCH_IF (insert_robes) BEGIN
				WRITE_LONG 0x0030 (purchased_count+1)
				INSERT_BYTES (purchased_offset + purchased_count*4) 4	
				WRITE_LONG (purchased_offset + purchased_count*4) 0x0043
			END
			

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////
//Armor Overhaul
BEGIN @300

INCLUDE ~3ed/REPLACE_SUBSTRING.tph~

COPY ~3ed/Armor~ ~override~


//making dex penalty depending on dex value and armor dex limit
OUTER_FOR (i=10;i<=20;i+=2) BEGIN
	
	COPY_EXISTING ~ARMDEX.EFF~ ~override\ARMDEX%i%.EFF~
		WRITE_LONG 0x001c 0
		WRITE_LONG 0x0020 0
		WRITE_EVALUATED_ASCII 0x0030 ~ARMDEX%i%~
		
		
		COPY_EXISTING ~ARMDEX.SPL~ ~override\ARMDEX%i%.SPL~	
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 duration = 1 STR_VAR resource=~ARMACPE~ END //ac penalty
			FOR (j=i+2;j<=25;j=j+2) BEGIN								
				LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 duration = 1 parameter1 = j parameter2 = 122 STR_VAR resource=~ARMACPE~  END //ac penalty
			END				
END


COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~
  READ_STRREF 0x0008 "unid_name"
  READ_STRREF 0x000c "id_name"

  
  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Leather Armor~) BEGIN //Leather Armor
  	PATCH_PRINT ~%SOURCE_FILE%~
	LPF DELETE_EFFECT INT_VAR match_opcode = 145 END //restores spell casting
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0 parameter1 = 1   parameter2 = 0  duration=1 timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX20~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0 parameter1 = 1   parameter2 = 0  duration=1 timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSPF10~  END
	
	SPRINT ref301 @301
	SPRINT new_substring EVALUATE_BUFFER ~%ref301%Armor Class:~
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
		
  END  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Studded Leather Armor~) BEGIN //Studded Leather Armor
  	PATCH_PRINT ~%SOURCE_FILE%~
	LPF DELETE_EFFECT INT_VAR match_opcode = 145 END //restores spell casting
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0   duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX18~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSKL18~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSPF15~  END
	
	SPRINT ref302 @302
	SPRINT new_substring EVALUATE_BUFFER ~%ref302%Armor Class:~
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
			
  END ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Hide Armor~) BEGIN //Hide Armor
  	PATCH_PRINT ~%SOURCE_FILE%~
	LPF DELETE_EFFECT INT_VAR match_opcode = 145 END //restores spell casting
	LPF DELETE_EFFECT INT_VAR match_opcode = 275 END//hide in shadows
	LPF DELETE_EFFECT INT_VAR match_opcode = 59  END//move silently
	LPF DELETE_EFFECT INT_VAR match_opcode = 90  END//Open Locks
	LPF DELETE_EFFECT INT_VAR match_opcode = 91  END//Find Traps
	LPF DELETE_EFFECT INT_VAR match_opcode = 92  END//PickPockets
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0   duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX16~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSKL16~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSPF20~  END
	

	SPRINT ref303 @303
	SPRINT new_substring EVALUATE_BUFFER ~%ref303%Armor Class:~
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
			
  END  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Chain Mail~ OR ~%unid_name%~ STRING_EQUAL ~Ankheg Plate Mail~) AND NOT ((~%id_name%~ STRING_EQUAL ~Elven Chain Mail~) OR (~%id_name%~ STRING_EQUAL ~Mithral Chain Mail +4~)) BEGIN //Chain Mail or Ankheg armor
  	PATCH_PRINT ~%SOURCE_FILE%~

	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0   duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX16~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSKL16~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSPF20~  END
	
	//disallow combat intuition
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode=321 target=1 duration=1 timing=2 STR_VAR resource=~CMBINTBN~ END
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode=206 target=1 duration=1 timing=2 STR_VAR resource=~CMBINTBN~ END

	SPRINT ref304 @304
	SPRINT new_substring EVALUATE_BUFFER ~%ref304%Armor Class:~
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	
  END  ELSE  PATCH_IF (~%id_name%~ STRING_EQUAL ~Elven Chain Mail~) BEGIN //Elven Chain Mail
  	PATCH_PRINT ~%SOURCE_FILE%~
	LPF DELETE_EFFECT INT_VAR match_opcode = 145 END //restores spell casting
	LPF DELETE_EFFECT INT_VAR match_opcode = 275 END//hide in shadows
	LPF DELETE_EFFECT INT_VAR match_opcode = 59  END//move silently
	LPF DELETE_EFFECT INT_VAR match_opcode = 90  END//Open Locks
	LPF DELETE_EFFECT INT_VAR match_opcode = 91  END//Find Traps
	LPF DELETE_EFFECT INT_VAR match_opcode = 92  END//PickPockets
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0   duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX18~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSKL18~  END
		
	SPRINT ref3041 @3041
	SPRINT new_substring EVALUATE_BUFFER ~%ref3041%Armor Class:~
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Pick Pockets: -20%~  new_substring=~Pick Pockets: -5%~ END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Pick Pockets: -20%~  new_substring=~Pick Pockets: -5%~ END
	
  END  ELSE  PATCH_IF (~%id_name%~ STRING_EQUAL ~Mithral Chain Mail +4~) BEGIN //Drizzt mithral mail
  	PATCH_PRINT ~%SOURCE_FILE%~
	LPF DELETE_EFFECT INT_VAR match_opcode = 145 END //restores spell casting
	LPF DELETE_EFFECT INT_VAR match_opcode = 144 END //restore thieving skills
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter1 = 0   parameter2 = 2 duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX18~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSKL18~  END


	SPRINT ref304 @304
	SPRINT new_substring EVALUATE_BUFFER ~%ref304%Armor Class:~
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	SPRINT ref3042 @3042
	SPRINT new_substring EVALUATE_BUFFER ~%ref3042%Armor Class:~	
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	
  END  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Splint Mail~) BEGIN //Splint Mail
  	PATCH_PRINT ~%SOURCE_FILE%~
	LPF DELETE_EFFECT INT_VAR match_opcode = 145 END //restores spell casting
	LPF DELETE_EFFECT INT_VAR match_opcode = 144 END //restore thieving skills
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0   duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX14~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSKL14~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSPF30~  END

	//disallow combat intuition
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode=321 target=1 duration=1 timing=2 STR_VAR resource=~CMBINTBN~ END
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode=206 target=1 duration=1 timing=2 STR_VAR resource=~CMBINTBN~ END
	
	SPRINT ref305 @305
	SPRINT new_substring EVALUATE_BUFFER ~%ref305%Armor Class:~
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Armor Class:~  new_substring END

  END  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Plate Mail~) BEGIN //Plate Mail
  	PATCH_PRINT ~%SOURCE_FILE%~
	LPF DELETE_EFFECT INT_VAR match_opcode = 145 END //restores spell casting
	LPF DELETE_EFFECT INT_VAR match_opcode = 144 END //restore thieving skills
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0   duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX12~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSKL12~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSPF40~  END
	
	//disallow combat intuition
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode=321 target=1 duration=1 timing=2 STR_VAR resource=~CMBINTBN~ END
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode=206 target=1 duration=1 timing=2 STR_VAR resource=~CMBINTBN~ END
	
	SPRINT ref306 @306
	SPRINT new_substring EVALUATE_BUFFER ~%ref306%Armor Class:~
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	
  END  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Full Plate Armor~) BEGIN //Full Plate
  	PATCH_PRINT ~%SOURCE_FILE%~
	LPF DELETE_EFFECT INT_VAR match_opcode = 145 END //restores spell casting
	LPF DELETE_EFFECT INT_VAR match_opcode = 144 END //restore thieving skills
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0   duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX10~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSKL10~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 272 target = 1  power = 0  parameter1 = 1   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMSPF50~  END

	//disallow combat intuition
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode=321 target=1 duration=1 timing=2 STR_VAR resource=~CMBINTBN~ END
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode=206 target=1 duration=1 timing=2 STR_VAR resource=~CMBINTBN~ END
	
	SPRINT ref307 @307
	SPRINT new_substring EVALUATE_BUFFER ~%ref307%Armor Class:~
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~Armor Class:~  new_substring END
	//set strength requirement to 14 instead of 15
	WRITE_SHORT 0x0026 14
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~15 Strength~  new_substring=~14 Strength~ END
	LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~15 Strength~  new_substring=~14 Strength~ END
  END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~WP_+.*\.itm~ ~override~ //replace weapon of polymorphs to provide protection from dex decrease
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 206 target = 1  power = 0  parameter1 = 0   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX10~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 206 target = 1  power = 0  parameter1 = 0   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX12~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 206 target = 1  power = 0  parameter1 = 0   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX14~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 206 target = 1  power = 0  parameter1 = 0   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX16~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 206 target = 1  power = 0  parameter1 = 0   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX18~  END
	LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 206 target = 1  power = 0  parameter1 = 0   parameter2 = 0  duration=1  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  STR_VAR  resource = ~ARMDEX20~  END
	
/////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////
// magic fang spell for druid and ranger
BEGIN @400
////////////////////////////////////magic fang////////////////////////////////////////////////

INCLUDE ~3ed/ADD_SPELL_HEADER.tph~

////////////
//magic fang
////////////

//copying icons 
COPY ~3ed/MagicFang/rgmfangb.bam~ ~override~
COPY ~3ed/MagicFang/rgmfangc.bam~ ~override~

SET maxlvl=20

OUTER_FOR (i=1;i<=maxlvl;i+=1) BEGIN
	COPY ~3ed/MagicFang/MFANG.eff~ ~override/MFANG%i%.eff~ WRITE_EVALUATED_ASCII 0x30 ~MFANG%i%~ #8   WRITE_LONG 0x001c i   //copying eff and change caster level
	COPY ~3ed/MagicFang/MFANGBN.spl~ ~override/MFANG%i%.spl~ LPF ALTER_SPELL_EFFECT  INT_VAR  duration = 60*i  END  //copying spl and updating duration
END
COPY ~3ed/MagicFang/MFANG.spl~ ~override~ 

FOR (i=1;i<=maxlvl;i+=1) BEGIN
	SPRINT resource EVALUATE_BUFFER ~MFANG%i%~
	LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=2 target=1 target_count=0 range=1 required_level=i speed=6 projectile=1  STR_VAR icon=~RGMFANGB~ END
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=1 parameter1=103 parameter2=4 header=i  STR_VAR resource END //bear
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=1 parameter1=105 parameter2=4 header=i  STR_VAR resource END //dog
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=1 parameter1=116 parameter2=4 header=i  STR_VAR resource END //spider
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=1 parameter1=117 parameter2=4 header=i  STR_VAR resource END //wolf
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=1 parameter1=118 parameter2=4 header=i  STR_VAR resource END //wyvern
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=1 parameter1=122 parameter2=4 header=i  STR_VAR resource END //lycantrope
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=1 parameter1=137 parameter2=4 header=i  STR_VAR resource END //cat
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=1 parameter1=160 parameter2=4 header=i  STR_VAR resource END //beetle
END
//adding removal of previous casts of magic fang or greater magic fang
FOR (i=1;i<=maxlvl;i+=1) BEGIN
	SPRINT resource EVALUATE_BUFFER ~MFANG%i%~
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=321 target=2 power=10 insert_point=0 STR_VAR resource END 
	SPRINT resource EVALUATE_BUFFER ~GMFANG%i%~
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=321 target=2 power=10 insert_point=0 STR_VAR resource END 
END


//properly adding magic fang spell to spells.ids
ADD_SPELL "override/MFANG.spl" 1  1 DRUID_MAGIC_FANG 
  SAY NAME1 @401
  SAY UNIDENTIFIED_DESC @402
///////////////////////////////////////
///////////////////////////////////////
////////////
//greater magic fang
////////////

//copying icons 
COPY ~3ed/MagicFang/rgmfnggb.bam~ ~override~
COPY ~3ed/MagicFang/rgmfnggc.bam~ ~override~

SET maxlvl=20

OUTER_FOR (i=1;i<=maxlvl;i+=1) BEGIN
	COPY ~3ed/MagicFang/MFANG.eff~ ~override/GMFANG%i%.eff~ WRITE_EVALUATED_ASCII 0x30 ~GMFANG%i%~ #8   WRITE_LONG 0x001c i   //copying eff and change caster level
	COPY ~3ed/MagicFang/GMFANGBN.spl~ ~override/GMFANG%i%.spl~ //copying spl
	LPF ALTER_SPELL_EFFECT  INT_VAR  match_opcode=345 power=3 parameter1=1+i/5 duration = 60*i  END   // updating enchantment bonus and duration
	LPF ALTER_SPELL_EFFECT  INT_VAR  match_opcode=278 power=3 parameter1=1+i/5 duration = 60*i  END   // updating THAC0 bonus  and duration
	LPF ALTER_SPELL_EFFECT  INT_VAR  match_opcode=73  power=3 parameter1=1+i/5 duration = 60*i  END   // updating damage bonus and duration	
	LPF ALTER_SPELL_EFFECT  INT_VAR  match_opcode=9   power=3 duration = 60*i  END   // updating color pulse duration 
END
COPY ~3ed/MagicFang/GMFANG.spl~ ~override~ 

FOR (i=1;i<=maxlvl;i+=1) BEGIN
	SPRINT resource EVALUATE_BUFFER ~GMFANG%i%~
	LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=2 target=1 target_count=0 range=1 required_level=i speed=6 projectile=1  STR_VAR icon=~RGMFNGGB~ END
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=3 parameter1=103 parameter2=4 header=i  STR_VAR resource END //bear
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=3 parameter1=105 parameter2=4 header=i  STR_VAR resource END //dog
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=3 parameter1=116 parameter2=4 header=i  STR_VAR resource END //spider
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=3 parameter1=117 parameter2=4 header=i  STR_VAR resource END //wolf
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=3 parameter1=118 parameter2=4 header=i  STR_VAR resource END //wyvern
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=3 parameter1=122 parameter2=4 header=i  STR_VAR resource END //lycantrope
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=3 parameter1=137 parameter2=4 header=i  STR_VAR resource END //cat
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=177 target=2 power=3 parameter1=160 parameter2=4 header=i  STR_VAR resource END //beetle
END
//adding removal of previous casts
FOR (i=1;i<=maxlvl;i+=1) BEGIN
	SPRINT resource EVALUATE_BUFFER ~MFANG%i%~
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=321 target=2 power=10 insert_point=0 STR_VAR resource END 
	SPRINT resource EVALUATE_BUFFER ~GMFANG%i%~
	LPF ADD_SPELL_EFFECT  INT_VAR  opcode=321 target=2 power=10 insert_point=0 STR_VAR resource END 
END


//properly adding greater magic fang spell to spells.ids
ADD_SPELL "override/GMFANG.spl" 1  3 DRUID_GMAGIC_FANG 
  SAY NAME1 @403
  SAY UNIDENTIFIED_DESC @404
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//spells modification
BEGIN @500

INCLUDE ~3ed/REPLACE_SUBSTRING.tph~
INCLUDE ~3ed/ADD_SPELL_HEADER.tph~
INCLUDE ~3ed/UPDATE_SPELL_SAVES.tph~
INCLUDE ~3ed/ADD_EVASION_TO_SPELL.tph~
INCLUDE ~3ed/CHANGE_SPELL_PROPERTIES.tph~
INCLUDE ~3ed/MODIFY_DISPEL_MAGIC.tph~
	
	//change dispel and remove magic
	LAF MODIFY_DISPEL_MAGIC INT_VAR start_chance=30 per_level_chance=1 start_inquisitor_chance=60 per_lvl_inquisitor_chance=2 END
	STRING_SET 25967  @579 //inquisitor/priest dispel
	STRING_SET 32372  @579 //innate dispel
	STRING_SET 26443  @580 //wizard dispel
	STRING_SET 12145  @580 //wizard sipel scroll
	STRING_SET 25899  @593 //wizard remove magic
	STRING_SET 30267  @593 //wizard remove magic scroll
	
	
	


	//make creatures wake up on sleep
	COPY_EXISTING ~SPWI116.SPL~ ~override~
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=39 parameter2=0 END

	//change finger of death to make 8d6 damage in case of failure independent of level
	COPY ~3ed/Spells/FingerOfDeath/SPWI713.SPL~ ~override~
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @583	
	COPY ~3ed/Spells/FingerOfDeath/SPPR708.SPL~ ~override~
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @584	
	//reduce horror duration to 3 rounds
	COPY_EXISTING ~SPWI205.SPL~ ~override~
		LPF ALTER_SPELL_EFFECT INT_VAR duration_high = 18 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @586
	//remove level dependent saving throws penalty for spook
	COPY_EXISTING ~SPWI125.SPL~ ~override~
		LPF ALTER_SPELL_EFFECT INT_VAR savebonus = 0 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @587
	
	//update cure spells
	COPY ~3ed/Spells/CureSpells/SPPR315.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @561
	COPY ~3ed/Spells/CureSpells/SPPR401.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @562
		STRING_SET 12325 @562 //scroll description
	COPY ~3ed/Spells/CureSpells/SPPR502.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @563	
		STRING_SET 18729 @563 //scroll description
	//update harm spells
	COPY ~3ed/Spells/HarmSpells/SPPR414.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @564	
	COPY ~3ed/Spells/HarmSpells/SPPR510.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @565
	COPY ~3ed/Spells/HarmSpells/SPPR511.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @566
	COPY ~3ed/Spells/HarmSpells/SPPR608.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @567
	//update chill touch
	COPY ~3ed/Spells/ChillTouch/SPWI117a.EFF~ ~override~ 
	COPY ~3ed/Spells/ChillTouch/SPWI117.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @568
		STRING_SET 12186 @568 //scroll description
	//update ghoul touch
	COPY ~3ed/Spells/GhoulTouch/SPWI218.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @569
		STRING_SET 12187 @569 //scroll description
	//update shocking grasp
	COPY ~3ed/Spells/ShockingGrasp/SPWI115.SPL~ ~override~ 
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @570
		STRING_SET 12195 @570 //scroll description
		
	
	//make exaltation into cleric spell	
	COPY_EXISTING ~ohtyr1.SPL~ ~override~
		LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_level=3 spell_type=2 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @571
	//make lathander boon into cleric spell (rename to Diety's boon)
	COPY_EXISTING ~SPCL741.SPL~ ~override~
		LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_level=6 spell_type=2 END
		READ_LONG 0x0008 ~spell_name~
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %spell_name% @572
		STRING_SET_EVALUATE %descr_strref% @573
	//make divine favor into cleric spell
	COPY_EXISTING ~ohtyr2.SPL~ ~override~
		LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_level=5 spell_type=2 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @574	
	//making seeking sword give only one additional apr and creating headers for levels 5-9 (also make lvl 3 cleric spell)
	COPY_EXISTING ~SPCL731.SPL~ ~override~ 
		LPF ALTER_SPELL_HEADER INT_VAR  header=1 min_level=10 END
		FOR (i=10;i>=6;i=i - 1) BEGIN
			LPF ADD_SPELL_HEADER INT_VAR  insert_point=1 copy_header=1 END
			LPF ALTER_SPELL_HEADER INT_VAR  header=2 min_level=i END
			LPF ALTER_SPELL_EFFECT INT_VAR header = 2 duration_high=6*i END
		END
		LPF ALTER_SPELL_HEADER INT_VAR  header=1 min_level=1 END
		LPF ALTER_SPELL_EFFECT INT_VAR header = 1 duration_high=30 END
		
		LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_level=3 spell_type=2 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @575
    //make strom shield into cleric spell
	COPY_EXISTING ~SPCL721.SPL~ ~override~
		LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_level=6 spell_type=2 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @576		
	//add stoneskin as possible 6 level cleric spell
	COPY_EXISTING ~SPWI408.SPL~ ~override/PRSTNSK.SPL~
		LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_level=6 spell_type=2 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @577	
	
	//add haste as lvl4 potential cleric spell
	COPY_EXISTING ~SPWI305.SPL~ ~override/PRHASTE.SPL~
		LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_level=4 spell_type=2 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @578		
	
	
	//change earthquake to work through 3 separate effects
	COPY ~3ed/Spells/Earthquake~ ~override~
	
	COPY ~3ed/Spells/Savingthrows/IMMDMG.EFF~ ~override~
	//replace spell saves to breath weapon for these damaging spells
	COPY ~3ed/Spells/Savingthrows/BreathWeaponSaves.2DA~ ~override~ 
		COUNT_2DA_ROWS 2 n_rows			
		FOR (i=0;i<n_rows;i=i+1) BEGIN
			READ_2DA_ENTRY %i% 0 3 spell_name 
			READ_2DA_ENTRY %i% 2 3 scroll_name 
				INNER_ACTION BEGIN
					COPY_EXISTING ~%spell_name%.SPL~ ~override~
						LPF UPDATE_SPELL_SAVES INT_VAR original_save=0b00001 target_save=0b00010 END
						LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~vs. Spell~  new_substring=~vs. Breath Weapon~ END
					ACTION_IF !(~%scroll_name%~ STRING_EQUAL ~x~) BEGIN
						COPY_EXISTING ~%scroll_name%.ITM~ ~override~
						LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~vs. Spell~  new_substring=~vs. Breath Weapon~ END
					END
				END
		END
		
		
	COPY ~3ed/Spells/Savingthrows/DeathSaves.2DA~ ~override~ 
		COUNT_2DA_ROWS 2 n_rows			
		FOR (i=0;i<n_rows;i=i+1) BEGIN
			READ_2DA_ENTRY %i% 0 3 spell_name 
			READ_2DA_ENTRY %i% 2 3 scroll_name 
				INNER_ACTION BEGIN
					COPY_EXISTING ~%spell_name%.SPL~ ~override~
						LPF UPDATE_SPELL_SAVES INT_VAR match_opcode=134 original_save=0b00001 target_save=0b10000 END //stone to flesh
						LPF UPDATE_SPELL_SAVES INT_VAR original_save=0b00001 target_save=0b00100 END
						LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~vs. Spell~  new_substring=~vs. Death~ END				
					ACTION_IF !(~%scroll_name%~ STRING_EQUAL ~x~) BEGIN
						COPY_EXISTING ~%scroll_name%.ITM~ ~override~
						LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~vs. Spell~  new_substring=~vs. Death~ END
					END
					
				END
		END
		
		
	COPY ~3ed/Spells/Savingthrows/PolySaves.2DA~ ~override~ 
		COUNT_2DA_ROWS 2 n_rows			
		FOR (i=0;i<n_rows;i=i+1) BEGIN
			READ_2DA_ENTRY %i% 0 3 spell_name 
			READ_2DA_ENTRY %i% 2 3 scroll_name 
				INNER_ACTION BEGIN
					COPY_EXISTING ~%spell_name%.SPL~ ~override~
						LPF UPDATE_SPELL_SAVES INT_VAR original_save=0b00001 target_save=0b10000 END
						LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~vs. Spell~  new_substring=~vs. Petrification~ END						
					ACTION_IF !(~%scroll_name%~ STRING_EQUAL ~x~) BEGIN
						COPY_EXISTING ~%scroll_name%.ITM~ ~override~
						LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~vs. Spell~  new_substring=~vs. Petrification~ END
					END
				END
		END
		
		
		
	COPY ~3ed/Spells/Savingthrows/WandItems.2DA~ ~override~ 
		COUNT_2DA_ROWS 2 n_rows			
		FOR (i=0;i<n_rows;i=i+1) BEGIN
			READ_2DA_ENTRY %i% 0 3 item_name 
			
				INNER_ACTION BEGIN
					COPY_EXISTING ~%item_name%.ITM~ ~override~
						LPF UPDATE_SPELL_SAVES INT_VAR original_save=0b00001 target_save=0b1000 END
						LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~vs. Spell~  new_substring=~vs. Wand~ END
						LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0054 STR_VAR substring_to_replace=~vs. Spell~  new_substring=~vs. Wand~ END							
				END
		END
		
	COPY_EXISTING_REGEXP GLOB ~.+\.spl~ ~override~
		LPF ADD_EVASION_TO_SPELL INT_VAR END
		
	COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~  //add evasion to items
		LPF ADD_EVASION_TO_SPELL INT_VAR target_save_type = 10 END //breath and wand saves 

	//change description of specific spells 
	COPY_EXISTING ~SPWI714.SPL~ ~override~ //prismatic spray
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @581
	COPY_EXISTING ~SPWI118.SPL~ ~override~ //chromatic orb
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @582	
		STRING_SET 12174 @582 //scroll description
		
//barkskin
	COPY_EXISTING ~SPPR202.spl~ ~override~
		LPF ADD_SPELL_EFFECT INT_VAR  opcode = 321 power=2 target=2  duration=1 timing=0 resist_dispel=3 insert_point=0 STR_VAR resource=~SPPR202~ END //remove effects from previous cast
		LPF ALTER_SPELL_EFFECT INT_VAR  header = 1 match_opcode = 0 parameter1 = 3 parameter2 = 0 END
		FOR (i=1;i<=4;i+=1) BEGIN   //1,4,5,6 +3 AC
			LPF ALTER_SPELL_EFFECT INT_VAR  header = i match_opcode = 0 parameter1 = 3 parameter2 = 0 END
		END
		FOR (i=5;i<=10;i+=1) BEGIN   //7-12 +4 AC
			LPF ALTER_SPELL_EFFECT INT_VAR  header = i match_opcode = 0 parameter1 = 4 parameter2 = 0 END
		END
		FOR (i=11;i<=18;i+=1) BEGIN   //13-20  +5 AC
			LPF ALTER_SPELL_EFFECT INT_VAR  header = i match_opcode = 0 parameter1 = 5 parameter2 = 0 END
		END
	//allow only druids to cast it 
		READ_BYTE 0x0021 ~cleric_usability~
		WRITE_BYTE 0x0021 (cleric_usability BOR 0b01000000)
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @501
		
//shield
	COPY ~3ed/Spells/Shield/SHLD_AC.eff~ ~override~
	COPY ~3ed/Spells/Shield/SHLD_AC.spl~ ~override~
	COPY ~3ed/Spells/Shield/SPWI114.spl~ ~override~
		READ_LONG 0x0050 ~descr_strref~		
		STRING_SET_EVALUATE %descr_strref% @502
		STRING_SET 12182 @502 //scroll description
	
//Champions strength
	COPY_EXISTING ~SPPR507.spl~ ~override~
		LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete=97 END//remove exceptional strength
		LPF ALTER_SPELL_HEADER INT_VAR target = 5 range = 0 END //target is set to caster
		LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete=145 END //delete spell casting disable (to allow casting innates)
		FOR (i=1;i<=12;i+=1) BEGIN   
			SET cstr_lvl=i+8 
			LPF ALTER_SPELL_EFFECT INT_VAR  header = i match_opcode = 54 parameter1 = (20 - cstr_lvl) parameter2 = 1 END // set thac0 to fighters
			LPF ALTER_SPELL_EFFECT INT_VAR  header = i match_opcode = 44 parameter1 = 4 parameter2 = 0 END //STR bonus	
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 1 target = 2 power = 5 parameter1 = 6 parameter2 = 0 duration = 18*(cstr_lvl) resist_dispel = 3 END //+ 0.5 APR
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 145 target = 2 power = 5 parameter1 = 0 parameter2 = 0 duration = 18*(cstr_lvl) resist_dispel = 3 END // forbid casting wizard spells
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 145 target = 2 power = 5 parameter1 = 0 parameter2 = 1 duration = 18*(cstr_lvl) resist_dispel = 3 END // forbid casting cleric spells
		END
		
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @503
		STRING_SET 22834 @503 //scroll description
//Holy power 
	COPY_EXISTING ~SPPR412.spl~ ~override~
		LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete=97 END//remove exceptional strength
		LPF ALTER_SPELL_HEADER INT_VAR target = 5 range = 0 END //target is set to caster
		FOR (i=1;i<=14;i+=1) BEGIN   
			SET cstr_lvl=i+6 
			LPF ALTER_SPELL_EFFECT INT_VAR  header = i match_opcode = 54 parameter1 = (20 - cstr_lvl)  parameter2=1 END // thac0
			LPF ALTER_SPELL_EFFECT INT_VAR  header = i match_opcode = 44 parameter1 = 2 parameter2 = 0 END //STR bonus	
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 1 target = 2 power = 4 parameter1 = 6 parameter2 = 0 duration = 6*(cstr_lvl) resist_dispel = 3 END // +0.5 APR
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 145 target = 2 power = 4 parameter1 = 0 parameter2 = 0 duration = 6*(cstr_lvl) resist_dispel = 3 END // forbid casting wizard spells
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 145 target = 2 power = 4 parameter1 = 0 parameter2 = 1 duration = 6*(cstr_lvl) resist_dispel = 3 END // forbid casting cleric spells
		END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @5031
		

		
		
	//Tenser's transformation
	COPY_EXISTING ~SPWI603.spl~ ~override~
		LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete=54 END//remove existing  THAC0 bonuses
		LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete=145 END //delete spell casting disable (to allow casting innates)
		FOR (i=1;i<=9;i+=1) BEGIN   
			SET cstr_lvl=i+11
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 54 target = 1 power = 6 parameter1 = (18 - cstr_lvl)  parameter2 = 1 duration = 6 *(cstr_lvl) resist_dispel = 3 END // thac0	
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 1 target = 1 power = 6 parameter1 = 1 parameter2 = 0 duration = 6*(cstr_lvl) resist_dispel = 3 END //+1 APR
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 145 target = 1 power = 6 parameter1 = 0 parameter2 = 0 duration = 6*(cstr_lvl) resist_dispel = 3 END // forbid casting wizard spells
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 145 target = 1 power = 6 parameter1 = 0 parameter2 = 1 duration = 6*(cstr_lvl) resist_dispel = 3 END // forbid casting cleric spells
		END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @504
	//Call Lightning (allow to cast indoors reduce dmg to 1d6 per level up to 10d6)
	COPY_EXISTING ~SPPR302.spl~ ~override~
		READ_BYTE 0x0019  outdoor_flag//outdoors flag in bit 5
		WRITE_BYTE 0x0019 (outdoor_flag BAND 0b11011111)
		
		FOR (i=11;i<=18;i=i+1) BEGIN
			LPF DELETE_SPELL_HEADER INT_VAR header_type=1 min_level=i END
		END
		
		FOR (i=5;i<=10;i=i+1) BEGIN
			SET k=i - 4
			LPF ALTER_SPELL_EFFECT INT_VAR header=k match_opcode=12 dicenumber=(i/2) dicesize=6 parameter1=2*(i - 2*(i/2)) END
		END
				
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @505
		
		
	//Goodberry spell- change spell description
	COPY_EXISTING ~SPPR207.spl~ ~override~
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @506	//change description
	//Berry item - change 1 hp to 3 and description
	COPY_EXISTING ~GBERRY.itm~ ~override~	
		LPF ALTER_EFFECT INT_VAR match_opcode=17 parameter1=3 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @5061
	
//adding sleep to druid spellbook 
	COPY_EXISTING ~SPWI116.SPL~ ~override/DRSLEEP.SPL~
		SAY UNIDENTIFIED_DESC @585 //description
			WRITE_BYTE 0x001e 0b00000000
			WRITE_BYTE 0x0021 0b01000000
			WRITE_SHORT 0x001c 2
		ADD_SPELL "override/DRSLEEP.spl" 1  1 DRUID_SLEEP
//adding camoufalge do druid spellbook	
	COPY ~3ed/Spells/Camouflage/DRCMFLGB.bam~ ~override~
	COPY ~3ed/Spells/Camouflage/DRCMFLGC.bam~ ~override~
	
	COPY ~3ed/Spells/Camouflage/DRCMFLG.spl~ ~override~
		FOR (i=1;i<=20;i=i+1) BEGIN
			LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=2 target=1 target_count=1 range=1 required_level=i speed=2 projectile=1  STR_VAR icon=~DRCMFLGB~ END
			LPF ADD_SPELL_EFFECT  INT_VAR  opcode=142 target=2 power=1 parameter2=58 timing=0 duration=i*60 resist_dispel=3 header=i  END //display icon
			LPF ADD_SPELL_EFFECT  INT_VAR  opcode=275 target=2 power=1 parameter1=20 parameter2=0 timing=0 duration=i*60 resist_dispel=3 header=i  END //+20 hide in shadows
			LPF ADD_SPELL_EFFECT  INT_VAR  opcode=59 target=2 power=1 parameter1=20 parameter2=0 timing=0 duration=i*60 resist_dispel=3 header=i  END //+20 move silently
			LPF ADD_SPELL_EFFECT  INT_VAR  opcode=66 target=2 power=1 parameter1=100 parameter2=0 timing=0 duration=i*60 resist_dispel=3 header=i  END //fade
			LPF ADD_SPELL_EFFECT  INT_VAR  opcode=9 target=2 power=1 parameter1=0 parameter2=1+256*256*40 timing=0 duration=i*60 resist_dispel=3 header=i  END //minor colour pulse
			LPF ADD_SPELL_EFFECT  INT_VAR  opcode=9 target=2 power=1 parameter1=0 parameter2=2+256*256*30 timing=0 duration=i*60 resist_dispel=3 header=i  END //major colour pulse
			LPF ADD_SPELL_EFFECT  INT_VAR  opcode=174 target=1 power=1  timing=0 duration=0 resist_dispel=3 header=i STR_VAR ~EFF_M33~  END //play sound
		END
		SAY NAME1 @507
		SAY UNIDENTIFIED_DESC @508
		ADD_SPELL "override/DRCMFLG.spl" 1  1 DRUID_CAMOUFLAGE 		
		SPRINT resource EVALUATE_BUFFER ~%DEST_RES%~
		
			LPF ADD_SPELL_EFFECT INT_VAR  opcode = 321 power=1 target=2  duration=1 timing=0 resist_dispel=3 insert_point=0 STR_VAR resource END //remove effects from previous cast
			
			
//altering flame blade to give bonus damage instead of creating weapon
	COPY ~3ed/Spells/FlameWeapon/FLMWPN.eff~ ~override~ 
	COPY_EXISTING ~SPPR206.SPL~ ~override~
		LPF ALTER_SPELL_HEADER INT_VAR target=1 END //allow to cast on living actors
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=111  new_opcode=248 target=2 STR_VAR resource = ~FLMWPN~ END //add effect on hit
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=215  target=2 END //move visual effect to targer 
	
		FOR (i=0;i<=10;i=i+1) BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR opcode=249 target=2 power=2 timing=0 duration=24+i*6 resist_dispel=1 header=i+1 STR_VAR resource = ~FLMWPN~ END //add effect on ranged hit
			LPF ADD_SPELL_EFFECT INT_VAR opcode=142 target=2 power=2 parameter2=188 timing=0 duration=24+i*6 resist_dispel=1 header=i+1  END //display weapon icon
			LPF ADD_SPELL_EFFECT INT_VAR opcode=9 target=2 power=2 parameter1=256*255+71*256*256+41*256*256*256 parameter2=16+256*256*30 timing=0 duration=24+i*6 resist_dispel=1 header=i+1  END //major colour pulse //weapon glow
		END
		LPF ADD_SPELL_EFFECT INT_VAR  opcode = 321 power=2 target=2  duration=1 timing=0 resist_dispel=3 insert_point=0 STR_VAR resource=~SPPR206~ END //remove effects from previous cast
		//allow only druids to cast it 
		READ_BYTE 0x0021 ~cleric_usability~
		WRITE_BYTE 0x0021 (cleric_usability BOR 0b01000000)
		READ_LONG 0x0008 ~name_strref~
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %name_strref% @509	//change name 
		STRING_SET_EVALUATE %descr_strref% @510	//change description
		
	
//righteous might
	COPY_EXISTING ~SPPR513.spl~ ~override~
		
		FOR (i=1;i<=12;i+=1) BEGIN   
			SET cstr_lvl=i+8 
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 0 target = 1 power = 5 parameter1 = 2 parameter2 = 0 duration = 6*(cstr_lvl) resist_dispel = 3 END // +2 AC
			//damage resistance
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 86 target = 1 power = 5 parameter1 = 5*(cstr_lvl/3) parameter2 = 0 duration = 6*(cstr_lvl) resist_dispel = 3 END 
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 87 target = 1 power = 5 parameter1 = 5*(cstr_lvl/3) parameter2 = 0 duration = 6*(cstr_lvl) resist_dispel = 3 END 
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 88 target = 1 power = 5 parameter1 = 5*(cstr_lvl/3) parameter2 = 0 duration = 6*(cstr_lvl) resist_dispel = 3 END 
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 89 target = 1 power = 5 parameter1 = 5*(cstr_lvl/3) parameter2 = 0 duration = 6*(cstr_lvl) resist_dispel = 3 END 
		
		END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @511

			
	//Allow only druids to cast animal summoning 1,2,3 and poison
	COPY_EXISTING ~SPPR402.SPL~ ~override~ // animal summoning 1
		READ_BYTE 0x0021 ~cleric_usability~
		WRITE_BYTE 0x0021 (cleric_usability BOR 0b01000000)
	COPY_EXISTING ~SPPR411.SPL~ ~override~ // poison
		READ_BYTE 0x0021 ~cleric_usability~
		WRITE_BYTE 0x0021 (cleric_usability BOR 0b01000000)
	COPY_EXISTING ~SPPR501.SPL~ ~override~ // animal summoning 2
		READ_BYTE 0x0021 ~cleric_usability~
		WRITE_BYTE 0x0021 (cleric_usability BOR 0b01000000)
	COPY_EXISTING ~SPPR602.SPL~ ~override~ // animal summoning 1
		READ_BYTE 0x0021 ~cleric_usability~
		WRITE_BYTE 0x0021 (cleric_usability BOR 0b01000000)
	
	//make flame strike aoe spell
	COPY_EXISTING ~SPPR503.SPL~ ~override~
		LPF ALTER_SPELL_HEADER INT_VAR projectile=215 target=4 END
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=215 parameter2=2 END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @591	//change description
		STRING_SET 12319 @591 //scroll description
		
	//allow druids to cast flame strike as 4th level spell
	COPY_EXISTING ~SPPR503.SPL~ ~override/DRDFSTK.SPL~ // flamestrike
		READ_BYTE 0x0021 ~cleric_usability~
		WRITE_BYTE 0x0021 ((cleric_usability BAND BNOT(0b10000000)) BOR (0b01000000))
		
		WRITE_LONG 0x0034 4 //spell level		
		LPF ADD_SPELL_HEADER  INT_VAR  copy_header=3 insert_point=0 END
		LPF ADD_SPELL_HEADER  INT_VAR  copy_header=3 insert_point=0 END
			LPF ALTER_SPELL_EFFECT INT_VAR header=1  match_opcode=12 new_opcode=0 END 
			LPF DELETE_SPELL_EFFECT INT_VAR  match_opcode=0 END
			LPF ADD_SPELL_EFFECT INT_VAR  insert_point=3 header = 1 opcode = 12 target = 2 power = 5 parameter1 = 0 parameter2 = 524288 duration = 0 timing=1 resist_dispel = 1 dicenumber=4 dicesize=8 savingthrow=0b00000001000000000000000000000000 END    //4d8 no save
			LPF ADD_SPELL_EFFECT INT_VAR  insert_point=4 header = 1 opcode = 12 target = 2 power = 5 parameter1 = 0 parameter2 = 524288 duration = 0 timing=1 resist_dispel = 1 dicenumber=3 dicesize=8 savingthrow=0b00000001000000000000000000000010 END //3d8 with save vs breath
		
		LPF ALTER_SPELL_HEADER INT_VAR header=2 min_level=8 END
			LPF ALTER_SPELL_EFFECT INT_VAR header=2  dicenumber=4 END //2 x 4d8 at lvl 8
		LPF ALTER_SPELL_HEADER INT_VAR header=3 min_level=9 END
		LPF ALTER_SPELL_EFFECT INT_VAR power=4 END //set power lvl to 4
		SAY UNIDENTIFIED_DESC @512
		
		ADD_SPELL "override/DRDFSTK.spl" 1  4 DRUID_FLAMESTRIKE
		
	
	//change magic stone to create magical bullets
	COPY ~3ed/Spells/MagicStone~ ~override~
	COPY_EXISTING ~SPPR106.SPL~ ~override~
		FOR (i=2;i<=20;i+=1) BEGIN
			LPF ADD_SPELL_HEADER INT_VAR  copy_header=1 END
			LPF ALTER_SPELL_HEADER INT_VAR  header=i min_level=i END
			LPF ALTER_SPELL_EFFECT INT_VAR  header = i match_opcode = 122 parameter1 = 2*i END 
		END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @513
	COPY_EXISTING ~MGCSTN.ITM~ ~override~
		SET   item_name=RESOLVE_STR_REF (@514)
		SET   item_descr=RESOLVE_STR_REF (@515)
		WRITE_LONG 0x0008 item_name
		WRITE_LONG 0x000c item_name
		WRITE_LONG 0x0050 item_descr
		WRITE_LONG 0x0054 item_descr
		
	//allow armor and ghost armor to be cast on any party members
	//SPWI102
	//SPWI317
	COPY_EXISTING ~SPWI102.SPL~ ~override~
		LPF ALTER_SPELL_HEADER INT_VAR target=1 END
		LPF ALTER_SPELL_EFFECT INT_VAR target=2 END
		LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Area of Effect: The caster~  new_substring=~Area of Effect: 1 Creature~ END
		LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Range: 0~  new_substring=~Range: Touch~ END
		
	COPY_EXISTING ~SPWI317.SPL~ ~override~
		LPF ALTER_SPELL_HEADER INT_VAR target=1 END
		LPF ALTER_SPELL_EFFECT INT_VAR target=2 END
		LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Area of Effect: The caster~  new_substring=~Area of Effect: 1 Creature~ END
		LPF REPLACE_SUBSTRING INT_VAR strref_offset=0x0050 STR_VAR substring_to_replace=~Range: 0~  new_substring=~Range: Touch~ END


	//add chain lightning to druid spell list
	COPY_EXISTING ~SPDR601.SPL~ ~override~ 
		SAY UNIDENTIFIED_DESC @516 //description
		READ_BYTE 0x0021 ~cleric_usability~
		WRITE_BYTE 0x0021 ((cleric_usability BAND BNOT(0b10000000)) BOR (0b01000000))
		ADD_SPELL "override/SPDR601.spl" 1  6 DRUID_CHAINLIGHTNING
	
	//make burning hands area of effect spell
	ADD_PROJECTILE      ~3ed/Spells/BurningHands/BRNHND.PRO~
	COPY_EXISTING ~SPWI103.SPL~ ~override~
		SET projectile=BRNHND
		LPF ALTER_SPELL_HEADER INT_VAR projectile target=4 range=8 END //updating projectile (aoe, range -15 ft - somehow for cone-shaped spells the distance should be divided by 2)
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @517
		STRING_SET 12141 @517 //scroll description
		
	//add burning hands to druid spell list
		
	COPY_EXISTING ~SPWI103.SPL~ ~override/DRDBHDS.SPL~ 
		SAY UNIDENTIFIED_DESC @518 //description
		WRITE_BYTE 0x001e 0b00000000
		WRITE_BYTE 0x0021 0b01000000
		WRITE_SHORT 0x001c 2
		ADD_SPELL "override/DRDBHDS.spl" 1  1 DRUID_BURN_HANDS
	
	//remove bless from druid spell list
	COPY_EXISTING ~SPPR101.SPL~ ~override~ 
		WRITE_BYTE 0x0021 0b10000000
		
		
	//nerf draw upon holy might
	COPY_EXISTING_REGEXP GLOB ~\(SPPR214.SPL\)\|\(SPIN103.SPL\)~ ~override~ 
		LPF ALTER_SPELL_HEADER INT_VAR header=2 min_level=5 END
		LPF ALTER_SPELL_HEADER INT_VAR header=3 min_level=10 END
		LPF ALTER_SPELL_HEADER INT_VAR header=4 min_level=15 END
		LPF ALTER_SPELL_HEADER INT_VAR header=5 min_level=20 END
		LPF ALTER_SPELL_HEADER INT_VAR header=6 min_level=40 END
		LPF DELETE_SPELL_HEADER INT_VAR header_type=~-1~ min_level=40 END
		
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @519
		
	//update spiritual hammer description (it is throwing now and gets +1 per 3 caster levels)
	COPY_EXISTING ~SPPR213.SPL~ ~override~
		FOR (i=6;i<=20;i=i+1) BEGIN
			SET k= i - 2
			SET enc = i/3
			PATCH_IF enc>5 BEGIN
				SET enc=5
			END			
			SPRINT resource EVALUATE_BUFFER ~SHAMMR%enc%~
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=111 header=k STR_VAR resource END
		END
		STRING_SET 25901 @520
	//do the same for innate
	COPY_EXISTING ~SPPR213.SPL~ ~override/SPIN113.SPL~
		WRITE_SHORT 0x001c 4
		LPF ALTER_SPELL_HEADER INT_VAR location=4 END
		
	//disallow druid to cast defensive harmony
	COPY_EXISTING ~SPPR406.SPL~ ~override~ 
		WRITE_BYTE 0x0021 0b10000000

		
	//make cloak of fear work in 5ft radius with -4 save bonus
	COPY_EXISTING ~SPPR416.SPL~ ~override~ 
		SET savebonus= 0 - 4
		LPF ALTER_SPELL_HEADER INT_VAR projectile=218 END
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=24  savebonus END
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=142 savebonus END
		LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=174 savebonus END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @521

		
	//allow druid to cast hold monster as 4-th level spell	
	COPY_EXISTING ~SPWI507.SPL~ ~override/DRHLDMN.SPL~
		WRITE_BYTE 0x0021 (0b01000000)	//dissalow cleric casting it 
		WRITE_BYTE 0x001F (0b00000000)  //clear invoker flag
		WRITE_LONG 0x0034 4 //spell level	
		WRITE_SHORT 0x001c 2// priest spell
		SAY UNIDENTIFIED_DESC @522 //description
		LPF ALTER_SPELL_EFFECT INT_VAR power=4 END //set power lvl to 4
		ADD_SPELL "override/DRHLDMN.spl" 1  4 DRUID_HOLD_MONSTER
	//allow druid to cast ice storm as 4-th level spell
	COPY_EXISTING ~SPWI404.SPL~ ~override/DRDIST.SPL~
		WRITE_BYTE 0x0021 (0b01000000)	//dissalow cleric casting it 
		WRITE_BYTE 0x001F (0b00000000)  //clear invoker flag	
		WRITE_SHORT 0x001c 2// priest spell
		SAY UNIDENTIFIED_DESC @592 //description
		ADD_SPELL "override/DRDIST.spl" 1  4 DRUID_ICE_STORM	
		
	//update sun soulray ability
	OUTER_FOR (i=1;i<=5;i=i+1) BEGIN
		COPY ~3ed/Spells/SunSoulray/SUNSLR.EFF~ ~override/SUNSLR%i%.EFF~
			WRITE_LONG 0x0038 i
	END
	COPY ~3ed/Spells/SunSoulray/SUNSLR.SPL~ ~override/SPCL236.SPL~
		FOR (i=2;i<=10;i=i+1) BEGIN
			LPF ADD_SPELL_HEADER INT_VAR  copy_header=1 END
			LPF ALTER_SPELL_HEADER INT_VAR  header=i min_level=i END
			LPF ALTER_SPELL_EFFECT INT_VAR  header = i match_opcode = 12 dicenumber = (i/2) END  //damage
			
			//undead damage
			SET d=(i+1)/2
			SPRINT resource EVALUATE_BUFFER ~SUNSLR%d%~
			LPF ADD_SPELL_EFFECT INT_VAR  header = i opcode = 177 target=2 power=0 parameter1 = 4 parameter2=3 timing=1 resist_dispel=0 STR_VAR resource END

		END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @523
		
	//make it into searing light  spell (cleric level 3)
	COPY_EXISTING ~SPCL236.SPL~ ~override/SRLIGHT.SPL~
		LPF ALTER_SPELL_EFFECT INT_VAR  power=3 END //lvl3 
		LPF ALTER_SPELL_HEADER INT_VAR speed=3 location=2 END //casting speed 3
		SAY NAME1 @524
		SAY UNIDENTIFIED_DESC @525
		WRITE_BYTE 0x0021 0b10000000 //disallow druids to cast it
		WRITE_SHORT 0x0022 15 //invocation
		WRITE_BYTE 0x0025 6//invocation
		WRITE_BYTE 0x0027 10//offensive damage
		WRITE_BYTE 0x0034 3 //spell level
		WRITE_SHORT 0x001c 2 //cleric spell
		ADD_SPELL "override/SRLIGHT.spl" 1  3 CLERIC_SEARING_LIGHT
		
	//update aid to add hp based on level and be cumulitive with bless
	COPY_EXISTING ~SPPR201.SPL~ ~override~
		LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete=129 END
		FOR (i=1;i<=20;i=i+1) BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR opcode=325 power=2  target=2 parameter1=1 parameter2=0 timing=0 resist_dispel=3 duration=6*(i+1) header=i insert_point=0 END //saves bonus
			LPF ADD_SPELL_EFFECT INT_VAR opcode=278 power=2  target=2 parameter1=1 parameter2=0 timing=0 resist_dispel=3 duration=6*(i+1) header=i insert_point=0 END //thaco bonus
			SET hp_bonus=i
			PATCH_IF (hp_bonus>10) BEGIN
				SET hp_bonus=10
			END
			SET hp_bonus=hp_bonus+5
			LPF ADD_SPELL_EFFECT INT_VAR opcode=18 power=2  target=2 parameter1=hp_bonus parameter2=0 timing=0 resist_dispel=3 duration=6*(i+3) header=i insert_point=0  END //hp
		END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=321 power=2  target=2 parameter2=0 timing=0 resist_dispel=3 duration=1 insert_point=0 STR_VAR resource=~SPPR201~ END //remove previous casts
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @526
	
	//make aid 10' radius
	COPY ~3ed/Spells/MassAid~ ~override~
	COPY_EXISTING ~SPPR201.SPL~ ~override/MASSAID.SPL~
		SAY NAME1 @527
		SAY UNIDENTIFIED_DESC @528
		WRITE_BYTE 0x0034 3 //spell level
		FOR (i=1;i<=20;i=i+1) BEGIN
			SET hp_bonus=i
			PATCH_IF (hp_bonus>15) BEGIN
				SET hp_bonus=15
			END
			SET hp_bonus=hp_bonus+5
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=18  parameter1=hp_bonus header=i END //hp
		END
		LPF ALTER_SPELL_EFFECT INT_VAR power=3 END //update power
		LPF ALTER_SPELL_HEADER INT_VAR projectile=158 target=5 STR_VAR icon=~MASSAIDB~ END //30 ft radius around caster, icon
		WRITE_ASCII 0x003a ~MASSAIDC~ #8 //spellbook icon
		
		ADD_SPELL "override/MASSAID.spl" 1  3 CLERIC_MASS_AID		
			SPRINT resource EVALUATE_BUFFER ~%DEST_RES%~		
			LPF ADD_SPELL_EFFECT INT_VAR  opcode = 321 power=1 target=2  duration=1 timing=0 resist_dispel=3 insert_point=0 STR_VAR resource END //remove effects from previous cast
	
		COPY_EXISTING ~SPPR201.SPL~ ~override~
			LPF ADD_SPELL_EFFECT INT_VAR  opcode = 321 power=1 target=2  duration=1 timing=0 resist_dispel=3 insert_point=0 STR_VAR resource END //remove effects of mass aid from recipient of simple aid
			
	//make animate dead call 2 skeletons per cast
	COPY_EXISTING_REGEXP GLOB ~\(SPCL106.SPL\)\|\(SPPR301.SPL\)\|\(SPWI501.SPL\)~ ~override~ 
		LPF CLONE_EFFECT INT_VAR check_globals=0 END
		READ_LONG 0x0050 ~descr_strref~
		INNER_ACTION BEGIN
			STRING_SET_EVALUATE %descr_strref% @529
		END
		STRING_SET 12185 @529
	//make absorb health a melee spell
	COPY_EXISTING ~SPCL102.SPL~ ~override~ 
		LPF ALTER_SPELL_HEADER INT_VAR range=1 END
	
	//make lay on hands cure disiese starting from level 6, and poison starting from level 9
	COPY_EXISTING ~SPCL211.SPL~ ~override~ 
		READ_SHORT 0x0068 "Nheaders" //number of headers
		FOR (i=6;i<=Nheaders;i=i+1) BEGIN 
			LPF ADD_SPELL_EFFECT INT_VAR  opcode = 79 power=1 target=2  duration=1 timing=0 resist_dispel=3 insert_point=i  END //disease 
			PATCH_IF (i>=9) BEGIN
				LPF ADD_SPELL_EFFECT INT_VAR  opcode = 11 power=1 target=2  duration=1 timing=0 resist_dispel=3 insert_point=i  END  //poison
			END
		END
		READ_LONG 0x0050 ~descr_strref~
		STRING_SET_EVALUATE %descr_strref% @530
			
	

		
	//Spell DC regularization
	INCLUDE ~3ed/ADD_SPELL_PROGRESSIVE_SAVES.tph~
	COPY_EXISTING_REGEXP GLOB ~.+\.spl~ ~override~
		READ_SHORT 0x1c "spell_type"
		PATCH_IF spell_type = 1 OR spell_type = 2 BEGIN
			LPF ADD_SPELL_PROGRESSIVE_SAVES INT_VAR n_copies1=7 caster_level_step=30 END
		END
	
	INCLUDE ~3ed/ADD_PSEPB_FEAT.tph~
	LAF ADD_PSEPB_FEAT INT_VAR min_val=12 max_val=25 step=2 abi_g=128 min_bonus_val=30 bonus_step=30 STR_VAR ability_name=~SPDCW~ target_name=~SPDCW~ END //wizard spells (int)
	LAF ADD_PSEPB_FEAT INT_VAR min_val=12 max_val=25 step=2 abi_g=130 min_bonus_val=30 bonus_step=30 STR_VAR ability_name=~SPDCP~ target_name=~SPDCP~ END //priest spells (wis)
	
	LAF ADD_PSEPB_FEAT INT_VAR min_val=12 max_val=25 step=2 abi_g=132 min_bonus_val=30 bonus_step=30 STR_VAR ability_name=~SPDCW~ target_name=~SPDCS~ END //wizard spells (cha)
	LAF ADD_PSEPB_FEAT INT_VAR min_val=12 max_val=25 step=2 abi_g=132 min_bonus_val=30 bonus_step=30 STR_VAR ability_name=~SPDCP~ target_name=~SPDCH~ END //priest spells (cha)
	
	LAF ADD_PSEPB_FEAT INT_VAR min_val=10 max_val=25 step=2 abi_g=130 min_bonus_val=8 bonus_step=30 STR_VAR ability_name=~SPDCP~ target_name=~SPDCL~ END //priest spells for paladin (wis)
	LAF ADD_PSEPB_FEAT INT_VAR min_val=10 max_val=25 step=2 abi_g=130 min_bonus_val=7 bonus_step=30 STR_VAR ability_name=~SPDCP~ target_name=~SPDCR~ END //priest spells for ranger (wis)
	
	//adding dc and caster level adjustments
	INCLUDE ~3ed/ADD_BONUS_FEATS.tph~
	
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLABMA+.*\)\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRENS.SPL~
						feat_type_file=~SPDCWFT~ caption=~SPDCWIZ~ END
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLABMA01\)\|\(CLABSO+.*\)\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRES.SPL~
						feat_type_file=~SPDCSFT~ caption=~SPDCSOR~ END
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLABBA+.*\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SPDCSFT~ END
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~clabshgs\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SPDCH~ END
						
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLABPR+.*\)\|\(CLABDR+.*\)\|\(OHTYR\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SPDCPFT~ END			
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
						STR_VAR clab=~\(CLABSH+.*\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SPDCHFT~ END							
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=4 d_level=1 add_at_level1=0 
						STR_VAR clab=~\(CLABPA+.*\)\.2DA~ mask_file=~~
						feat_type_file=~~ caption=~SPDCLFT~ END	
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=4 d_level=1 add_at_level1=0 
						STR_VAR clab=~\(CLABRN+.*\)\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRER.SPL~
						feat_type_file=~SPDCRFT~ caption=~SPDCRNG~ END	

	
	INCLUDE ~3ed/UPDATE_PRIEST_DOMAIN.tph~
	OUTER_SET   yes_response=RESOLVE_STR_REF (@500001)
	OUTER_SET   no_response=RESOLVE_STR_REF (@500002)
	
	
	//priest domain
	OUTER_SET k=1
	COPY ~3ed/Feats/PriestDomains/Domains2DA~ ~override~
		DEFINE_ASSOCIATIVE_ARRAY domain_list BEGIN "%k%" => "%SOURCE_RES%" END
		SET k=k+1
		
		
	ACTION_PHP_EACH domain_list AS domain_i =>domain_file_name BEGIN
		OUTER_SET domain_id=domain_i
		OUTER_SPRINT domain_file_2DA EVALUATE_BUFFER ~%domain_file_name%~
		LAF UPDATE_PRIEST_DOMAIN INT_VAR domain_id yes_response no_response STR_VAR domain_file_2DA END	
	END
	
	
	COPY ~3ed/Feats/PriestDomains/domain.d~  ~3ed/Feats/PriestDomains/domain.d~  	
		REPLACE_TEXTUALLY ~%~	~|~
		REPLACE_TEXTUALLY ~|domain_description_str|~ ~~
		REPLACE_TEXTUALLY ~|domain_str|~ ~~
		COMPILE ~3ed/Feats/PriestDomains/domain.d~ 
		
		
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/DFTCRE.CRE~
		WRITE_ASCII 0x248 ~SFTCRE~ #8//override script
		WRITE_ASCII 0x02cc ~domain~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/DFTCRE.EFF~
		WRITE_ASCII 0x0030 ~DFTCRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/DFTCRE.SPL~	
		LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~DFTCRE~ END

	
	//sorceror bloodlines
	
	INCLUDE ~3ed/UPDATE_SORC_BLOODLINE.tph~
	OUTER_SET   yes_response=RESOLVE_STR_REF (@500003)
	OUTER_SET   no_response=RESOLVE_STR_REF (@500004)
	
	OUTER_SET k=1
	COPY ~3ed/Feats/SorcBloodlines/Bloodlines2DA~ ~override~
		DEFINE_ASSOCIATIVE_ARRAY bloodline_list BEGIN "%k%" => "%SOURCE_RES%" END
		SET k=k+1
		
		
	ACTION_PHP_EACH bloodline_list AS bloodline_i =>bloodline_file_name BEGIN
		OUTER_SET bloodline_id=bloodline_i
		OUTER_SPRINT bloodline_file_2DA EVALUATE_BUFFER ~%bloodline_file_name%~
		LAF UPDATE_SORC_BLOODLINE INT_VAR bloodline_id yes_response no_response STR_VAR bloodline_file_2DA END	
	END
	
	
	COPY ~3ed/Feats/SorcBloodlines/bldline.d~  ~3ed/Feats/SorcBloodlines/bldline.d~  	
		REPLACE_TEXTUALLY ~%~	~|~
		REPLACE_TEXTUALLY ~|bloodline_description_str|~ ~~
		REPLACE_TEXTUALLY ~|bloodline_str|~ ~~
		COMPILE ~3ed/Feats/SorcBloodlines/bldline.d~ 
		
		
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/BDFTCRE.CRE~
		WRITE_ASCII 0x248 ~SFTCRE~ #8//override script
		WRITE_ASCII 0x02cc ~bldline~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/BDFTCRE.EFF~
		WRITE_ASCII 0x0030 ~BDFTCRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/BDFTCRE.SPL~	
		LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~BDFTCRE~ END
		
	//apply to sorceror
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
		STR_VAR clab=~CLABMA01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCRES.SPL~
		feat_type_file=~BDFTCRE~ caption=~SRBLDLN~ END
		
		
	//spellfused warden 
	INCLUDE ~3ed/SPLIT_SPELLS_BY_SCHOOL.tph~
	INCLUDE ~3ed/UPDATE_SPELLFUSED_WARDEN_SCHOOLS.tph~
	
	OUTER_SET   yes_response=RESOLVE_STR_REF (@500005)
	OUTER_SET   no_response=RESOLVE_STR_REF (@500006)
	
	LAF SPLIT_SPELLS_BY_SCHOOL END
	OUTER_SET k=1
	COPY ~3ed/Feats/SpellfusedWarden/Schools2DA~ ~override~
		DEFINE_ASSOCIATIVE_ARRAY school_list BEGIN "%k%" => "%SOURCE_RES%" END
		SET k=k+1
		
		
	ACTION_PHP_EACH school_list AS school_i =>school_file_name BEGIN
		OUTER_SET school_id=school_i
		OUTER_SPRINT school_file_2DA EVALUATE_BUFFER ~%school_file_name%~
		LAF UPDATE_SPELLFUSED_WARDEN_SCHOOLS INT_VAR school_id yes_response no_response STR_VAR school_file_2DA END	
	END	
	
	COPY ~3ed/Feats/SpellfusedWarden/school.d~  ~3ed/Feats/SpellfusedWarden/school.d~  	
		REPLACE_TEXTUALLY ~%~	~|~
		REPLACE_TEXTUALLY ~|school_description_str|~ ~~
		REPLACE_TEXTUALLY ~|school_str|~ ~~
		COMPILE ~3ed/Feats/SpellfusedWarden/school.d~ 
		
		
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/SWFTCRE.CRE~
		WRITE_ASCII 0x248 ~SFTCRE~ #8//override script
		WRITE_ASCII 0x02cc ~school~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/SWFTCRE.EFF~
		WRITE_ASCII 0x0030 ~SWFTCRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/SWFTCRE.SPL~	
		LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~SWFTCRE~ END
		
	//apply to sorceror
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=1 add_at_level1=1 
		STR_VAR clab=~CLABDR02\.2DA~ mask_file=~~
		feat_type_file=~~ caption=~SWFTCRE~ END
	
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////Spontaneous casting
//////
BEGIN @600
	INCLUDE ~3ed/ADD_SPELL_HEADER.tph~
	
	
	OUTER_SET cure_level=241
	OUTER_SET summ_level=242
	OUTER_SET bcst_level=243
	OUTER_SET cstk_level=244
	OUTER_SET harm_level=245
	
	//application of heal/harm depeding on alignement or spell burning (ftr/cleric) or spell channeling (ftr/thf)
	COPY ~3ed/SpontaneousCasting/HealHarm/GSPCAST.spl~ ~override~
	COPY ~3ed/SpontaneousCasting/HealHarm/rgspcast.spl~ ~override~
	COPY ~3ed/SpontaneousCasting/HealHarm/rgspcast.bam~ ~override~
    COPY ~3ed/SpontaneousCasting/HealHarm/rgspheal.eff~ ~override~
	COPY ~3ed/SpontaneousCasting/HealHarm/rgspharm.eff~ ~override~
	//Spontaneous Heal
	COPY ~3ed/SpontaneousCasting/HealHarm/rgspheal.bam~ ~override~
	COPY ~3ed/SpontaneousCasting/HealHarm/rgspheal.spl~ ~override~
		SAY NAME1 @601
		SAY UNIDENTIFIED_DESC @602
	//Cure moderate wounds
	COPY ~3ed/SpontaneousCasting/HealHarm/CureModerateWounds/rgspa02b.bam~ ~override~
	COPY ~3ed/SpontaneousCasting/HealHarm/CureModerateWounds/rgspa02c.bam~ ~override~
	COPY ~3ed/SpontaneousCasting/HealHarm/CureModerateWounds/rgspa02.spl~ ~override~
		SAY NAME1 @611
		SAY UNIDENTIFIED_DESC @612
		ADD_SPELL "override/rgspa02.spl" 1  2 CLERIC_CURE_MODERATE
	/////////////////////////////
	//Spontaneous Harm
	COPY ~3ed/SpontaneousCasting/HealHarm/rgspharm.bam~ ~override~
	COPY ~3ed/SpontaneousCasting/HealHarm/rgspharm.spl~ ~override~
		SAY NAME1 @603
		SAY UNIDENTIFIED_DESC @604
	
	//Cause light wounds	
	COPY ~3ed/SpontaneousCasting/HealHarm/CauseLightWounds/rgspb01.itm~ ~override~
		SAY NAME1 @605
	COPY ~3ed/SpontaneousCasting/HealHarm/CauseLightWounds/rgspb01.spl~ ~override~
		SAY NAME1 @605
		SAY UNIDENTIFIED_DESC @606
		ADD_SPELL "override/rgspb01.spl" 1  1 CLERIC_CAUSE_LIGHT
	
	//Cause moderate wounds
	COPY ~3ed/SpontaneousCasting/HealHarm/CauseModerateWounds/rgspb02b.bam~ ~override~
	COPY ~3ed/SpontaneousCasting/HealHarm/CauseModerateWounds/rgspb02c.bam~ ~override~
	COPY ~3ed/SpontaneousCasting/HealHarm/CauseModerateWounds/rgspb02.itm~ ~override~
		SAY NAME1 @607	
	COPY ~3ed/SpontaneousCasting/HealHarm/CauseModerateWounds/rgspb02.spl~ ~override~
		SAY NAME1 @607
		SAY UNIDENTIFIED_DESC @608
		ADD_SPELL "override/rgspb02.spl" 1  2 CLERIC_CAUSE_MODERATE
	
	//Cause medium wounds
	COPY ~3ed/SpontaneousCasting/HealHarm/CauseMediumWounds/rgspb03.itm~ ~override~
		SAY NAME1 @609	
	COPY ~3ed/SpontaneousCasting/HealHarm/CauseMediumWounds/rgspb03.spl~ ~override~
		SAY NAME1 @609
		SAY UNIDENTIFIED_DESC @610
		ADD_SPELL "override/rgspb03.spl" 1  3 CLERIC_CAUSE_MEDIUM
	//////////////////////////////
	//Spontaneous Summon
	COPY ~3ed/SpontaneousCasting/Summon/rgsummon.bam~ ~override~
	COPY ~3ed/SpontaneousCasting/Summon/rgsummon.vvc~ ~override~
	COPY ~3ed/SpontaneousCasting/Summon/rgspsumm.bam~ ~override~
	COPY ~3ed/SpontaneousCasting/Summon/RGSPSUMM.EFF~ ~override~
	COPY ~3ed/SpontaneousCasting/Summon/rgspsumm.spl~ ~override~
		SAY NAME1 @613
		SAY UNIDENTIFIED_DESC @614
	
	
    //Lesser animal summoning 1
	COPY ~3ed/SpontaneousCasting/Summon/AnimalSum1~ ~override~
	COPY ~3ed/SpontaneousCasting/Summon/AnimalSum1/rgspc01.spl~ ~override~
		SAY NAME1 @615
		SAY UNIDENTIFIED_DESC @616
		ADD_SPELL "override/rgspc01.spl" 1  1 DRUID_LESSER_SUMM1
	
	//Lesser animal summoning 2
	COPY ~3ed/SpontaneousCasting/Summon/AnimalSum2~ ~override~
	COPY ~3ed/SpontaneousCasting/Summon/AnimalSum2/rgspc02.spl~ ~override~
		SAY NAME1 @617
		SAY UNIDENTIFIED_DESC @618	
		ADD_SPELL "override/rgspc02.spl" 1  2 DRUID_LESSER_SUMM2
	
	//Lesser animal summoning 3
	COPY ~3ed/SpontaneousCasting/Summon/AnimalSum3~ ~override~
	COPY ~3ed/SpontaneousCasting/Summon/AnimalSum3/rgspc03.spl~ ~override~
		SAY NAME1 @619
		SAY UNIDENTIFIED_DESC @620
		ADD_SPELL "override/rgspc03.spl" 1  3 DRUID_LESSER_SUMM3

	//spontaneous battlecasting
	COPY ~3ed/SpontaneousCasting/BattleCasting/BAM~ ~override~ //bam icons
	COPY ~3ed/SpontaneousCasting/BattleCasting/RGSPBCST.EFF~ ~override~ //mask for attribution
	COPY ~3ed/SpontaneousCasting/BattleCasting/RGSPBCST.SPL~ ~override~ //spontaneous conversion to battle casting innate ability
		SPRINT icon EVALUATE_BUFFER ~BTLCTNGB~
		LPF ALTER_SPELL_HEADER STR_VAR icon END
		WRITE_ASCII 0x003a ~BTLCTNGC~ #8
		SAY NAME1 @621
		SAY UNIDENTIFIED_DESC @622
	
	OUTER_FOR (i=1;i<=7;i=i+1) BEGIN
		COPY ~3ed/SpontaneousCasting/BattleCasting/BCST.SPL~ ~override/BCST%i%.SPL~
			WRITE_LONG 0x0034 i //spell level
			LPF ALTER_SPELL_EFFECT INT_VAR power=i END
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=73 parameter1=i END //damage bonus
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=278 parameter1=i END //thac0
			FOR (k=1;k<=7;k=k+1) BEGIN
				SPRINT resource EVALUATE_BUFFER ~BCST%k%~
				LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 power=i duration=1 timing=0 STR_VAR resource END //remove previous casts
			END
			SPRINT icon EVALUATE_BUFFER ~BTLCTN%i%B~
			LPF ALTER_SPELL_HEADER STR_VAR icon END
			WRITE_ASCII 0x003a ~BTLCTN%i%C~ #8
			SET SpellName=622 + i
			SAY NAME1 (AT "SpellName")
	END
	// spell channeling 
	COPY ~3ed/SpontaneousCasting/ChannelStrike/BAM~ ~override~ //bam icons
	COPY ~3ed/SpontaneousCasting/ChannelStrike/RGSPCSTK.EFF~ ~override~ //mask for attribution
	COPY ~3ed/SpontaneousCasting/ChannelStrike/RGSPCSTK.SPL~ ~override~ //spontaneous conversion to spell channeling
			SPRINT icon EVALUATE_BUFFER ~CHSTRKB~
			LPF ALTER_SPELL_HEADER STR_VAR icon END
			WRITE_ASCII 0x003a ~CHSTRKC~ #8
			SAY NAME1 @631
			SAY UNIDENTIFIED_DESC @632
	
	OUTER_FOR (i=1;i<=7;i=i+1) BEGIN
		COPY ~3ed/SpontaneousCasting/ChannelStrike/CHSTKE.SPL~ ~override/CHSTKE%i%.SPL~
			WRITE_LONG 0x0034 i //spell level
			LPF ALTER_SPELL_EFFECT INT_VAR power=i END
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=12 dicenumber=i END //number of 1d4 dices
			
		COPY ~3ed/SpontaneousCasting/ChannelStrike/CHSTK.SPL~ ~override/CHSTK%i%.SPL~
			WRITE_LONG 0x0034 i //spell level
			LPF ALTER_SPELL_EFFECT INT_VAR power=i END
			SPRINT resource EVALUATE_BUFFER ~CHSTKE%i%~
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=340 STR_VAR resource END //backstab hit effect

			FOR (k=1;k<=7;k=k+1) BEGIN
				SPRINT resource EVALUATE_BUFFER ~CHSTK%k%~
				LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2 power=i duration=1 timing=0 STR_VAR resource END //remove previous casts
			END
			SPRINT icon EVALUATE_BUFFER ~CSTRK%i%B~
			LPF ALTER_SPELL_HEADER STR_VAR icon END
			WRITE_ASCII 0x003a ~CSTRK%i%C~ #8
			SET SpellName=632 + i
			SAY NAME1 (AT "SpellName")
	END
	
	
	
	//////////////////////////////array in spell icon format
	ACTION_DEFINE_ARRAY spheal BEGIN 
		~SPPR103~ ~SPPR103B~//cure light wounds 
		~RGSPA02~ ~RGSPA02B~//cure moderate wounds
		~SPPR315~ ~SPPR315B~//cure medium wounds
		~SPPR401~ ~SPPR401B~//cure serious wounds
		~SPPR502~ ~SPPR502B~//cure critical wounds
		~SPPR607~ ~SPPR607B~//heal
		~SPPR712~ ~SPPR712B~//ressurection		
	END
	ACTION_DEFINE_ARRAY spharm BEGIN 
		~RGSPB01~ ~1WDCLWB~//cause light wounds 
		~RGSPB02~ ~RGSPB02B~//cause moderate wounds
		~RGSPB03~ ~1WDCMWB~//cause medium wounds
		~SPPR414~ ~SPPR414B~//cause serious wounds
		~SPPR510~ ~SPPR510B~//cause critical wounds
		~SPPR608~ ~SPPR608B~//harm
		~SPPR708~ ~SPPR708B~ //finger of death	
	END
	ACTION_DEFINE_ARRAY spsumm BEGIN 
		~RGSPC01~ ~RGSPC01B~//lesser animal summomning 1
		~RGSPC02~ ~RGSPC02B~//lesser animal summomning 2
		~RGSPC03~ ~RGSPC03B~//lesser animal summomning 3
		~SPPR402~ ~SPPR402B~//animal summomning 1
		~SPPR501~ ~SPPR501B~//animal summomning 2
		~SPPR602~ ~SPPR602B~//animal summomning 3
		~SPPR702~ ~SPPR702B~ //summon earth elemental
	END
	

	//Now we will loop over all spells and add spontaneous effects
	COPY_EXISTING_REGEXP GLOB ~.+\.spl~ ~override~
		READ_SHORT 0x1c "spell_type"
		READ_SHORT 0x34 "spell_level"
		READ_SHORT 0x0068 "Nheaders" //number of headers
		PATCH_IF spell_type = 2 AND spell_level <= 7 BEGIN
			
			SET icon_id = 2*(spell_level - 1)+1
			SET spell_id =  2*(spell_level - 1)
			//healing
			SPRINT icon EVALUATE_BUFFER $spheal("%icon_id%")
			SPRINT resource EVALUATE_BUFFER $spheal("%spell_id%")
			LPF ADD_SPELL_HEADER  INT_VAR  type=1  location=2  target=1+2*(spell_level = 7)  target_count=0  range=1+29*(spell_level = 7)  required_level=cure_level  STR_VAR  icon   END
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+1  opcode = 146  target = 2  parameter1 = 1  timing = 1  resist_dispel = 2  STR_VAR  resource   END //cast spell
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+1  opcode = 321  target = 1  timing = 0 duration=1  resist_dispel = 2  STR_VAR resource=~RGSPHEAL~ END	 //remove effects	
			
			//summoning
			SPRINT icon EVALUATE_BUFFER $spsumm("%icon_id%")
			SPRINT resource EVALUATE_BUFFER $spsumm("%spell_id%")
			LPF ADD_SPELL_HEADER  INT_VAR  type=1  location=2  target=4  target_count=0  range=30  required_level=summ_level  STR_VAR  icon   END
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+2  opcode = 148  target = 1  parameter1 = 1  timing = 1  resist_dispel = 2  STR_VAR  resource   END //cast spell at point
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+2  opcode = 321  target = 1  timing = 0 duration=1  resist_dispel = 2  STR_VAR resource=~RGSPSUMM~ END	 //remove effects	
			
			//battlecasting
			SPRINT icon EVALUATE_BUFFER ~BTLCTN%spell_level%B~
			SPRINT resource EVALUATE_BUFFER ~BCST%spell_level%~
			LPF ADD_SPELL_HEADER  INT_VAR  type=1  location=2  target=5  target_count=0  range=0  required_level=bcst_level  STR_VAR  icon   END
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+3  opcode = 146  target = 2  parameter1 = 1  timing = 1  resist_dispel = 2  STR_VAR  resource   END //cast spell on caster
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+3  opcode = 321  target = 1  timing = 0 duration=1  resist_dispel = 2  STR_VAR resource=~RGSPBCST~ END	 //remove effects	
			
			//channeling
			SPRINT icon EVALUATE_BUFFER ~CSTRK%spell_level%B~
			SPRINT resource EVALUATE_BUFFER ~CHSTK%spell_level%~
			LPF ADD_SPELL_HEADER  INT_VAR  type=1  location=2  target=5  target_count=0  range=0  required_level=cstk_level  STR_VAR  icon   END
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+4  opcode = 146  target = 2  parameter1 = 1  timing = 1  resist_dispel = 2  STR_VAR  resource   END //cast spell on caster
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+4  opcode = 321  target = 1  timing = 0 duration=1  resist_dispel = 2  STR_VAR resource=~RGSPCSTK~ END	 //remove effects	
			
			//harming
			SPRINT icon EVALUATE_BUFFER $spharm("%icon_id%")
			SPRINT resource EVALUATE_BUFFER $spharm("%spell_id%")
			LPF ADD_SPELL_HEADER  INT_VAR  type=1  location=2  target=1  target_count=0  range=1+39*(spell_level = 7)  required_level=harm_level  STR_VAR  icon   END
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+5  opcode = 146  target = 2  parameter1 = 91  timing = 1  resist_dispel = 2  STR_VAR  resource   END //cast spell
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+5  opcode = 321  target = 1  timing = 0 duration=1  resist_dispel = 2  STR_VAR resource=~RGSPHARM~ END	 //remove effects
			
		END  
		ELSE PATCH_IF (spell_type = 1 AND spell_level <= 7) BEGIN
			//battlecasting
			SPRINT icon EVALUATE_BUFFER ~BTLCTN%spell_level%B~
			SPRINT resource EVALUATE_BUFFER ~BCST%spell_level%~
			LPF ADD_SPELL_HEADER  INT_VAR  type=1  location=2  target=5  target_count=0  range=0  required_level=bcst_level  STR_VAR  icon   END
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+1  opcode = 146  target = 2  parameter1 = 1  timing = 1  resist_dispel = 2  STR_VAR  resource   END //cast spell on caster
			LPF ADD_SPELL_EFFECT  INT_VAR  header = Nheaders+1  opcode = 321  target = 1  timing = 0 duration=1  resist_dispel = 2  STR_VAR resource=~RGSPBCST~ END	 //remove effects
		END BUT_ONLY	
	
	INCLUDE ~3ed/UPDATE_SP_CASTING_LEVEL.tph~
		LAF UPDATE_SP_CASTING_LEVEL INT_VAR target_caster_level=cure_level max_levels=30 STR_VAR ability_name=~rgspheal~ END
		LAF UPDATE_SP_CASTING_LEVEL INT_VAR target_caster_level=summ_level max_levels=30 STR_VAR ability_name=~rgspsumm~ END
		LAF UPDATE_SP_CASTING_LEVEL INT_VAR target_caster_level=bcst_level max_levels=30 STR_VAR ability_name=~rgspbcst~ END
		LAF UPDATE_SP_CASTING_LEVEL INT_VAR target_caster_level=cstk_level max_levels=30 STR_VAR ability_name=~rgspcstk~ END
		LAF UPDATE_SP_CASTING_LEVEL INT_VAR target_caster_level=harm_level max_levels=30 STR_VAR ability_name=~rgspharm~ END
	


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Animal Companion
BEGIN @700

	INCLUDE ~3ed/ADD_SPELL_HEADER.tph~

	//icons and wav
	COPY ~3ed/Classes/AnimalCompanion/ANM_CMPB.BAM~ ~override~ 
	COPY ~3ed/Classes/AnimalCompanion/ANM_CMPC.BAM~ ~override~ 
	COPY ~3ed/Classes/AnimalCompanion/ANM_CST.WAV~ ~override~
	COPY ~3ed/Classes/AnimalCompanion/ANM_EFF.WAV~ ~override~
	COPY ~3ed/Classes/AnimalCompanion/ANM_WLF.WAV~ ~override~
	

//copying animal companion script
//animal summon spell
	COPY ~3ed/Classes/AnimalCompanion/ANM_CMP.BCS~ ~override~ 
	
	OUTER_SPRINT move_string ~~
	OUTER_SPRINT kill_string ~~
	
	
//creating wolfs for diffetent levels
    OUTER_SET maxlvl=20

	OUTER_SET name_ref=RESOLVE_STR_REF (@701)
	OUTER_SET descr_ref=RESOLVE_STR_REF (@702)
OUTER_FOR (player_id=1;player_id<=6;player_id=player_id+1) BEGIN
	OUTER_SPRINT script_name EVALUATE_BUFFER ~ANWLF%player_id%~
	OUTER_FOR (i=1;i<=maxlvl;i+=1) BEGIN
		COPY ~3ed/Classes/AnimalCompanion/ANMWLF1.CRE~ ~override/ANWLF%player_id%%i%.CRE~ 
	
		//SAY 0x0008 @701
		//SAY 0x000c @701
	
		WRITE_LONG  0x0008 name_ref  //name
		WRITE_LONG  0x000c name_ref  //tooltip
		WRITE_SHORT 0x0024 3+i*(3+2+(1+i/4)/2)+i/2 //Current Hp
		WRITE_SHORT 0x0026 3+i*(3+2+(1+i/4)/2)+i/2//Max Hp
		WRITE_SHORT 0x0046 (7 - i / 2 )//Natural AC
		WRITE_SHORT 0x0048 (7 - i / 2 )//Effective AC
		WRITE_BYTE  0x0052 (20 - (3 * i) / 4)//THAC0
		//APR
		PATCH_IF (i<8) BEGIN
			WRITE_BYTE   0x0053  1  //1
		END 
		ELSE PATCH_IF (i>=8) && (i<15) BEGIN
			WRITE_BYTE   0x0053  7  //1.5
		END 
		ELSE BEGIN
			WRITE_BYTE   0x0053  2  //2
		END

		//saves
		WRITE_BYTE   0x0054   15 - i / 3 //Death
		WRITE_BYTE   0x0055   13 - i / 2//Wand
		WRITE_BYTE   0x0056   15 - i / 3 //Polymorph
		WRITE_BYTE   0x0057   13 - i / 2 //Breath
		WRITE_BYTE   0x0058   13 - i / 2 //Spells
	
		WRITE_BYTE   0x0234   i //level
		WRITE_BYTE   0x0238   13+i/4 //Str
		WRITE_BYTE   0x023c   15+i/4 //Dex
		WRITE_BYTE   0x023d   15+i/4 //Con
	
		READ_LONG    0x2bc itemsoffset //weapon (damage)
		PATCH_IF (i<4) BEGIN
			WRITE_ASCII itemsoffset ~P1-4~  
		END 
		ELSE PATCH_IF (i>=4) && (i<8) BEGIN
			WRITE_ASCII itemsoffset ~P1-6~  
		END 
		ELSE PATCH_IF (i>=8) && (i<12) BEGIN
			WRITE_ASCII itemsoffset ~P1-8~  
		END 
		ELSE  PATCH_IF (i>=12) && (i<16) BEGIN
			WRITE_ASCII itemsoffset ~P1-10~  
		END 
		ELSE PATCH_IF (i>=16) && (i<20) BEGIN
			WRITE_ASCII itemsoffset  ~P1-12~  
		END 
		ELSE BEGIN
			WRITE_ASCII itemsoffset ~P2-16~  
		END 
		
		WRITE_EVALUATED_ASCII 0x0280 ~%script_name%~
		WRITE_EVALUATED_ASCII 0x0248 ~%script_name%~ #8
	END
	//string for area scripts
	OUTER_SPRINT move_string EVALUATE_BUFFER 
	~%move_string%
	MoveGlobalObject("%script_name%",Player1)
	ChangeEnemyAlly("%script_name%",CONTROLLED)~
	
	//string for baldur.bcs
	OUTER_SPRINT kill_string EVALUATE_BUFFER 
	~%kill_string%
	ActionOverride("%script_name%",DestroySelf())~
	
	//companion script
	COPY ~3ed/Classes/AnimalCompanion/ANM_CMP.bcs~ ~override/%script_name%.bcs~	
	EXTEND_TOP ~%script_name%.bcs~ ~3ed/Classes/AnimalCompanion/ANM_CMP.baf~
		EVALUATE_BUFFER
		
	//companion spell
	COPY ~3ed/Classes/AnimalCompanion/ANM_CMP.SPL~ ~override/AN_CMP%player_id%.SPL~ 
	
		WRITE_LONG  0x0008 name_ref  //name
		WRITE_LONG  0x0050 descr_ref  //description
		
		FOR (i=1;i<=maxlvl;i+=1) BEGIN
			SPRINT resource EVALUATE_BUFFER ~ANWLF%player_id%%i%~
			LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=4 target_count=0 range=1 required_level=i speed=1 projectile=1  STR_VAR icon=~ANM_CMPB~ END
			LPF ADD_SPELL_EFFECT  INT_VAR  header = i opcode = 67 target = 1 power = 0 parameter1 = 0 parameter2 = 0 duration = 7200 resist_dispel = 0 STR_VAR resource END 
			LPF ADD_SPELL_EFFECT  INT_VAR  header = i opcode = 174 target = 1 power = 0 parameter1 = 0 parameter2 = 0 duration = 0   resist_dispel = 0   STR_VAR resource=~ANM_EFF~ END
			LPF ADD_SPELL_EFFECT  INT_VAR  header = i opcode = 174 target = 1 power = 0 parameter1 = 0 parameter2 = 0 duration = 0   resist_dispel = 0   STR_VAR resource=~ANM_WLF~ END
		END	
	
	//create spell giving proper animal companion and removing all other abilities
	COPY ~3ed/Classes/AnimalCompanion/GV_CMP.spl~ ~override/GV_CMP%player_id%.spl~
		FOR (i=1;i<=6;i=i+1) BEGIN
			SPRINT resource EVALUATE_BUFFER ~AN_CMP%i%~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=2 timing=0 duration=1 STR_VAR resource END
			PATCH_IF (i=player_id) BEGIN
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=2 timing=0 duration=1 STR_VAR resource END
				LPF ADD_SPELL_EFFECT INT_VAR opcode=233 target=2 timing=1 duration=1 parameter1=i parameter2=134 END
			END
		END
		
	//update baldur bcs (global script summon giving ability)
	OUTER_SPRINT spell_name EVALUATE_BUFFER ~GV_CMP%player_id%~
	EXTEND_TOP ~baldur.bcs~ ~3ed/Classes/AnimalCompanion/baldur.baf~
		EVALUATE_BUFFER	
	
END
	
	COPY_EXISTING_REGEXP GLOB ~.+\.are~ ~override~
	//first create empty scripts for areas without ones		
		PATCH_IF NOT (FILE_EXISTS_IN_GAME ~%SOURCE_RES%.bcs~) THEN BEGIN
			SPRINT AR_NAME EVALUATE_BUFFER ~%SOURCE_RES%~
			INNER_ACTION BEGIN 
				COPY ~3ed/Classes/AnimalCompanion/AreaExtension/ARTEMP.bcs~ ~override/%AR_NAME%.bcs~
			END
			WRITE_EVALUATED_ASCII 0x0094 ~%AR_NAME%~ #8
		END
		
	//then add script that moves all existing animal companions to area of player 1	
	OUTER_SET ar_id=0
	COPY_EXISTING_REGEXP GLOB ~.+\.are~ ~override~
		SET ar_id=ar_id+1
		SPRINT AreaName EVALUATE_BUFFER ~%SOURCE_RES%~
		INNER_ACTION BEGIN 
			EXTEND_TOP ~%SOURCE_RES%.bcs~ ~3ed/Classes/AnimalCompanion/ACMPAR.baf~
				EVALUATE_BUFFER
		END
	//unsummon all companions after rest
	EXTEND_TOP ~baldur.bcs~ ~3ed/Classes/AnimalCompanion/baldur2.baf~
		EVALUATE_BUFFER
	

	
//beastmaster bonuses
	COPY ~3ed/Classes/AnimalCompanion/BeastMaster~ ~override~ 

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
///////Allow Wizardslayers to use magical items but restrict them to light armor only
///////Allow kensai to use head gear, light armors and bracers
///////Allow monks to use all thief weapons including 2handed (quarterstaff, crossbow, bow)
///////ftr druid armor restriction and allow beastmasters to use druid weapons and allow fighter druids to use all weapons
///////Disallow rangers and berserkers to use heavy armor (plate, full plate), except ankhneg
///////Disallow berserkers to use ranged weapons 
///////remove cleric restriction on non bladed weapons for multiclass clerics
BEGIN @800


COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~
  READ_STRREF 0x0008 "unid_name"
  READ_STRREF 0x000c "id_name"
  READ_SHORT  0x001c "category"
  READ_BYTE   0x001f "ftr_usability" //usability mask for fighter (bit 3)
  READ_BYTE   0x002f "kit_usability" //usability mask for berserker (0) wizardslayer (bit 1) and kensai (bit 2) 
  READ_BYTE   0x0029 "kit2_usability" //usability mask for barbarian (bit 6) 
  READ_BYTE   0x0020 "thf_usability" //usability mask for thief in bit 6, ranger -5, mage-thief -7 
  READ_BYTE   0x0021 "mnk_usability" //usability for monk in bit 5
  READ_BYTE   0x001e "bard_usability" //usability for bard in bit 6
  READ_BYTE   0x0020 "thf_usability" //usability mask for thief in bit 6 and mage_thief in bit 3
  READ_BYTE   0x002b "stl_usability" //usability mask for stalker in bit 0
  READ_BYTE   0x002d "arch_usability" //usability mask for archer in bit 7
  
  
  SET brsrk_mask=0b00000001
  SET wzslyr_mask=0b00000010
  SET kensai_mask=0b00000100
  SET ftr_mask=0b00001000
  SET usable_by_ftr = NOT (ftr_usability BAND ftr_mask)
  SET usable_by_wzslyr = NOT (kit_usability BAND wzslyr_mask)
  SET thief_mask= 0b01000000
  SET thief_mage_mask= 0b00001000
  SET ranger_mask= 0b00100000
  SET barb_mask= 0b01000000
  SET bard_mask= 0b01000000
  SET monk_mask = 0b00100000
  SET stl_mask =0b00000001
  SET arch_mask=0b10000000
  SET usable_by_thf = NOT (thf_usability BAND thief_mask)
  SET usable_by_monk = NOT (mnk_usability BAND monk_mask)
  
  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Leather Armor~) AND usable_by_ftr BEGIN //Leather Armor
	WRITE_BYTE 0x002f (kit_usability BAND BNOT(kensai_mask))	
  END  
  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Studded Leather Armor~) AND usable_by_ftr BEGIN //Studded Leather Armor
	WRITE_BYTE 0x002f (kit_usability BAND BNOT(kensai_mask))			
  END 
  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Hide Armor~) AND usable_by_ftr BEGIN //Hide Armor
	WRITE_BYTE 0x002f (kit_usability BAND BNOT(kensai_mask))			
  END  
  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Chain Mail~ OR ~%unid_name%~ STRING_EQUAL ~Ankheg Plate Mail~) AND NOT ((~%id_name%~ STRING_EQUAL ~Elven Chain Mail~) OR (~%id_name%~ STRING_EQUAL ~Mithral Chain Mail +4~)) AND usable_by_ftr BEGIN //Chain Mail or Ankheg armor
    WRITE_BYTE 0x002f (kit_usability BOR wzslyr_mask)
	WRITE_BYTE 0x0029 (kit2_usability BAND BNOT(barb_mask))	
	WRITE_BYTE 0x001e (bard_usability BAND BNOT(bard_mask))	
  END  
  ELSE  PATCH_IF (~%id_name%~ STRING_EQUAL ~Elven Chain Mail~ OR ~%id_name%~ STRING_EQUAL ~Mithral Chain Mail +4~)  BEGIN //Elven Chain Mail or Drizzt chain
	WRITE_BYTE 0x002f (kit_usability BAND BNOT(kensai_mask) BAND BNOT(wzslyr_mask))	
	WRITE_BYTE 0x0020 (thf_usability BAND BNOT(thief_mask) BAND BNOT(thief_mage_mask))
	WRITE_BYTE 0x002b (stl_usability BAND BNOT(stl_mask))
    WRITE_BYTE 0x002d (arch_usability BAND BNOT(arch_mask))		
  END  
  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Splint Mail~) AND usable_by_ftr BEGIN //Splint Mail
	WRITE_BYTE 0x002f (kit_usability BOR wzslyr_mask)
  END  
  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Plate Mail~) AND usable_by_ftr BEGIN //Plate Mail
	WRITE_BYTE 0x002f (kit_usability BOR (brsrk_mask BOR wzslyr_mask))
	WRITE_BYTE 0x0020 (thf_usability BOR ranger_mask)	
  END  
  ELSE  PATCH_IF (~%unid_name%~ STRING_EQUAL ~Full Plate Armor~) AND usable_by_ftr BEGIN //Full Plate
  	PATCH_PRINT ~%SOURCE_FILE%~
	WRITE_BYTE 0x002f (kit_usability BOR (brsrk_mask BOR wzslyr_mask))
	WRITE_BYTE 0x0020 (thf_usability BOR ranger_mask)	
  END 
  ELSE PATCH_IF (category = 0x0006 OR category = 0x0007) AND usable_by_ftr BEGIN //bracers
	WRITE_BYTE 0x002f (kit_usability BAND BNOT(kensai_mask) BAND BNOT(wzslyr_mask))
  END 
  ELSE PATCH_IF (category = 0x001a OR category = 0x001b OR category = 0x000f OR category=0x0005 OR category=0x0005 OR category=0x001f) AND usable_by_thf BEGIN //quarterstaff,crossbow, bow,arrow, bolt
	WRITE_BYTE 0x0021 (mnk_usability BAND BNOT(monk_mask))
  END  
  ELSE PATCH_IF  usable_by_ftr AND NOT(usable_by_wzslyr) BEGIN //everything else
	WRITE_BYTE 0x002f (kit_usability BAND BNOT(wzslyr_mask))
  END
BUT_ONLY

//disalow ninjas (bounty hunters to use armor and shields)
COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~
	READ_BYTE 0x002b "ninja_usability" //unusability for bounty hunter in bit 3
	READ_SHORT  0x001c "category"
	
	SET ninja_mask = 0b00001000
	SET usable_by_ninja=NOT (ninja_usability BAND ninja_mask)
	
	PATCH_IF (usable_by_ninja AND (category = 0x0002 OR category = 0x000c)) BEGIN
		WRITE_BYTE 0x002b (ninja_usability BOR ninja_mask)
	END
BUT_ONLY


//berserker inability to use ranged weapons

COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~
	  READ_BYTE   0x002f "kit_usability" //usability mask for berserker (0) wizardslayer (bit 1) and kensai (bit 2) and cavalier bit(3)
	  SET brsrk_mask=0b00000001
	  SET cvlr_mask=0b00001000
	  READ_SHORT  0x001c "category"
	  SET usable_by_cvlr = NOT (kit_usability BAND cvlr_mask)
	  
	  PATCH_IF (category != 0x0002 AND category!= 0x000c AND NOT(usable_by_cvlr) ) BEGIN //(everything except armors and shields)
		WRITE_BYTE 0x002f (kit_usability BOR brsrk_mask)
	  END
	  
//ftr/druid and cleric/ranger armor restriction (ranger) and beastmaster weapons, allow druid kits to use same armors as true class druids

COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~
 // READ_BYTE 0x0031 "proficiency"
  READ_BYTE 0x001f "fd_usability" //unusability for ftr druids in bit 4  /fighter in bit 3, cleric ranger in bit 2
  READ_BYTE 0x0021 "d_usability"  //unusability for druids in bit 6
  READ_BYTE 0x002b "bs_usability" //unusability for beastmaster in bit 1
  READ_BYTE 0x001c "category"     //for checking whether item is armor or axe
  READ_BYTE 0x0029 "druid_kit_usability" //druid kits in bits 3 4 5
  READ_BYTE 0x0020 "rng_usability" //usability mask for ranger - bit 5

  
  SET rng_mask= 0b00100000
  SET c_r_mask= 0b00000100
  SET usable_by_rng = NOT (rng_usability BAND rng_mask)
  SET usable_by_druid = NOT (d_usability BAND 0b01000000 )
  //beastmaster weapons
  PATCH_IF ((usable_by_druid) AND (bs_usability BAND 0b00000010)) OR (category = 25) BEGIN //axes
	WRITE_BYTE 0x0002b (bs_usability BAND 0b11111101)
	WRITE_BYTE 0x0001f (fd_usability BAND 0b11101111)
  END  
  //patch crossbows (allow ftr/druid, restrict beastmaster)
  ELSE PATCH_IF (category = 23) BEGIN
    WRITE_BYTE 0x0002b (bs_usability BOR 0b00000010)
	WRITE_BYTE 0x0001f (fd_usability BAND 0b11101111)
  END
  //fighter/druid and cleric/ranger armor restricted to that of a ranger or druid
  ELSE PATCH_IF NOT (usable_by_druid OR usable_by_rng)  AND (category = 0x0002) BEGIN
	WRITE_BYTE 0x0001f (fd_usability BOR 0b00010000 BOR c_r_mask)	
  END  
  //allow druid kits to use armors that normal druids can
  ELSE PATCH_IF usable_by_druid AND (category = 0x0002) BEGIN
	WRITE_BYTE 0x00029 (druid_kit_usability BAND 0b11000111)	
  END
  //weapons for ftr/druid are that of a fighter
  ELSE PATCH_IF (NOT (fd_usability BAND 0b00001000 )) AND (category != 0x0002) BEGIN
	WRITE_BYTE 0x0001f (fd_usability BAND 0b11101111)	
  END
BUT_ONLY

//allow jester to use only weapons of a mage
COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~
 
  READ_BYTE 0x001e "bard_usability" //unusability for bards in bit 6
  READ_BYTE 0x0020 "mage_usability"  //unusability for mages in bit 2
  READ_BYTE 0x001c "category" 
  READ_BYTE 0x002b "jester_usability" //unusability for jester in bit 6

  SET jester_bard_mask=0b01000000
  SET mage_mask = 0b00000100

  SET usable_by_mage = NOT (mage_usability BAND mage_mask)

 PATCH_IF (NOT(usable_by_mage) AND category != 0x0002 AND category!=0x0006 AND category !=0x000c) BEGIN //nonarmors, non shields, non bracers
	WRITE_BYTE 0x0002b (jester_usability BOR jester_bard_mask)
  END  

BUT_ONLY

//remove restriction on only cleric weapons from multiclass clerics but put restrictions on armor and shields
COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~
	READ_BYTE 0x001f  "usability2"  //0-cleric/mage, 1- cleric/thief, 2- cleric/ranger, 3- fighter, 6- fighter/cleric, 7-fighter/cleric/mage, 
	READ_BYTE 0x0020  "usability3"  //2- mage, 5- ranger, 6 -thief
	READ_BYTE 0x001c "category"     //for checking whether item is armor or shield

	SET usable_by_fighter= NOT (usability2 BAND 0b00001000)
	SET usable_by_ranger= NOT (usability3 BAND 0b00100000)
	SET usable_by_thief= NOT (usability3 BAND 0b01000000)
	SET usable_by_mage = NOT (usability3 BAND 0b00000100)
	
	SET cleric_mage_mask =0b00000001
	SET cleric_thief_mask =0b00000010
	SET cleric_ranger_mask =0b00000100
	SET fighter_cleric_mask =0b01000000
	
	PATCH_IF (usable_by_fighter) BEGIN
		SET usability2=usability2 BAND BNOT(fighter_cleric_mask)		
	END
	
	PATCH_IF (usable_by_thief) BEGIN
		SET usability2=usability2 BAND BNOT(cleric_thief_mask)		
	END	
	PATCH_IF (NOT usable_by_thief) AND (category = 0x0002 OR category = 0x000c) BEGIN
		SET usability2=usability2 BOR cleric_thief_mask		
	END
	
	PATCH_IF (usable_by_ranger) BEGIN
		SET usability2=usability2 BAND BNOT(cleric_ranger_mask)		
	END
	PATCH_IF (NOT usable_by_ranger) AND (category = 0x0002) BEGIN
		SET usability2=usability2 BOR cleric_ranger_mask		
	END
	
	
	PATCH_IF (usable_by_mage) BEGIN
		SET usability2=usability2 BAND BNOT(cleric_mage_mask)		
	END
	PATCH_IF (NOT usable_by_mage) AND (category = 0x0002 OR category=0x000c) BEGIN
		SET usability2=usability2 BOR cleric_mage_mask		
	END
	
	WRITE_BYTE 0x001f  usability2  
	
BUT_ONLY

//allow sorcerors to use equipment of thieves (except thief specific items)
//through allowing it to mages and adding restirct item effect on them
OUTER_SET mage_strref=3139
COPY_EXISTING_REGEXP GLOB ~.+\.itm~ ~override~

	READ_BYTE 0x0020  "t_usability"  //2- mage, 5- ranger, 6 -thief
	READ_BYTE 0x001f "f_usability" //unusability for ftr druids in bit 4  /fighter in bit 3, cleric ranger in bit 2
	SET usable_by_fighter= NOT (f_usability BAND 0b00001000)	
	SET usable_by_thief= NOT (t_usability BAND 0b01000000)
	SET allow_sorc = usable_by_fighter AND usable_by_thief
	
	SET usable_by_mage = NOT (t_usability BAND 0b00000100)
	
	PATCH_IF (NOT usable_by_mage) AND allow_sorc BEGIN
		WRITE_BYTE 0x0020  (t_usability BAND BNOT(0b00000100))
		LPF ADD_ITEM_EQEFFECT INT_VAR  opcode = 319 target = 1  power = 0  parameter1 = 1   parameter2 = 5  timing = 2  resist_dispel = 0   probability1 = 100  probability2 = 0  special = mage_strref  END
	END
	

BUT_ONLY




//////////////////////////////////////////////////////////////////////////////////////////////////
//////Ranger Favored Enemies
BEGIN @900
    //Copy Icons
	COPY ~3ed\Classes\FavoredEnemy\FE_B.BAM~ ~override~
	COPY ~3ed\Classes\FavoredEnemy\FE_C.BAM~ ~override~
	
	ACTION_DEFINE_ASSOCIATIVE_ARRAY FavoredEnemies BEGIN
	    ~Humans~           => 1
        ~Elves~            => 2
        ~Half-elves~       => 3
        ~Dwarfs~           => 4
        ~Halflings~        => 5
        ~Gnomes~           => 6
        ~Half-orcs~         => 7
        ~Basilisks~        => 102
		~Carrion crawlers~ => 104    
		~Ettercaps~        => 107     
        ~Ghouls~           => 108     
		~Gibberlings~      => 109
		~Gnolls~           => 110
		~Hobgoblins~       => 111
		~Kobolds~          => 112
		~Ogres~            => 113
		~Skeletons~        => 115
		~Giant spiders~    => 116
		~Trolls~           => 129
		~Orcs~             => 143
		~Shadows~          => 132
        ~Elementals~       => 145
		~Demons~           => 121
		~Dragons~          => 146 
	END
	
	OUTER_SET   yes_response=RESOLVE_STR_REF (@908)
	OUTER_SET   no_response=RESOLVE_STR_REF (@909)
	
	COPY  ~3ed/Classes/FavoredEnemy/FE_MRK.SPL~ ~override/FE_MRK.SPL~
	COPY  ~3ed/Classes/FavoredEnemy/FE_MRK.SPL~ ~override/FE_RMV.SPL~

	 
	OUTER_SET i=0
	ACTION_PHP_EACH FavoredEnemies AS FavoredEnemy => ID BEGIN
	
		 COPY ~3ed/Classes/FavoredEnemy/FE_TEMP.EFF~ ~override/FE_DM%i%.eff~ // damage bonus
		 WRITE_SHORT 0x001c ID  // change ID to next favored enemy
		 
		 COPY ~3ed/Classes/FavoredEnemy/FE_AC.EFF~ ~override/FE_AC%i%.eff~ // AC bonus
		 WRITE_SHORT 0x001c ID  // change ID to next favored enemy
		 
		 COPY ~3ed/Classes/FavoredEnemy\FE_TH.EFF~ ~override/FE_TH%i%.eff~ // thac0 bonus
		 WRITE_SHORT 0x001c ID  // change ID to next favored enemy
		 //Racial Enemy
		 COPY  ~3ed/Classes/FavoredEnemy/FE_TEMP.SPL~ ~override/FE_%i%.spl~		 
		 SAY_EVALUATED 0x0008 ~Racial Enemy: %FavoredEnemy%~
		 SAY_EVALUATED 0x0050 ~Racial Enemy: %FavoredEnemy%
		 
Character gains +4 bonus on damage rolls against %FavoredEnemy%.~

		 SPRINT resource EVALUATE_BUFFER ~FE_DM%i%~
	     LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 272  STR_VAR resource END  //apply damage bonus to racial enemy effects
		 SPRINT resource EVALUATE_BUFFER ~FE_PR%i%~
		 LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 171  STR_VAR resource END //give 7 level spell
		 SPRINT resource EVALUATE_BUFFER ~FE_MK%i%~
	     LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 206  STR_VAR resource END //protection from marking
		 
		
		 READ_LONG 0x0008 ~Spell_Name~
		 READ_LONG 0x0050 ~Spell_Descr~
		 
		 COPY  ~3ed/Classes/FavoredEnemy/FE_PRTMP.SPL~ ~override/FE_PR%i%.spl~		 
		 WRITE_LONG 0x0008 Spell_Name
		 WRITE_LONG 0x0050 Spell_Descr

		 COPY  ~3ed/Classes/FavoredEnemy/FE_MK.SPL~   ~override/FE_MK%i%.SPL~ 
		 SPRINT resource EVALUATE_BUFFER ~FE_MK%i%~
	     LPF ADD_SPELL_EFFECT INT_VAR opcode = 265  target=2 parameter1=1 duration=1000 STR_VAR resource END //marking
		 
		 COPY_EXISTING  ~FE_MRK.SPL~   ~override~ 
		 SPRINT resource EVALUATE_BUFFER ~FE_MK%i%~
	     LPF ADD_SPELL_EFFECT INT_VAR opcode = 326  target=2 duration=1 STR_VAR resource END //apply marking
		 
		 COPY_EXISTING  ~FE_RMV.SPL~   ~override~ 
		 SPRINT resource EVALUATE_BUFFER ~FE_MK%i%~
	     LPF ADD_SPELL_EFFECT INT_VAR opcode = 265  target=2 parameter1=0 duration=1000 STR_VAR resource END //remove marking
		 
		OUTER_SPRINT condition_str EVALUATE_BUFFER  
			
~~~~~IF ~Global("FE_MK%i%","GLOBAL",1)~  THEN REPLY #%Spell_Name% 
DO ~ApplySpellRES("FE_%i%",LastSummonerOf(Myself))~  EXIT

|condition_str|~~~~~ 		 

		 
			COPY ~3ed/Classes/FavoredEnemy/fecre.d~  ~3ed/Classes/FavoredEnemy/fecre.d~  									
				EVALUATE_BUFFER	
			REPLACE_TEXTUALLY ~|~ ~%~	 
		 
		 
		 			 
		 //Improved Racial Enemy for Stalkers
		 COPY  ~3ed\Classes\FavoredEnemy\FE_TEMP.SPL~ ~override/FEI_%i%.spl~		 
		 SAY_EVALUATED 0x0008 ~Improved Racial Enemy: %FavoredEnemy%~
		 SAY_EVALUATED 0x0050 ~Improved Racial Enemy: %FavoredEnemy%
		 
Character gains +4 bonus on to hit and damage rolls as well +2 bonus to AC and saving throws against %FavoredEnemy%.~

		 SPRINT resource EVALUATE_BUFFER ~FE_DM%i%~
	     LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 272 header=1 STR_VAR resource END  //apply damage bonus bonus to racial enemy effects
		 SPRINT resource EVALUATE_BUFFER ~FE_AC%i%~
	     LPF ADD_SPELL_EFFECT INT_VAR opcode = 272 target=2 duration=1 timing=9 parameter1=5 parameter2=3 insert_point=0 STR_VAR resource END  //apply ac/saving throws bonus bonus to racial enemy effects
		 SPRINT resource EVALUATE_BUFFER ~FE_TH%i%~
	     LPF ADD_SPELL_EFFECT INT_VAR opcode = 272 target=2 duration=1 timing=9 parameter1=5 parameter2=3 insert_point=0 STR_VAR resource END  //apply thac0 bonus bonus to racial enemy effects
		 
		 SPRINT resource EVALUATE_BUFFER ~FEI_PR%i%~
		 LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 171  STR_VAR resource END //give 7 level spell
		 SPRINT resource EVALUATE_BUFFER ~FE_MK%i%~
	     LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 206  STR_VAR resource END //protection from marking
		 
		
		 READ_LONG 0x0008 ~Spell_Name~
		 READ_LONG 0x0050 ~Spell_Descr~
		 
		 COPY  ~3ed\Classes\FavoredEnemy\FE_PRTMP.SPL~ ~override/FEI_PR%i%.spl~		 
			WRITE_LONG 0x0008 Spell_Name
			WRITE_LONG 0x0050 Spell_Descr
		 
		 
		OUTER_SPRINT condition_str EVALUATE_BUFFER  
			
~~~~~IF ~Global("FE_MK%i%","GLOBAL",1)~  THEN REPLY #%Spell_Name%
DO ~ApplySpellRES("FEI_%i%",LastSummonerOf(Myself))~  EXIT

|condition_str|~~~~~ 		 

		 
			COPY ~3ed/Classes/FavoredEnemy/feicre.d~  ~3ed/Classes/FavoredEnemy/feicre.d~  									
				EVALUATE_BUFFER	
			REPLACE_TEXTUALLY ~|~ ~%~	
		 	     	 								 		 
		 i=i+1
	END		 
	

	COPY ~3ed/Classes/FavoredEnemy/fecre.d~  ~3ed/Classes/FavoredEnemy/fecre.d~  	
	REPLACE_TEXTUALLY ~%~	~|~
	REPLACE_TEXTUALLY ~|condition_str|~ ~~
	COMPILE ~3ed/Classes/FavoredEnemy/fecre.d~  
	
	COPY ~3ed/Classes/FavoredEnemy/FECRE.BCS~ ~override~
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/FECRE.CRE~
	WRITE_ASCII 0x248 ~FECRE~ #8//override script
	WRITE_ASCII 0x02cc ~fecre~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/FECRE.EFF~
	WRITE_ASCII 0x0030 ~FECRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/FECRE.SPL~	
	LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~FECRE~ END
	
	
	COPY ~3ed/Classes/FavoredEnemy/feicre.d~  ~3ed/Classes/FavoredEnemy/feicre.d~  	
	REPLACE_TEXTUALLY ~%~	~|~
	REPLACE_TEXTUALLY ~|condition_str|~ ~~
	COMPILE ~3ed/Classes/FavoredEnemy/feicre.d~  
	
	COPY ~3ed/Classes/FavoredEnemy/FECRE.BCS~ ~override/FEICRE.BCS~
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.CRE~ ~override/FEICRE.CRE~
	WRITE_ASCII 0x248 ~FEICRE~ #8//override script
	WRITE_ASCII 0x02cc ~feicre~ #8//dialog
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.EFF~ ~override/FEICRE.EFF~
	WRITE_ASCII 0x0030 ~FEICRE~ #8//creature name
	COPY ~3ed/Feats/FeatAttribution/FEATCRE.SPL~ ~override/FEICRE.SPL~	
	LPF  ALTER_SPELL_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~FEICRE~ END
		
	
	COPY ~3ed\Classes\FavoredEnemy\HATERACE.2DA~ ~override~ // description for ranger favored enemies at character generation - we leave just one entry
	SET name_ref=RESOLVE_STR_REF (@905)
	SET descr_ref=RESOLVE_STR_REF (@906)
	
	SET_2DA_ENTRY 0 1 4 ~%name_ref%~
	SET_2DA_ENTRY 0 3 4 ~%descr_ref%~
	
	//#17256, #24317  - racial enemy description
	STRING_SET 17256 @907
	STRING_SET 24317 @907
	
	INCLUDE ~3ed/ADD_BONUS_FEATS.tph~
//racial enemies to rangers (except stlaker) and  cleric/rangers
	LAF ADD_BONUS_FEATS INT_VAR min_level=5 max_level=20 d_level=5 add_at_level1=1 
						STR_VAR clab=~\(CLABRN01\)\|\(CLABRN02\)\|\(CLABRN04\)\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREAL.SPL~ feat_type_file=~FECRE~ caption=~FECREAL~ END
						
		//set delay to 1 second (to avoid hangup on 1st level character creation)
	COPY_EXISTING ~FECREAL.SPL~ ~override~
		LPF  ALTER_SPELL_EFFECT INT_VAR duration  =1 timing =3  END
//improved racial enemies for stalkers
	LAF ADD_BONUS_FEATS INT_VAR min_level=5 max_level=20 d_level=5 add_at_level1=1 
						STR_VAR clab=~CLABRN03\.2DA~ mask_file=~~ feat_type_file=~~ caption=~FEICRE~ END
//racial enemies to fighter druids
	LAF ADD_BONUS_FEATS INT_VAR min_level=5 max_level=20 d_level=5 add_at_level1=1 
						STR_VAR clab=~CLABDR01\.2DA~ mask_file=~3ed/Feats/FeatAttribution/SFTCREFD.SPL~ feat_type_file=~FECRE~ caption=~FECREFD~ END
	
	
/////////////////////////////////////////////////////////
///////INT thieving skill bonuses (5% per 1 point of INT above 10)
BEGIN @1000

	OUTER_FOR (i=11;i<=25;i+=1) BEGIN
		COPY ~3ed/IntSkillBonus/INTSK.spl~ ~override/INTSK%i%.spl~
		LPF ALTER_SPELL_EFFECT INT_VAR parameter1=100+(i - 10)*5 timing=0 duration=10 END   
		SPRINT resource EVALUATE_BUFFER ~INTSK%i%~
		LPF ADD_SPELL_EFFECT INT_VAR insert_point=0 opcode=321 target=2  duration=1 STR_VAR resource END
	END
	
	OUTER_FOR (i=11;i<=25;i+=1) BEGIN
		COPY ~3ed/IntSkillBonus/INTSKB.spl~ ~override/INTSKB%i%.spl~
		SPRINT resource EVALUATE_BUFFER ~INTSK%i%~
		LPF ALTER_SPELL_EFFECT INT_VAR parameter1=i+1 STR_VAR resource END
	END
	
	
	COPY ~3ed/IntSkillBonus/INTSKBN.spl~  ~override~

	FOR (i=11;i<=25;i+=1) BEGIN
		SPRINT resource EVALUATE_BUFFER ~INTSKB%i%~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=326 target=2 duration=1 parameter1=i parameter2=128 STR_VAR resource END           //apply effect
	END

	
	COPY ~3ed/IntSkillBonus/INTSKBN.eff~  ~override~
	COPY ~3ed/IntSkillBonus/INTSKBNA.spl~  ~override~

////////////////////////////////////////////////////////////////////////////////////////
////bardic performance
////
////
BEGIN @1100
	INCLUDE ~3ed/INCREMENT_SPELL_DC.tph~
	INCLUDE ~3ed/ADD_SPELL_HEADER.tph~
	INCLUDE ~3ed/CREATE_PSEUDO_SPELL_SELECTION.tph~
	INCLUDE ~3ed/ADD_BONUS_FEATS.tph~

	
	COPY ~3ed/BardSongs/Bam~ ~override~
	COPY ~3ed/BardSongs/Songs/BARD1.SPL~ ~override~
		SAY NAME1 @1101
		SAY UNIDENTIFIED_DESC @1102
	COPY ~3ed/BardSongs/Songs/BARD2.SPL~ ~override~
		SAY NAME1 @1103
		SAY UNIDENTIFIED_DESC @1104	
		
	COPY ~3ed/BardSongs/Songs/BARD3.SPL~ ~override~
		SAY NAME1 @1105
		SAY UNIDENTIFIED_DESC @1106
		
	COPY ~3ed/BardSongs/Songs/BARD4.SPL~ ~override~
		SAY NAME1 @1107
		SAY UNIDENTIFIED_DESC @1108
		
	COPY ~3ed/BardSongs/Songs/BARD5.SPL~ ~override~
		SAY NAME1 @1109
		SAY UNIDENTIFIED_DESC @1110
		
	COPY ~3ed/BardSongs/Songs/BARD6.SPL~ ~override~
		SAY NAME1 @1111
		SAY UNIDENTIFIED_DESC @1112
		
	COPY ~3ed/BardSongs/Songs/BARD7.SPL~ ~override~
		SAY NAME1 @1113
		SAY UNIDENTIFIED_DESC @1114
	
	COPY ~3ed/BardSongs/Songs/BARD8.SPL~ ~override~
		SAY NAME1 @1115
		SAY UNIDENTIFIED_DESC @1116
		
	COPY ~3ed/BardSongs/Songs/BARD9.SPL~ ~override~
		SAY NAME1 @1117
		SAY UNIDENTIFIED_DESC @1118
		
	COPY ~3ed/BardSongs/Songs/BARDA.SPL~ ~override~
		SAY NAME1 @1119
		SAY UNIDENTIFIED_DESC @1120
		
	COPY ~3ed/BardSongs/Songs/BARDB.SPL~ ~override~
		SAY NAME1 @1121
		SAY UNIDENTIFIED_DESC @1122
		
		
	
	OUTER_SET max_val=25
	OUTER_SET min_val=10
	OUTER_SET step=2
	
	//bard
	COPY ~3ed/BardSongs/Bard~ ~override~
		SPRINT string EVALUATE_BUFFER ~%SOURCE_RES%~
		LPF SUBSTRING INT_VAR start=4 length=2 STR_VAR string RET k=substring END
		DEFINE_ASSOCIATIVE_ARRAY bard_song_list BEGIN "%k%" => "%SOURCE_RES%" END
		
	COPY ~3ed/BardSongs/BSNG.SPL~ ~override/BSNG.SPL~
		PHP_EACH bard_song_list AS header_lvl =>spell_list_2DA BEGIN			
			LPF CREATE_PSEUDO_SPELL_SELECTION INT_VAR min_val max_val step par1=132 header_lvl STR_VAR spell_list_2DA icon=~BARD0~ END
		END
		SAY NAME1 @1131
		SAY UNIDENTIFIED_DESC @1132	
	//jester	
	COPY ~3ed/BardSongs/Jester~ ~override~
		SPRINT string EVALUATE_BUFFER ~%SOURCE_RES%~
		LPF SUBSTRING INT_VAR start=4 length=2 STR_VAR string RET k=substring END
		DEFINE_ASSOCIATIVE_ARRAY jester_song_list BEGIN "%k%" => "%SOURCE_RES%" END
		
	COPY ~3ed/BardSongs/BSNG.SPL~ ~override/JSNG.SPL~
		PHP_EACH jester_song_list AS header_lvl =>spell_list_2DA BEGIN			
			LPF CREATE_PSEUDO_SPELL_SELECTION INT_VAR min_val max_val step par1=132 header_lvl STR_VAR spell_list_2DA icon=~BARD0~ END
		END
		SAY NAME1 @1131
		SAY UNIDENTIFIED_DESC @1134
		
	//blade and skald	
	COPY ~3ed/BardSongs/SkaldBlade~ ~override~
		SPRINT string EVALUATE_BUFFER ~%SOURCE_RES%~
		LPF SUBSTRING INT_VAR start=4 length=2 STR_VAR string RET k=substring END
		DEFINE_ASSOCIATIVE_ARRAY skald_song_list BEGIN "%k%" => "%SOURCE_RES%" END
		
	COPY ~3ed/BardSongs/BSNG.SPL~ ~override/SSNG.SPL~
		PHP_EACH skald_song_list AS header_lvl =>spell_list_2DA BEGIN			
			LPF CREATE_PSEUDO_SPELL_SELECTION INT_VAR min_val max_val step par1=132 header_lvl STR_VAR spell_list_2DA icon=~BARD0~ END
		END		
		SAY NAME1 @1131
		SAY UNIDENTIFIED_DESC @1133
		
		
		
	//eff files for kit masks 
	
	COPY ~3ed/BardSongs/GVSONG.EFF~ ~override/GVBSNG.EFF~
		WRITE_ASCII 0x0030 ~BSNG~ #8
	COPY ~3ed/BardSongs/GVSONG.EFF~ ~override/GVSSNG.EFF~
		WRITE_ASCII 0x0030 ~SSNG~ #8
	COPY ~3ed/BardSongs/GVSONG.EFF~ ~override/GVJSNG.EFF~
		WRITE_ASCII 0x0030 ~JSNG~ #8
		
	COPY ~3ed/BardSongs/BSNG.SPL~ ~override/GVSNG.SPL~
		FOR (i=1;i<=20;i=i+1) BEGIN
			PATCH_IF (i==1 OR ((i/2) * 2 = i)) BEGIN
				LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 required_level=i speed=1 projectile=1 END
				READ_SHORT 0x68 n_headers //number of extended_headers
				FOR (j=1;j<=i;j=j+1) BEGIN
					PATCH_IF (j==1 OR ((j/2) * 2 = j)) BEGIN
						LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=177 power=10 target=2 timing=0 duration=1 parameter2=9 parameter1=16384 STR_VAR resource=~GVBSNG~ END //bard				
					END	
					PATCH_IF (j==1 OR ((j/4) * 4 = j)) BEGIN				
						LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=177 power=10 target=2 timing=0 duration=1 parameter2=9 parameter1=16398 STR_VAR resource=~GVJSNG~ END //jester
						LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=177 power=10 target=2 timing=0 duration=1 parameter2=9 parameter1=16397 STR_VAR resource=~GVSSNG~ END //blade	
						LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=177 power=10 target=2 timing=0 duration=1 parameter2=9 parameter1=16399 STR_VAR resource=~GVSSNG~ END //skald				
					END	
				END
			END	
		END
		
	//bonus songs
	COPY ~3ed/BardSongs/BSNG.SPL~ ~override/GVSNGA.SPL~
		LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
			    LPF ADD_SPELL_EFFECT INT_VAR opcode=177 power=10 target=2 timing=0 duration=1 parameter2=9 parameter1=16384 STR_VAR resource=~GVBSNG~ END //bard
				LPF ADD_SPELL_EFFECT INT_VAR opcode=177 power=10 target=2 timing=0 duration=1 parameter2=9 parameter1=16398 STR_VAR resource=~GVJSNG~ END //jester
                LPF ADD_SPELL_EFFECT INT_VAR opcode=177 power=10 target=2 timing=0 duration=1 parameter2=9 parameter1=16397 STR_VAR resource=~GVSSNG~ END //blade	
                LPF ADD_SPELL_EFFECT INT_VAR opcode=177 power=10 target=2 timing=0 duration=1 parameter2=9 parameter1=16399 STR_VAR resource=~GVSSNG~ END //skald
				
	COPY ~3ed/BardSongs/BSNG.SPL~ ~override/BNSNG.SPL~
		LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
		FOR (i=min_val+step;i<=max_val;i=i+step) BEGIN
			LPF ADD_SPELL_EFFECT INT_VAR opcode=326 power=10 target=2 timing=0 duration=1 parameter2=132 parameter1=i STR_VAR resource=~GVSNGA~ END //apply effect
		END
	//remove song	
	COPY ~3ed/BardSongs/BSNG.SPL~ ~override/RMSNG.SPL~
		LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
			LPF ADD_SPELL_EFFECT INT_VAR opcode=172 power=10 target=2 timing=0 duration=1 STR_VAR resource=~BSNG~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=172 power=10 target=2 timing=0 duration=1 STR_VAR resource=~SSNG~ END
			LPF ADD_SPELL_EFFECT INT_VAR opcode=172 power=10 target=2 timing=0 duration=1 STR_VAR resource=~JSNG~ END

	
	//script for giving bardic songs
	OUTER_FOR (player_id=1;player_id<=6;player_id=player_id + 1) BEGIN
		EXTEND_TOP ~BALDUR.BCS~ ~3ed/BardSongs/BSNG.baf~
			EVALUATE_BUFFER			
	END
	//bonus songs at level 1 
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=4 add_at_level1=1 
						STR_VAR clab=~CLABBA+.*\.2DA~ mask_file=~~ caption=~BNSNG~ END		
	//bards
	LAF ADD_BONUS_FEATS INT_VAR min_level=2 max_level=20 d_level=2 add_at_level1=1 
						STR_VAR clab=~CLABBA01\.2DA~ mask_file=~~ caption=~GVSNGA~ END	
						
	//bard kits
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=20 d_level=4 add_at_level1=1 
						STR_VAR clab=~\(CLABBA02\)\|\(\CLABBA03\)\|\(CLABBA04\)\.2DA~ mask_file=~~ caption=~GVSNGA~ END	
						
	COPY ~3ed/BardSongs/BSNG.SPL~ ~override/DSBLSNG.SPL~					
		LPF ADD_SPELL_HEADER INT_VAR  type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
		LPF ADD_SPELL_EFFECT INT_VAR header=1 opcode=144 target=2 parameter1=0 parameter2=10 timing=1 duration=1 END //disable bardic music button
		
	LAF ADD_BONUS_FEATS INT_VAR min_level=1 max_level=1 d_level=4 add_at_level1=1 
						STR_VAR clab=~CLABBA+.*\.2DA~ mask_file=~~ caption=~DSBLSNG~ END	
////////////////////////////////////////////////////////////////////////////////////////
/////////// assassin spellcasting
BEGIN @1200

	INCLUDE ~3ed/INCREMENT_SPELL_DC.tph~
	INCLUDE ~3ed/ADD_SPELL_HEADER.tph~
	INCLUDE ~3ed/CREATE_PSEUDO_SPELL_SELECTION.tph~
	INCLUDE ~3ed/CHANGE_SPELL_PROPERTIES.tph~
	INCLUDE ~3ed/ADD_BONUS_FEATS.tph~
	
	OUTER_SET min_val=10
	OUTER_SET max_val=25
	OUTER_SET step=2
	OUTER_SET par1=128 //intelligence >=
	
	COPY ~3ed/Classes/Assassin/Spells/SPELLMAP.2DA~ ~override~
		COUNT_2DA_ROWS 3 n_rows
		FOR (i=0;i<n_rows;i=i+1) BEGIN
			READ_2DA_ENTRY i 1 3 new_spell_name 
			READ_2DA_ENTRY i 2 3 spell_name 
			INNER_ACTION BEGIN //create innate copies of spells
				COPY_EXISTING ~%spell_name%.SPL~ ~override/%new_spell_name%.SPL~
					LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_type=4 END			
			END
		END
		
	COPY ~3ed/Classes/Assassin/Spells/Bam~ ~override~
	COPY ~3ed/Classes/Assassin/Spells/SpellList~ ~override~
	OUTER_FOR (i=1;i<=4;i=i+1) BEGIN
		COPY ~3ed/Classes/Assassin/Spells/ASN.SPL~ ~override/ASNL%i%.SPL~
			SPRINT spell_list_2DA EVALUATE_BUFFER ~ASN0%i%~
			SPRINT icon EVALUATE_BUFFER ~ASN%i%~
			LPF CREATE_PSEUDO_SPELL_SELECTION INT_VAR min_val max_val step par1 header_lvl=i STR_VAR spell_list_2DA icon END	
			
			SET SpellName=1200+2*(i - 1)+1
			SET SpellDescription=1200+2*i
			SAY NAME1 (AT "SpellName")
			SAY UNIDENTIFIED_DESC (AT "SpellDescription")	
	END
	
	
	COPY ~3ed/Classes/Assassin/Spells/GVASN.SPL~ ~override/GVASN.SPL~
		FOR (i=1;i<=20;i=i+1) BEGIN
			LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 required_level=i speed=1 projectile=1 END
			READ_SHORT 0x68 n_headers //number of extended_headers
			
			FOR (j=4;j<=i;j=j+4) BEGIN //1st lvl
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource=~ASNL1~ END 				
			END	
			
			FOR (j=7;j<=i;j=j+4) BEGIN //2nd lvl
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource=~ASNL2~ END 				
			END	
			
			FOR (j=10;j<=i;j=j+4) BEGIN //3rd lvl
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource=~ASNL3~ END 				
			END	
			
			FOR (j=13;j<=i;j=j+4) BEGIN //4th lvl
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource=~ASNL4~ END 				
			END	
			
			PATCH_IF (i=20) BEGIN
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource=~ASNL4~ END
			END
			

		END
	
		OUTER_FOR (i=1;i<=4;i=i+1) BEGIN
			COPY ~3ed/Classes/Assassin/Spells/GVASN.SPL~ ~override/GVASN%i%.SPL~
				LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
				SPRINT resource EVALUATE_BUFFER ~ASNL%i%~
			    LPF ADD_SPELL_EFFECT INT_VAR opcode=171 power=10 target=2 timing=0 duration=1  STR_VAR resource END 				
		END		
		
		OUTER_SET id=1
	OUTER_FOR (k=12;k<=18;k=k+2) BEGIN	
		COPY ~3ed/Classes/Assassin/Spells/GVASN.SPL~ ~override/BNASN%id%.SPL~
			SPRINT resource EVALUATE_BUFFER ~GVASN%id%~
			LPF ADD_SPELL_HEADER  INT_VAR  required_level=1 type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
			FOR (i=k;i<=max_val;i=i+8) BEGIN			
				LPF ADD_SPELL_EFFECT INT_VAR opcode=326 power=10 target=2 timing=0 duration=1 parameter2=par1 parameter1=i STR_VAR resource END 
			END
		SET id=id+1
	END
		
								
	COPY ~3ed/Classes/Assassin/Spells/GVASN.SPL~ ~override/BNASN.SPL~
		//1st lvl
		LPF ADD_SPELL_HEADER  INT_VAR  required_level=1 type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=326 power=10 target=2 timing=0 duration=1 STR_VAR resource=~BNASN1~ END
		//2nd lvl
		LPF ADD_SPELL_HEADER  INT_VAR  copy_header=1 END
		LPF ALTER_SPELL_HEADER INT_VAR  header=2 min_level=7 END
		LPF ADD_SPELL_EFFECT INT_VAR header=2 opcode=326 power=10 target=2 timing=0 duration=1 STR_VAR resource=~BNASN2~ END		
		//3rd lvl
		LPF ADD_SPELL_HEADER  INT_VAR  copy_header=2 END
		LPF ALTER_SPELL_HEADER INT_VAR  header=3 min_level=10 END
		LPF ADD_SPELL_EFFECT INT_VAR header=3 opcode=326 power=10 target=2 timing=0 duration=1 STR_VAR resource=~BNASN3~ END	
		//4th lvl
		LPF ADD_SPELL_HEADER  INT_VAR  copy_header=3 END
		LPF ALTER_SPELL_HEADER INT_VAR  header=4 min_level=13 END
		LPF ADD_SPELL_EFFECT INT_VAR header=4 opcode=326 power=10 target=2 timing=0 duration=1 STR_VAR resource=~BNASN4~ END	
			
	//remove song	
	COPY ~3ed/Classes/Assassin/Spells/ASN.SPL~ ~override/RMASN.SPL~
		LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
			LPF ADD_SPELL_EFFECT INT_VAR opcode=172 power=10 target=2 timing=0 duration=1 STR_VAR resource=~ASNL1~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=172 power=10 target=2 timing=0 duration=1 STR_VAR resource=~ASNL2~ END 
			LPF ADD_SPELL_EFFECT INT_VAR opcode=172 power=10 target=2 timing=0 duration=1 STR_VAR resource=~ASNL3~ END
			LPF ADD_SPELL_EFFECT INT_VAR opcode=172 power=10 target=2 timing=0 duration=1 STR_VAR resource=~ASNL4~ END 			



	//script for giving assasin spells
	OUTER_FOR (player_id=1;player_id<=6;player_id=player_id + 1) BEGIN
		EXTEND_TOP ~BALDUR.BCS~ ~3ed/Classes/Assassin/Spells/ASN.baf~
			EVALUATE_BUFFER			
	END

	//give spells at lvl ups
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=16 d_level=4 add_at_level1=0 
						STR_VAR clab=~CLABTH02\.2DA~ mask_file=~~ caption=~GVASN1~ END	
	LAF ADD_BONUS_FEATS INT_VAR min_level=4 max_level=4 d_level=4 add_at_level1=0 
						STR_VAR clab=~CLABTH02\.2DA~ mask_file=~~ caption=~BNASN1~ END
						
	LAF ADD_BONUS_FEATS INT_VAR min_level=7 max_level=19 d_level=4 add_at_level1=0 
						STR_VAR clab=~CLABTH02\.2DA~ mask_file=~~ caption=~GVASN2~ END	
	LAF ADD_BONUS_FEATS INT_VAR min_level=7 max_level=7 d_level=4 add_at_level1=0 
						STR_VAR clab=~CLABTH02\.2DA~ mask_file=~~ caption=~BNASN2~ END
						
	LAF ADD_BONUS_FEATS INT_VAR min_level=10 max_level=18 d_level=4 add_at_level1=0 
						STR_VAR clab=~CLABTH02\.2DA~ mask_file=~~ caption=~GVASN3~ END	
	LAF ADD_BONUS_FEATS INT_VAR min_level=10 max_level=10 d_level=4 add_at_level1=0 
						STR_VAR clab=~CLABTH02\.2DA~ mask_file=~~ caption=~BNASN3~ END
						
	LAF ADD_BONUS_FEATS INT_VAR mask=0b10010001000000000000 
						STR_VAR clab=~CLABTH02\.2DA~ mask_file=~~ caption=~GVASN4~ END
	LAF ADD_BONUS_FEATS INT_VAR min_level=13 max_level=13 d_level=4 add_at_level1=0 
						STR_VAR clab=~CLABTH02\.2DA~ mask_file=~~ caption=~BNASN4~ END					
	
////////////////////////////////////////////////////////////////////////////////////////
//
//	lvl up by one level
BEGIN @1300

	OUTER_SET reset_xp_mode=3
	
	COPY ~3ed/lvlUp1/SETXP0.SPL~ ~override~
	COPY ~3ed/lvlUp1/RESETXP.SPL~ ~override/RSTXP0.SPL~
		SPRINT resource EVALUATE_BUFFER ~SETXP0~
		LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 timing=0 duration=1 STR_VAR resource END
		
	//get informartion about xp	
	COPY ~3ed/Core/Xp/XPLEVEL.2DA~ ~3ed/Core/Xp/XPLEVEL1.2DA~
	COUNT_2DA_COLS n_cols
	FOR (i=2;i<n_cols;i=i+1) BEGIN
		READ_2DA_ENTRY 1 %i% %n_cols% 	xp_needed //xp for single class
		SET lvl=i - 1
		SET xp_needed2=2*xp_needed //xp for double multiclas
		SET xp_needed3=3*xp_needed //xp for triple multiclass
		DEFINE_ASSOCIATIVE_ARRAY XP_TABLE1 BEGIN "%lvl%" => "%xp_needed%" END
		DEFINE_ASSOCIATIVE_ARRAY XP_TABLE2 BEGIN "%lvl%" => "%xp_needed2%" END	
		DEFINE_ASSOCIATIVE_ARRAY XP_TABLE3 BEGIN "%lvl%" => "%xp_needed3%" END
	END
	

	OUTER_SET k=0 //counter for set xp ids
	
	//adding scripts for every case including single class (and possible double or triple multi)
 	ACTION_PHP_EACH XP_TABLE1 AS lvl1 => xp1 BEGIN
	
		COPY ~3ed/lvlUp1/RESETXP.SPL~ ~override/RSTXP%lvl1%.SPL~//create resets
		
		OUTER_SET xp_max = xp1
		OUTER_SET lvl_max = lvl1
		OUTER_SET k = k + 1
		OUTER_SET lvl2_found=0
		OUTER_SET lvl3_found=0
		ACTION_PHP_EACH XP_TABLE2 AS lvl2 => xp2 BEGIN
			ACTION_IF (xp1=xp2) BEGIN
				OUTER_SET lvl2_found=lvl2
			END
		END
		ACTION_PHP_EACH XP_TABLE3 AS lvl3 => xp3 BEGIN
			ACTION_IF (xp1=xp3) BEGIN
				OUTER_SET lvl3_found=lvl3
			END
		END
		OUTER_SPRINT lvlup_block ~~
		//OUTER_SPRINT object ~Player1~
		
		ACTION_IF (lvl2_found AND lvl3_found) BEGIN
		
			OUTER_SPRINT lvlup_block EVALUATE_BUFFER~
IF
	CheckStatGT(%object%,%xp1%,XP)
	OR(3)
		CheckStatGT(%object%,%lvl1%,LEVEL)
		!CheckStat(%object%,0,LEVEL2)
		!CheckStat(%object%,0,LEVEL3)
	OR(3)
		CheckStat(%object%,%lvl2_found%,LEVEL2)
		CheckStat(%object%,0,LEVEL2)
		!CheckStat(%object%,0,LEVEL3)	
	OR(2)
		CheckStat(%object%,%lvl3_found%,LEVEL3)
		CheckStat(%object%,0,LEVEL3)	
THEN
	RESPONSE #100
		ApplySpellRES("SETXP%k%",%object%)
		Continue()
END~		
			
		END 
		ELSE ACTION_IF (lvl2_found) BEGIN
		
			OUTER_SPRINT lvlup_block EVALUATE_BUFFER~
IF
	CheckStatGT(%object%,%xp1%,XP)
	CheckStat(%object%,0,LEVEL3)
	OR(2)
		CheckStat(%object%,%lvl1%,LEVEL)
		!CheckStat(%object%,0,LEVEL2)
	OR(2)
		CheckStat(%object%,%lvl2_found%,LEVEL2)
		CheckStat(%object%,0,LEVEL2)	
THEN
	RESPONSE #100
		ApplySpellRES("SETXP%k%",%object%)
		Continue()
END~				

		END
		ELSE BEGIN
		
			OUTER_SPRINT lvlup_block EVALUATE_BUFFER~
IF
	CheckStatGT(%object%,%xp1%,XP)
	CheckStat(%object%,0,LEVEL3)
	CheckStat(%object%,0,LEVEL2)
	CheckStat(%object%,%lvl1%,LEVEL)
THEN
	RESPONSE #100
		ApplySpellRES("SETXP%k%",%object%)
		Continue()
END~			

	
		END
		

		OUTER_SPRINT lvlup_block EVALUATE_BUFFER 
		~%lvlup_block%
|lvlup_block|~
	
		COPY ~3ed/lvlUp1/LvlUp.BAF~  ~3ed/lvlUp1/LvlUp.BAF~  									
		EVALUATE_BUFFER	
		REPLACE_TEXTUALLY ~|~ ~%~
		
	//adding new setxp spell
		COPY ~3ed/lvlUp1/SETXP0.SPL~ ~override/SETXP%k%.SPL~
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=104 parameter1=xp1 END
			
			
			

		OUTER_SPRINT resource EVALUATE_BUFFER ~SETXP%k%~
		COPY_EXISTING ~override/RSTXP%lvl1%.SPL~ ~override~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 timing=reset_xp_mode duration=1 STR_VAR resource END
		ACTION_IF (lvl2_found) BEGIN
			COPY_EXISTING ~override/RSTXP%lvl2_found%.SPL~ ~override~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 timing=reset_xp_mode duration=1 STR_VAR resource END
		END
		ACTION_IF (lvl3_found) BEGIN
			COPY_EXISTING ~override/RSTXP%lvl3_found%.SPL~ ~override~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 timing=reset_xp_mode duration=1 STR_VAR resource END
		END
					
	END

	
	//adding scripts for every case including double class only
	ACTION_PHP_EACH XP_TABLE2 AS lvl2 => xp2 BEGIN
		OUTER_SET lvl1_found=0
		ACTION_PHP_EACH XP_TABLE1 AS lvl1 => xp1 BEGIN
			ACTION_IF (xp1=xp2) BEGIN
				OUTER_SET lvl1_found=1
			END
		END
		
		ACTION_IF ((NOT lvl1_found) AND xp2<=xp_max) BEGIN
			OUTER_SET k=k+1

			OUTER_SPRINT lvlup_block EVALUATE_BUFFER~
IF
	CheckStatGT(%object%,%xp2%,XP)
	CheckStat(%object%,0,LEVEL3)
	CheckStat(%object%,%lvl2%,LEVEL2)
THEN
	RESPONSE #100
		ApplySpellRES("SETXP%k%",%object%)
		Continue()
END
|lvlup_block|~			
	
		COPY ~3ed/lvlUp1/LvlUp.BAF~  ~3ed/lvlUp1/LvlUp.BAF~  									
		EVALUATE_BUFFER	
		REPLACE_TEXTUALLY ~|~ ~%~
		
	//adding new setxp spell
		COPY ~3ed/lvlUp1/SETXP0.SPL~ ~override/SETXP%k%.SPL~
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=104 parameter1=xp2 END
	//adding it to reset
		OUTER_SPRINT resource EVALUATE_BUFFER ~SETXP%k%~
		COPY_EXISTING ~override/RSTXP%lvl2%.SPL~ ~override~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 timing=reset_xp_mode duration=reset_xp_mode STR_VAR resource END
			
		END
		
		
		
	END
	
	//adding scripts for every case including triple class only
	ACTION_PHP_EACH XP_TABLE3 AS lvl3 => xp3 BEGIN
		OUTER_SET lvl1_found=0
		ACTION_PHP_EACH XP_TABLE1 AS lvl1 => xp1 BEGIN
			ACTION_IF (xp1=xp3) BEGIN
				OUTER_SET lvl1_found=1
			END
		END
		
		ACTION_IF ((NOT lvl1_found) AND xp3<=xp_max) BEGIN
			OUTER_SET k=k+1

			
			OUTER_SPRINT lvlup_block EVALUATE_BUFFER~
IF
	CheckStatGT(%object%,%xp3%,XP)
	CheckStat(%object%,%lvl3%,LEVEL3)
THEN
	RESPONSE #100
		ApplySpellRES("SETXP%k%",%object%)
		Continue()
END
|lvlup_block|~			
	
		COPY ~3ed/lvlUp1/LvlUp.BAF~  ~3ed/lvlUp1/LvlUp.BAF~  									
		EVALUATE_BUFFER	
		REPLACE_TEXTUALLY ~|~ ~%~
		
	//adding new setxp spell
		COPY ~3ed/lvlUp1/SETXP0.SPL~ ~override/SETXP%k%.SPL~
			LPF ALTER_SPELL_EFFECT INT_VAR match_opcode=104 parameter1=xp3 END
	//adding it to reset
		OUTER_SPRINT resource EVALUATE_BUFFER ~SETXP%k%~
		COPY_EXISTING ~override/RSTXP%lvl3%.SPL~ ~override~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 timing=reset_xp_mode duration=1 STR_VAR resource END
		
		
		
		END	
	END
	
	//adding lvl1 xp limiter remover to clabs
	OUTER_SPRINT clab_line ~LVLUPABIL~
	
	OUTER_FOR (i=0;i<=lvl_max;i=i+1) BEGIN
		OUTER_SPRINT clab_line EVALUATE_BUFFER ~%clab_line%  AP_RSTXP%i%~
	END
	
	COPY_EXISTING_REGEXP GLOB ~\(CLAB+.\)\|\(OHTYR\)*\.2DA~ ~override~
		COUNT_2DA_ROWS 20 "nrows"
		INSERT_2DA_ROW nrows 20 ~%clab_line%~
	 

	
	 

	COPY ~3ed/lvlUp1/LvlUp.BAF~  ~3ed/lvlUp1/LvlUp.BAF~  	
	REPLACE_TEXTUALLY ~%~	~|~
	REPLACE_TEXTUALLY ~|lvlup_block|~ ~~
	REPLACE_TEXTUALLY ~|~	~%~
	
	OUTER_FOR (i=1;i<=6;i=i+1) BEGIN
		EXTEND_TOP ~baldur.bcs~ ~3ed/lvlUp1/LvlUp.BAF~
			SPRINT object EVALUATE_BUFFER ~Player%i%~
			SET party_size= i - 1
			EVALUATE_BUFFER
	END
//////////////////////////////////////////////////////////////////////////////////////////////
//	more powerfull monsters
BEGIN @1400

	COPY  ~3ed/EnemyScripts~ ~override~
	
	//make monsters more powerfull
	//+2 thaco +1/5 levels, +2 ac +1/5 levels, 150% hp, +1/2 APR per 5 levels
	
	 COPY_EXISTING_REGEXP GLOB ~.+\.CRE~ ~override~
		READ_BYTE 0x0234 level
		READ_BYTE 0x0270 allegience 
		SET THAC0_BONUS=2+level/5
		SET AC_BONUS=2+level/5
		SET APR_BONUS=0
		SET HP_BONUS=150 //in percent
		
		PATCH_IF (allegience=0 OR allegience>5) BEGIN //if not summoned
			PATCH_IF (level>5 AND level<10) BEGIN
				SET APR_BONUS=6 
			END
			ELSE PATCH_IF (level>10 AND level<15) BEGIN
				SET APR_BONUS=1
			END
			ELSE PATCH_IF (level>15 AND level<20) BEGIN
				SET APR_BONUS=7
			END
			ELSE PATCH_IF (level>20) BEGIN
				SET APR_BONUS=2
			END

		
			LPF ADD_CRE_EFFECT INT_VAR opcode=0 timing=1 parameter1=AC_BONUS duration=1 END
			LPF ADD_CRE_EFFECT INT_VAR opcode=278 timing=1 parameter1=THAC0_BONUS duration=1 END
			LPF ADD_CRE_EFFECT INT_VAR opcode=1 timing=1 parameter1=APR_BONUS duration=1 END
			LPF ADD_CRE_EFFECT INT_VAR opcode=18 timing=1 parameter1=HP_BONUS parameter2=2 duration=1 END
			LPF ADD_CRE_EFFECT INT_VAR opcode=17 timing=1 parameter1=HP_BONUS parameter2=2 duration=1 END
		END
		
		//spell dc bonus based on stats
		LPF ADD_CRE_EFFECT INT_VAR opcode=272 timing=9 parameter1=1 parameter2=3 duration=1 STR_VAR resource=~SPDCWBN~ END //int 
		LPF ADD_CRE_EFFECT INT_VAR opcode=272 timing=9 parameter1=1 parameter2=3 duration=1 STR_VAR resource=~SPDCPBN~ END //wis 
		//saves bonus based on stats
		LPF ADD_CRE_EFFECT INT_VAR opcode=272 timing=9 parameter1=1 parameter2=3 duration=1 STR_VAR resource=~DEXSAVBN~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=272 timing=9 parameter1=1 parameter2=3 duration=1 STR_VAR resource=~CONSAVBN~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode=272 timing=9 parameter1=1 parameter2=3 duration=1 STR_VAR resource=~WISSAVBN~ END

		
	
	COPY_EXISTING ~Lendar.CRE~ ~override~
		WRITE_ASCII 0x0250 ~LENDARN~ #8
		WRITE_ASCII 0x0258 ~MAGE4~ #8
		
		
	COPY_EXISTING ~Natash.CRE~ ~override~
		WRITE_ASCII 0x0250 ~NATASH~ #8
		WRITE_ASCII 0x0258 ~MAGE5~ #8
		
	COPY_EXISTING ~Hareis.CRE~ ~override~
		WRITE_ASCII 0x0250 ~HAREIS~ #8
		WRITE_ASCII 0x0258 ~MAGE4~ #8
		
	COPY_EXISTING ~OGREMA03.CRE~ ~override~
		WRITE_ASCII 0x0250 ~OGREMA03~ #8


BEGIN @1500		//point buy system
	
	//copying lua files for interface 
	COPY ~3ed/LUA/UI.menu~ ~override~
		
BEGIN @1600		
	//update npc stats and set their lvl to 0
	COPY  ~3ed/Npc~ ~override~
	
	//qualye dialog change
	STRING_SET 126  @1601 





	

