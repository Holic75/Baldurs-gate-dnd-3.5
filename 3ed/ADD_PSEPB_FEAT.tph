
DEFINE_ACTION_FUNCTION ~ADD_PSEPB_FEAT~ // persistent exclusive progressive bonus feat
	INT_VAR
		min_val=1
		max_val=25
		step=2
		abi_g=122 //ability>= code 
		min_bonus_val=0
		bonus_step=100
	STR_VAR
		ability_name=~dummy~
		target_name=~~
	BEGIN
	
		ACTION_IF (~%target_name%~ STRING_EQUAL ~~) BEGIN
			OUTER_SPRINT target_name EVALUATE_BUFFER ~%ability_name%~		
		END
		

		OUTER_SET k=0	
		//create bonuses
		OUTER_FOR (i=min_val;i<=max_val;i=i+step) BEGIN
            
			OUTER_SPRINT resource EVALUATE_BUFFER ~%target_name%%i%~		
			COPY ~3ed/Feats/PersistentAbilities/StatExclusiveProgressiveBonus/%ability_name%/%ability_name%.SPL~  ~override\%target_name%%i%.spl~
				LPF ALTER_SPELL_EFFECT INT_VAR timing = 9 parameter1 = min_bonus_val + k*bonus_step power = 0 END //full cumulative bonus
				LPF ADD_SPELL_EFFECT INT_VAR opcode=206 target=2 timing=9 duration=1 parameter1=0 parameter2=0 STR_VAR resource END //add protection from itself
                LPF ADD_SPELL_EFFECT INT_VAR opcode=318 target=2 parameter1=i+step duration=1 parameter2=abi_g insert_point = 0 STR_VAR resource END //add protection from itself
            OUTER_SET k=k+1
								
		END
		
		//apply bonus (in reverse order)	
		COPY ~3ed/Feats/PersistentAbilities/StatExclusiveProgressiveBonus/Template/PSEPBBN.SPL~ ~override\%target_name%BN.spl~
			
			FOR (i=min_val;i<=max_val;i=i+step) BEGIN	
				//SPRINT resource EVALUATE_BUFFER ~%target_name%BN~					
				//LPF ADD_SPELL_EFFECT INT_VAR opcode=318 target=2 timing=0 duration=0 parameter1=i parameter2=abi_g insert_point = 0 STR_VAR resource END //add protection from itself if stat>=	
				SPRINT resource EVALUATE_BUFFER ~%target_name%%i%~
				LPF ADD_SPELL_EFFECT INT_VAR opcode=326 target=2 duration=1  parameter1=i parameter2=abi_g insert_point = 0 STR_VAR resource END 	 //stat>=	
			END
		
		//apply removals in reverse order
		COPY ~3ed/Feats/PersistentAbilities/StatExclusiveProgressiveBonus/Template/PSEPBBN.SPL~ ~override\%target_name%RM.spl~
		FOR (i=min_val;i<=max_val;i=i+step) BEGIN
			SPRINT resource EVALUATE_BUFFER ~%target_name%%i%~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=321 target=2 timing=0 duration=1 insert_point = 0 STR_VAR resource END //remove effects
			SPRINT resource EVALUATE_BUFFER ~%target_name%RM~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=318 target=2 timing=0 duration=1 parameter1=i parameter2=abi_g insert_point = 0 STR_VAR resource END //add protection from itself if stat>=						
		END		
			
		OUTER_SPRINT resource EVALUATE_BUFFER ~%target_name%BN~
		COPY ~3ed/Feats/PersistentAbilities/StatExclusiveProgressiveBonus/Template/PSEPBBN.EFF~ ~override\%target_name%BN.EFF~
			WRITE_EVALUATED_ASCII 0x0030 ~%resource%~ #8
		
		OUTER_SPRINT resource EVALUATE_BUFFER ~%target_name%RM~
		COPY ~3ed/Feats/PersistentAbilities/StatExclusiveProgressiveBonus/Template/PSEPBBN.EFF~ ~override\%target_name%RM.EFF~
			WRITE_EVALUATED_ASCII 0x0030 ~%resource%~ #8
			
		COPY ~3ed/Feats/PersistentAbilities/StatExclusiveProgressiveBonus/Template/PSEPBFT.SPL~ ~override\%target_name%FT.SPL~
			SPRINT resource EVALUATE_BUFFER ~%target_name%BN~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=326  target = 2 timing = 0 duration = 1 STR_VAR resource END // initial bonus application
			LPF ADD_SPELL_EFFECT INT_VAR opcode=272  target = 2 parameter1 = 1 parameter2 = 3 timing = 9 duration = 1 STR_VAR resource END //apply bonus every n seconds	
			
			SPRINT resource EVALUATE_BUFFER ~%target_name%RM~
			LPF ADD_SPELL_EFFECT INT_VAR opcode=272  target = 2 parameter1 = 1 parameter2 = 3 timing = 9 duration = 1 STR_VAR resource END //check removing every n seconds	
		
	END
