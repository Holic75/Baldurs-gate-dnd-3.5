DEFINE_ACTION_FUNCTION ~CREATE_MINOR_SPELLCASTING~
    
    INT_VAR
        min_val=10
        max_val=25
        step=2
        par1=128 //parameter for governing ability>=
        LabelStart = 1200
    STR_VAR
        SpellmapFile = ~3ed/Classes/Assassin/Spells/SPELLMAP.2DA~
        SpellPrefix = ~ASN~ //3 characters max
        clab = ~CLABTH02\.2DA~
        
    BEGIN
	
	COPY ~%SpellmapFile%~ ~override~
		COUNT_2DA_ROWS 3 n_rows
		FOR (i=0;i<n_rows;i=i+1) BEGIN
			READ_2DA_ENTRY i 1 3 new_spell_name 
			READ_2DA_ENTRY i 2 3 spell_name 
			INNER_ACTION BEGIN //create innate copies of spells
				COPY_EXISTING ~%spell_name%.SPL~ ~override/%new_spell_name%.SPL~
					LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_type=4 END			
			END
		END
		

	OUTER_FOR (i=1;i<=4;i=i+1) BEGIN
		COPY ~3ed/Spells/MinorSpellcasting/Templates/CAST.SPL~ ~override/%SpellPrefix%L%i%.SPL~
			SPRINT spell_list_2DA EVALUATE_BUFFER ~%SpellPrefix%0%i%~
			SPRINT icon EVALUATE_BUFFER ~MNSPLV%i%~
			LPF CREATE_PSEUDO_SPELL_SELECTION INT_VAR min_val max_val step par1 header_lvl=i STR_VAR spell_list_2DA icon END	
			
			SET SpellName=LabelStart+2*(i - 1)+1
			SET SpellDescription=LabelStart+2*i
			SAY NAME1 (AT "SpellName")
			SAY UNIDENTIFIED_DESC (AT "SpellDescription")	
	END
	
	
	COPY ~3ed/Spells/MinorSpellcasting/Templates/GIVE.SPL~ ~override/GV%SpellPrefix%.SPL~
		FOR (i=1;i<=20;i=i+1) BEGIN
			LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 required_level=i speed=1 projectile=1 END
			READ_SHORT 0x68 n_headers //number of extended_headers
			
			FOR (j=4;j<=i;j=j+4) BEGIN //1st lvl
                SPRINT resource EVALUATE_BUFFER ~%SpellPrefix%L1~
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource END 				
			END	
			
			FOR (j=7;j<=i;j=j+4) BEGIN //2nd lvl
                SPRINT resource EVALUATE_BUFFER ~%SpellPrefix%L2~
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource END 				
			END	
			
			FOR (j=10;j<=i;j=j+4) BEGIN //3rd lvl
                SPRINT resource EVALUATE_BUFFER ~%SpellPrefix%L3~
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource END 				
			END	
			
			FOR (j=13;j<=i;j=j+4) BEGIN //4th lvl
                SPRINT resource EVALUATE_BUFFER ~%SpellPrefix%L4~
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource END 				
			END	
			
			PATCH_IF (i=20) BEGIN
                SPRINT resource EVALUATE_BUFFER ~%SpellPrefix%L4~
				LPF ADD_SPELL_EFFECT INT_VAR header=n_headers opcode=171 power=10 target=2 timing=0 duration=1 STR_VAR resource END
			END
			

		END
	
		OUTER_FOR (i=1;i<=4;i=i+1) BEGIN
			COPY ~3ed/Spells/MinorSpellcasting/Templates/GIVE.SPL~ ~override/GV%SpellPrefix%%i%.SPL~
				LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
				SPRINT resource EVALUATE_BUFFER ~%SpellPrefix%L%i%~
			    LPF ADD_SPELL_EFFECT INT_VAR opcode=171 power=10 target=2 timing=0 duration=1  STR_VAR resource END 				
		END		
		
		OUTER_SET id=1
	OUTER_FOR (k=11;k<=17;k=k+2) BEGIN	
		COPY ~3ed/Spells/MinorSpellcasting/Templates/GIVE.SPL~ ~override/BN%SpellPrefix%%id%.SPL~
			SPRINT resource EVALUATE_BUFFER ~GV%SpellPrefix%%id%~
			LPF ADD_SPELL_HEADER  INT_VAR  required_level=1 type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
			FOR (i=k;i<=max_val;i=i+8) BEGIN			
				LPF ADD_SPELL_EFFECT INT_VAR opcode=326 power=10 target=2 timing=0 duration=1 parameter2=par1 parameter1=i STR_VAR resource END 
			END
		SET id=id+1
	END
		
								
	COPY ~3ed/Spells/MinorSpellcasting/Templates/GIVE.SPL~ ~override/BN%SpellPrefix%.SPL~
		//1st lvl
        SPRINT resource EVALUATE_BUFFER ~BN%SpellPrefix%1~
		LPF ADD_SPELL_HEADER  INT_VAR  required_level=1 type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode=326 power=10 target=2 timing=0 duration=1 STR_VAR resource END
		//2nd lvl
        SPRINT resource EVALUATE_BUFFER ~BN%SpellPrefix%2~
		LPF ADD_SPELL_HEADER  INT_VAR  copy_header=1 END
		LPF ALTER_SPELL_HEADER INT_VAR  header=2 min_level=7 END
		LPF ADD_SPELL_EFFECT INT_VAR header=2 opcode=326 power=10 target=2 timing=0 duration=1 STR_VAR resource END		
		//3rd lvl
        SPRINT resource EVALUATE_BUFFER ~BN%SpellPrefix%3~
		LPF ADD_SPELL_HEADER  INT_VAR  copy_header=2 END
		LPF ALTER_SPELL_HEADER INT_VAR  header=3 min_level=10 END
		LPF ADD_SPELL_EFFECT INT_VAR header=3 opcode=326 power=10 target=2 timing=0 duration=1 STR_VAR resource END	
		//4th lvl
        SPRINT resource EVALUATE_BUFFER ~BN%SpellPrefix%4~
		LPF ADD_SPELL_HEADER  INT_VAR  copy_header=3 END
		LPF ALTER_SPELL_HEADER INT_VAR  header=4 min_level=13 END
		LPF ADD_SPELL_EFFECT INT_VAR header=4 opcode=326 power=10 target=2 timing=0 duration=1 STR_VAR resource END	
			
		
	COPY ~3ed/Spells/MinorSpellcasting/Templates/CAST.SPL~ ~override/RM%SpellPrefix%.SPL~
		LPF ADD_SPELL_HEADER  INT_VAR  type=1 location=4 target=5 target_count=0 range=1 speed=1 projectile=1 END
            FOR (i=1;i<=4;i=i+1) BEGIN
                SPRINT resource EVALUATE_BUFFER ~%SpellPrefix%L%i%~
                LPF ADD_SPELL_EFFECT INT_VAR opcode=172 power=10 target=2 timing=0 duration=1 STR_VAR resource END 
            END
			



	//give spells at lvl ups
    OUTER_SPRINT feat_name EVALUATE_BUFFER ~GV%SpellPrefix%1~
    OUTER_SPRINT caption EVALUATE_BUFFER ~SP%SpellPrefix%1~
	LAF ADD_FEATS_LVL INT_VAR min_level=4 max_level=16 d_level=4 add_at_level1=0
        STR_VAR clab mask_file=~3ed/Feats/FeatAttribution/SFTCREAL.SPL~ feat_name caption END	
    OUTER_SPRINT feat_name EVALUATE_BUFFER ~BN%SpellPrefix%1~
    OUTER_SPRINT caption EVALUATE_BUFFER ~SPB%SpellPrefix%1~ 
	LAF ADD_FEATS_LVL INT_VAR min_level=4 max_level=4 d_level=4 add_at_level1=0 
        STR_VAR clab mask_file=~3ed/Feats/FeatAttribution/SFTCREAL.SPL~ feat_name caption END
						
    OUTER_SPRINT feat_name EVALUATE_BUFFER ~GV%SpellPrefix%2~
    OUTER_SPRINT caption EVALUATE_BUFFER ~SP%SpellPrefix%2~                
	LAF ADD_FEATS_LVL INT_VAR min_level=7 max_level=19 d_level=4 add_at_level1=0
        STR_VAR clab mask_file=~3ed/Feats/FeatAttribution/SFTCREAL.SPL~ feat_name caption END	
    OUTER_SPRINT feat_name EVALUATE_BUFFER ~BN%SpellPrefix%2~ 
    OUTER_SPRINT caption EVALUATE_BUFFER ~SPB%SpellPrefix%2~     
	LAF ADD_FEATS_LVL INT_VAR min_level=7 max_level=7 d_level=4 add_at_level1=0 
        STR_VAR clab mask_file=~3ed/Feats/FeatAttribution/SFTCREAL.SPL~ feat_name caption END
	
    OUTER_SPRINT feat_name EVALUATE_BUFFER ~GV%SpellPrefix%3~
    OUTER_SPRINT caption EVALUATE_BUFFER ~SP%SpellPrefix%3~  
	LAF ADD_FEATS_LVL INT_VAR min_level=10 max_level=18 d_level=4 add_at_level1=0 
        STR_VAR clab mask_file=~3ed/Feats/FeatAttribution/SFTCREAL.SPL~ feat_name caption END END	
    OUTER_SPRINT feat_name EVALUATE_BUFFER ~BN%SpellPrefix%3~
    OUTER_SPRINT caption EVALUATE_BUFFER ~SPB%SpellPrefix%3~     
	LAF ADD_FEATS_LVL INT_VAR min_level=10 max_level=10 d_level=4 add_at_level1=0 
        STR_VAR clab mask_file=~3ed/Feats/FeatAttribution/SFTCREAL.SPL~ feat_name caption END
	
    OUTER_SPRINT feat_name EVALUATE_BUFFER ~GV%SpellPrefix%4~
    OUTER_SPRINT caption EVALUATE_BUFFER ~SP%SpellPrefix%4~   
	LAF ADD_FEATS_MASK INT_VAR mask=0b10010001000000000000 indexed = 1 
       STR_VAR clab mask_file=~3ed/Feats/FeatAttribution/SFTCREAL.SPL~ feat_name caption END END
    OUTER_SPRINT feat_name EVALUATE_BUFFER ~BN%SpellPrefix%4~   
    OUTER_SPRINT caption EVALUATE_BUFFER ~SPB%SpellPrefix%4~ 
	LAF ADD_FEATS_LVL INT_VAR min_level=13 max_level=13 d_level=4 add_at_level1=0 
        STR_VAR clab mask_file=~3ed/Feats/FeatAttribution/SFTCREAL.SPL~ feat_name caption END	
END
