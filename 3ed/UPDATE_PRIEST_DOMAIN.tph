DEFINE_ACTION_FUNCTION ~UPDATE_PRIEST_DOMAIN~
	INT_VAR
		domain_id=1
		yes_response=0 //strref to 'Choose this domain.' response
		no_response=0 //stref to 'Choose another domain.' response
	STR_VAR
		domain_file_2DA=~~  //file name (.2da is added as extension)	
	BEGIN

				
		COPY_EXISTING ~%domain_file_2DA%.2DA~ ~override~ //get list of spells 
			COUNT_2DA_ROWS 1 n_rows
			READ_2DA_ENTRY 0 0 1 domain_name_read //domain name (with _ instead of spaces)
			INNER_PATCH_SAVE domain_name ~%domain_name_read%~ BEGIN
				REPLACE_TEXTUALLY ~_~  ~ ~
            END    
            READ_2DA_ENTRY 1 0 1 DomainAbilityDescription_read //domain ability (with _ instead of spaces)
			INNER_PATCH_SAVE DomainAbilityDescription ~%DomainAbilityDescription_read%~ BEGIN
				REPLACE_TEXTUALLY ~_~  ~ ~
			END
			
			FOR (i=2;i<n_rows;i=i+1) BEGIN				
				READ_2DA_ENTRY %i% 0 1 spl_file //spell name without spl extension
				INNER_ACTION BEGIN
					
					OUTER_SPRINT spl_to_add EVALUATE_BUFFER ~%spl_file%~
					COPY_EXISTING  ~%spl_file%.SPL~ ~override~
						READ_SHORT 0x001c spell_type
						READ_STRREF 0x0008 spell_name
						READ_LONG 0x0034 spl_level
						
					ACTION_IF (spell_type!=2) BEGIN //if it is not a priest spell, make it's copy as priest spell and update description with domain description
                        OUTER_SET i1 = i - 1
						OUTER_SPRINT spl_to_add EVALUATE_BUFFER ~%domain_file_2DA%%i1%~
						COPY_EXISTING  ~%spl_file%.SPL~ ~override/%spl_to_add%.SPL~
							LPF CHANGE_SPELL_PROPERTIES INT_VAR spell_type=2 END 
							READ_STRREF 0x0050 descr_string
							
							INNER_PATCH_SAVE new_descr_string ~%descr_string%~ BEGIN
								REPLACE_TEXTUALLY ~Range:~ 
~Sphere: %domain_name%
Range:~
								REPLACE_TEXTUALLY CASE_SENSITIVE ~Wizard~ ~Priest~
								REPLACE_TEXTUALLY CASE_SENSITIVE ~wizard~ ~priest~
							END	
							SAY_EVALUATED 0x0050 ~%new_descr_string%~
			
					END
																				
				END
				DEFINE_ASSOCIATIVE_ARRAY spell_list BEGIN "%spl_to_add%", "%spl_level%" => "%spell_name%" END			
			END
			
		OUTER_SPRINT DescriptionString EVALUATE_BUFFER ~%domain_name% domain gives priest access to the following spells:~	
		//creating ability that would give priest a list of spells read previously and also filling description string	
		COPY ~3ed/Feats/LimitedUseAbilities/Template/LUAFT.SPL~  ~override/%domain_file_2DA%S.SPL~
			SET first_spell=1
			
			PHP_EACH spell_list AS spl_to_add => spl_name BEGIN
				SPRINT resource EVALUATE_BUFFER ~%spl_to_add_0%~
				LPF ADD_SPELL_EFFECT INT_VAR opcode=171 target=2 timing=9 duration=1 STR_VAR resource END			
								
				PATCH_IF (first_spell)	BEGIN
					first_spell=0
				END 
				ELSE BEGIN
					SPRINT DescriptionString EVALUATE_BUFFER ~%DescriptionString%,~
				END
				SPRINT DescriptionString EVALUATE_BUFFER ~%DescriptionString% %spl_name% (%spl_to_add_1%)~
			END
            
            SPRINT resource EVALUATE_BUFFER $DomainAbilities(~%domain_file_2DA%~)
            LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 2 timing = 9 duration = 1 STR_VAR resource END
		
		OUTER_SPRINT DescriptionString EVALUATE_BUFFER ~%DescriptionString%.
        
Domain ability: %DomainAbilityDescription%.~

        OUTER_SET DescriptionStringRef = RESOLVE_STR_REF (~%DescriptionString%~) 
					
		OUTER_SPRINT domain_str EVALUATE_BUFFER
			
~~~~~IF 
~!Global("DOM%domain_id%","LOCALS",1)~ THEN REPLY ~%domain_name%~ GOTO %domain_id%

|domain_str|~~~~~ 

		OUTER_SPRINT domain_description_str EVALUATE_BUFFER
			
~~~~~IF ~~ THEN BEGIN %domain_id%
	SAY #%DescriptionStringRef%
	IF ~!Global("CHOSEN","LOCALS",1)
Class(LastSummonerOf(Myself),CLERIC)~ THEN REPLY #%yes_response% DO 
	~ApplySpellRES("%domain_file_2DA%S",LastSummonerOf(Myself))
	 SetGlobal("CHOSEN","LOCALS",1)
	 SetGlobal("DOM%domain_id%","LOCALS",1)~ GOTO 1000
	IF ~Global("CHOSEN","LOCALS",1)~ THEN REPLY #%yes_response% DO
	~ApplySpellRES("%domain_file_2DA%S",LastSummonerOf(Myself))~ EXIT
	IF ~!Class(LastSummonerOf(Myself),CLERIC)~ THEN REPLY #%yes_response% DO
	~ApplySpellRES("%domain_file_2DA%S",LastSummonerOf(Myself))~ EXIT
	IF ~True()~ THEN REPLY #%no_response% GOTO 0
END	

|domain_description_str|~~~~~
	
		COPY_EXISTING ~domain.d~  ~override~  									
			EVALUATE_BUFFER	
			REPLACE_TEXTUALLY ~|~ ~%~
			
			
	
    END		
	

